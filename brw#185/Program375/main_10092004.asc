1 CLS:LOCATE 4,12
2 A$=" ************************************************"
3 B$=" **                                            **"
10 LOCATE ,12:PRINT A$
11 LOCATE ,12:PRINT A$
12 LOCATE ,12:PRINT B$
13 LOCATE ,12:PRINT" **                AES/SCI-TEC                 **
14 LOCATE ,12:PRINT" **  MKII/MKIII/MKIV Brewer Operating Program  **
15 LOCATE ,12:PRINT" **        Version 3.75c - Full Release        **
16 LOCATE ,12:PRINT" **              October 30, 2000              **
17 LOCATE ,12:PRINT B$
18 LOCATE ,12:PRINT B$
19 LOCATE ,12:PRINT" **     Originally written by  Dr. JB Kerr     **
20 LOCATE ,12:PRINT" ** Adapted for the IBM PC by  Dr. CT McElroy  **
21 LOCATE ,12:PRINT B$
22 LOCATE ,12:PRINT A$
23 LOCATE ,12:PRINT" ** Copyright(C) 1994 SCI-TEC Instruments Inc. **
25 LOCATE ,12:PRINT A$
30 '
31 ' *** Code works until 2050!! ***
32 '
40 :
41 BREWDIR$=ENVIRON$("BREWDIR"):IF BREWDIR$<>"" THEN 45
42   PRINT:PRINT SPC(12);"Missing environment variable BREWDIR"
43   PRINT SPC(25);"set BREWER=<path> to fix"
44   SYSTEM
45 X=1:GOSUB 7220
46 Q12%=0:A$=ENVIRON$("NOBREW"):IF A$<>"" THEN Q12%=1
47 CLS:GOTO 100
50 I=CSRLIN:J=POS(0)
51 LOCATE 12,45:PRINT "ÚÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ¿"
52 LOCATE ,45:PRINT USING"³ DS  O3 ######.# /######.# ³";CDS;MDS
54 LOCATE ,45:PRINT USING"³ ZS  O3 ######.# /######.# ³";CZS;MZS
56 LOCATE ,45:PRINT USING"³ DS SO2 ######.# /######.# ³";CSO2;MSO2
58 LOCATE ,45:PRINT "ÃÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄ´"
60 LOCATE ,45:PRINT USING"³ DUV     at \      \ ###.# ³";UTIME$;ULAST
62 LOCATE ,45:PRINT USING"³ SL R6   at \      \ ##### ³";STIME$;SLAST
64 LOCATE ,45:PRINT USING"³ Last HG at \      \ ###øC ³";HTIME$;HLAST%
66 LOCATE ,45:PRINT USING"³ Current temperature ###øC ³";TE%
68 LOCATE ,45:PRINT "ÀÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÄÙ"
70 LOCATE I,J:RETURN
100 DN%=8:DD$="\bdata\":CT$(0)="E  ":CT$(1)="I  "
110 E$=".rtn":READ PC$
120 ON ERROR GOTO 0
130 REM init
140 C$="init":GOSUB 6000:CHAIN MERGE C$+E$,150,ALL,DELETE 10000-65529
150 READ PC$:GOSUB 10000
180 NO$="-1":GOSUB 5300:REM program dir
220 I=0:GOSUB 5400:GOSUB 9000
280 HD$="B":EL$=CHR$(0)+"-> ":LF$=CHR$(10):CR$=CHR$(13):R$=CR$+LF$
290 LM$="???":DI=5
310 A$=INKEY$:ZS=-200:GOSUB 6050
330 ON ERROR GOTO 400:OPEN DD$+"TODAY.TMP" FOR INPUT AS 1
340 INPUT#1,JDAY$,MDS,MZS,MSO2,NDS,NZS,UTIME$,ULAST,STIME$,SLAST,HTIME$,HLAST%,CDS,CZS,CSO2
350 CLOSE 1:ON ERROR GOTO 0
360 GOTO 1200
400 RESUME 350
500 :
1200 M9$="0":REM brewer up?
1210 IF MDD$<>"o3" AND MDD$<>"n2" THEN MDD$="o3":GOTO 1300
1220 O1$="F,0,2:V,960,1":GOSUB 9400:TD=120:GOSUB 7000
1230 IF IS%=32 THEN GOSUB 3300:GOTO 1260
1240 TI=TIMER*60:IF TI<TD THEN 1230
1250 IS%=32:GOTO 1300
1260 REM IF Q14%=1 THEN 1320
1270 C$="da_lo":GOSUB 6000:CHAIN MERGE C$+E$,1280,ALL,DELETE 10000-65529
1280 READ PC$:GOSUB 10000
1290 R%=2:GOSUB 2480:R%=0
1291 IF A$="p" THEN C$="pb":GOTO 2700
1292 GOTO 1350
1299 :
1300 REM reset
1310 C$="re":GOSUB 6000:CHAIN MERGE C$+E$,1315,ALL,DELETE 10000-65529
1315 READ PC$:GOSUB 10000
1320 C$="da_lo":GOSUB 6000:CHAIN MERGE C$+E$,1325,ALL,DELETE 10000-65529
1325 READ PC$:GOSUB 10000:GOSUB 2300
1330 C$="fr":GOSUB 6000:CHAIN MERGE C$+E$,1335,ALL,DELETE 10000-65529
1335 READ PC$:GOSUB 10000
1340 GOSUB 9880:GOSUB 3300
1350 REM finish
1360 O1$="B,0":GOSUB 9450:IF Q14%=0 THEN O1$="L,19413,0,19414,255":GOSUB 9450
1399 :
1400 REM schedule?
1410 IO=0
1420 IF SK$="" THEN 1470
1430 C$="ab_sk":CHAIN MERGE C$+E$,1440,ALL,DELETE 10000-65529
1440 READ PC$:GOSUB 10000:IF SK$="" THEN 1470
1450 C$=DI$:IF DI$="menu" THEN 2600
1460 GOTO 2870
1470 DI$="menu":GOTO 2600
1499 :
1500 REM screen menus
1502 CHDIR BREWDIR$
1504 PRINT CL$:F$="command":LOCATE 6,SP
1506 IF C$="menu" THEN 1720
1508 IF C$="os" OR C$="om" OR C$="dm" OR C$="op" THEN 1530
1510 IF C$="sp" OR C$="tm" OR C$="nm" OR C$="em" THEN 1530
1512 IF C$="sm" OR C$="hk" THEN 1530
1514 IF C$<>"up" THEN RETURN
1516 IF C$="sm" OR C$="hk" THEN 1530
1520 :
1530 REM printout menu
1540 ON ERROR GOTO 1900
1550 CLOSE 9:OPEN C$+".fil" FOR INPUT AS 9
1560 IF EOF(9) THEN 1680
1561 INPUT#9,A$
1564 IF EOF(9) THEN 1680
1565 INPUT#9,B$
1580 IF C$=A$ THEN A$=F$:I=2:GOSUB 1800:GOTO 1560
1590 IF A$="cy" THEN B$=B$+" ("+CY$+")"
1600 IF A$="te" THEN B$=B$+STR$(TE%)+"c/"+TE$+"v)
1610 IF A$="tr" AND TR$<>"" THEN 1560
1620 IF A$="tf" AND TR$="" THEN 1560
1630 IF A$="di" AND DN%=8 THEN 1560
1640 IF A$="nr" AND DN%=0 THEN 1560
1650 IF A$="ld" AND PO%=1 THEN 1560
1660 IF A$="sd" AND PO%=0 THEN 1560
1670 GOSUB 1850:GOTO 1560
1680 CLOSE 9:GOSUB 1840:IF C$="menu" THEN 1695
1690 C$="menu":PC$=C$:LOCATE ,SP:PRINT "<return>  main menu";
1695 ON ERROR GOTO 3100
1700 RETURN
1701 A$=Q2$:GOTO 2093
1710 :
1720 REM main menu
1730 PRINT:LOCATE ,SP:PRINT "Enter a desired command or select a sub-menu"
1740 LOCATE ,SP:PRINT "from the following list:"
1790 GOTO 1530
1800 :
1810 REM menu border
1820 LOCATE ,SP:PRINT B1$;LX$;B0$;B0$;B2$;LY$;B3$:GOSUB 1850
1830 LOCATE ,SP:PRINT B7$;LX$;B0$;B0$;B8$;LY$;B9$:RETURN
1840 LOCATE ,SP:PRINT B4$;LX$;B0$;B0$;B5$;LY$;B6$:RETURN
1850 LOCATE ,SP:PRINT BR$;:LOCATE ,SP+2:PRINT A$;:LOCATE ,SP+10:PRINT BR$;
1860 LOCATE ,SP+12:PRINT B$;:LOCATE ,SP+41:PRINT BR$:RETURN
1880 :
1900 REM not found
1910 RESUME 1920
1920 CLS:LOCATE 10,SP:PRINT "Selected menu not found -- continuing"
1930 ON ERROR GOTO 3100
1940 CLOSE 9
1950 RETURN
1970 :
1990 GOSUB 9880:RETURN
1999 :
2000 B$="":GOSUB 2080
2005 IF RM%<>0 THEN RETURN
2010 GOSUB 2035:IF A$=CR$ THEN 2070
2015 IF A$<>BS$ THEN 2020
2016 IF B$<>"" THEN B$=LEFT$(B$,LEN(B$)-1):GOTO 2010
2017 GOTO 2010
2020 B$=B$+A$:GOTO 2010
2030 GOSUB 2080
2035 A$=INKEY$:IF A$="" THEN GOSUB 9891:GOTO 2035
2036 IF A$=Q3$ THEN RUN
2037 IF A$=Q4$ OR A$=Q1$ OR A$=Q5$ THEN A$=BS$:GOTO 2045
2040 IF LEN(A$)>1 THEN 2035
2045 PRINT A$;:VA=VAL(A$):RETURN
2050 GOSUB 2030
2060 B$=A$:GOSUB 2035:B$=B$+A$
2070 VB=VAL(B$):RETURN
2080 IF INKEY$<>"" THEN 2080
2085 RETURN
2090 A$=INKEY$:IF A$="" THEN RETURN
2093 '
2094 IF A$=Q1$ THEN TA%=1
2095 IF A$=Q2$ THEN HF%=1
2096 IF A$=Q3$ THEN RUN
2097 RETURN 
2099 :
2300 REM change tracker
2305 FLAG=0:IF Q14%=0 OR CP%=0 THEN RETURN
2310 O1$="?MOTOR.CLASS[2]":GOSUB 9450
2315 O1$="!MOTOR.CLASS[2] ":IF INSTR(I$,"NOMOTOR")>0 THEN 2330
2320 IF Q2%=0 THEN O1$=O1$+"NOMOTOR":GOSUB 9450
2325 RETURN
2330 IF Q2%=1 THEN LOCATE ,SP:PRINT "Enabling tracker":O1$=O1$+"TRACKERMOTOR":GOSUB 9450
2335 IF Q2%=1 THEN GOSUB 2350:FLAG=1
2340 RETURN
2349 :
2350 REM reset tracker
2355 M1$="2":GOSUB 6500:IF Y>=13500 AND Y<16000 THEN O1$="M,2,13400":GOSUB 9450:AZ%=13400
2360 O1$="I,2":GOSUB 9450
2365 RETURN
2399 :
2400 REM DOS shell
2410 CLS:PRINT"Entering DOS, type EXIT to return...":SHELL:RETURN
2440 REM moisture
2441 AH=0:RH=0:IF Q15%=0 THEN RETURN
2442 IF RS=0 THEN O1$="?RH.SLOPE":GOSUB 9450:RS=VA:IF RS=0 THEN RS=.029
2443 IF RO=0 THEN O1$="?RH.ORIGIN":GOSUB 9450:RO=VA:IF RO=0 THEN RO=.868
2444 O1$="?ANALOG.NOW[20]":GOSUB 9450:VA=(VA AND 1023)*5/1024:RH=(VA-RO)/RS
2445 O1$="?TEMP[FAN]":GOSUB 9450:RH=RH/(1.0546-.00216*VA)
2446 AH=RH*13.239*EXP(17.27*VA/(237.7+VA))/(273.15+VA)
2447 SH=0:IF AH>0 AND VA<>0 THEN SH=LOG(AH/2)*10000/66/VA
2449 RETURN
2450 REM temp
2454 IF Q7%=0 THEN 2460
2455 IF Q14%=1 THEN R%=3:GOTO 2480
2456 O1$="L,20248,0,20249,255:Z":GOSUB 9450:IF LEN(I$)<=6 THEN 2460
2457 TE$=STR$(INT(VAL(LEFT$(I$,5))*500/255)/100):R%=3:GOTO 2480
2460 PRINT CL$;TF$:PRINT RC$;"Brewer temp (0 - 5 Volts) ";
2470 GOSUB 2000:CLS:IF LEN(B$)=0 THEN 2496
2475 IF (VB<0) OR (VB>5) THEN 2460
2477 TE$=B$
2480 IF Q14%=0 THEN 2484
2481 O1$="?TEMP[PMT]":GOSUB 9450:TE%=VA:TE$=STR$((VA+33.27)/18.64):IF Q10%=0 THEN TE$=STR$((VA+30)/16)
2482 GOSUB 2440:A$=" moisture = "+MID$(STR$(AH+100.001),3,5)+" g/m3 ("+MID$(STR$(RH+1000.01),4,4)+"%)"
2484 IF Q14%=0 THEN TE%=-33.27+VAL(TE$)*18.64:IF Q10%=0 THEN TE%=-30+VAL(TE$)*16+.5
2486 TF$=TIME$+" "+MP$+DB$+YE$+" Brewer temp ="+STR$(TE%)+"C ("+TE$+"V)":IF Q14%=1 AND Q15%=1 THEN TF$=TF$+A$
2488 TG=(TE%-TG%)*PC+.5:IF TH%=0 THEN TG=.5
2490 IF C$<>"um" AND C$<>"up" THEN IF R%<>2 THEN PRINT#4,TF$:IF R%=0 THEN 2460
2492 IF C$="um" OR C$="up" THEN IF TE%<>TF% THEN PRINT#4,TF$
2494 TF%=TE%:R%=0:RETURN
2496 GOSUB 3260:GOSUB 5200:RETURN 
2499 :
2500 REM set CL$
2502 ON ERROR GOTO 3296
2505 PRINT CL$:LOCATE 10,SP:PRINT "*** Calculating free disk space ***"
2506 PRINT:LOCATE ,SP+20:PRINT TIME$
2510 LOCATE 4,1:COLOR 0,0
2517 CLOSE 10:OPEN DD$+"DIR.TMP" FOR OUTPUT AS 10:CLOSE 10
2519 FILES DD$+"DIR.TMP"
2520 COLOR 7,0
2525 LOCATE CSRLIN-2,1
2530 BF$="":FOR I=1 TO 12:BF$=BF$+CHR$(SCREEN(CSRLIN, I)):NEXT
2540 BF#=VAL(BF$)
2545 BF$=STR$(BF#)+" Bytes      "
2547 MEM=FRE(0):MEM=FRE("")
2550 CL$=CHR$(12)+MP$+DB$+YE$+" day= "+JD$+"  "+MDD$+"   #"+NO$+" * "+LO$+LF$
2555 IF LEN(BF$)<19 THEN BF$=STRING$(19-LEN(BF$)," ")+BF$
2560 CL1$=SP$:IF DN%=8 THEN CL1$=CL1$+BF$ ELSE CL1$=CL1$+" DISK TURNED OFF !!"
2563 IF LEN(CL1$)<49 THEN CL1$=CL1$+STRING$(49-LEN(CL1$)," ") ELSE CL1$=RIGHT$(CL1$,49)
2565 CL$=CL$+CL1$+"      in:   out:"+CR$
2570 IF BF#<102400! THEN CL$=CL$+"*!! Data disk almost FULL !!*"+LX$+LX$+LX$+LY$+CR$+CHR$(7) ELSE CL$=CL$+LX$+LY$+LX$+LY$+CR$
2590 ON ERROR GOTO 3100
2595 RETURN
2598 :
2600 REM set up menu
2605 HF%=0:IF TR$<>"" THEN PRINT CL$:LOCATE 10,SP:PRINT "*** Restoring motor positions ***":GOSUB 5100:GOSUB 7750
2610 C$=DI$:IF TR$="" THEN T0=TIMER/60
2611 GOSUB 1500
2613 IF TR$="fm" OR TR$="ma" THEN D$="Tracking Moon" ELSE D$="Tracking Sun"
2615 ED%=0:HF%=0:TA%=0:LO%=0:RM%=0:GOSUB 2080
2620 GOSUB 9891:F$="\  \##.###  \              \\    \###.##"
2630 FLAG=1:IF TR$="fm" OR TR$="ma" THEN FLAG=2
2640 GOSUB 7800:LOCATE 4,SP:PRINT USING F$;"mu=";M2;D$;"* za=";ZA
2645 LOCATE ,SP:PRINT "cm->   ";
2650 A1$(20)="l":GOSUB 5800:IF A1$(20)="" THEN 2600
2652 Z%=Z%+1:IF TR$<>"" AND A$="" AND Z%>10 THEN Z%=0:O1$="":GOSUB 9400
2655 IF A$="" THEN GOSUB 2090
2660 IF A$=CR$ THEN 2600
2665 IF A$=Q3$ THEN RUN
2670 IF A$="" THEN 2620
2675 IF (ASC(A$)<65) OR (ASC(A$)>122) THEN 2620
2680 PRINT A$;:B$=A$:GOSUB 2010:PRINT
2682 C$="":FOR I=1 TO LEN(B$):A$=MID$(B$,I,1):J=ASC(A$)
2683 IF J>64 AND J<91 THEN C$=C$+CHR$(J+32) ELSE C$=C$+A$
2685 NEXT:IF C$="sk" OR C$="skc" THEN ED%=1
2690 IF TR$="ds" THEN TR$="sa"
2692 IF TR$="fm" THEN TR$="ma"
2699 :
2700 REM built-in rtns
2701 GOSUB 2900:IF FLAG=1 THEN 2600
2702 IF C$="re" THEN COM(CP%) OFF:IS%=32:GOTO 2875
2703 IF TYP$="mkiv" AND C$="sw" THEN 2875
2715 IF C$="ul" OR C$="qs" OR C$="xl" THEN GOSUB 3700
2720 IF RIGHT$(C$,2) <>"pb" AND C$<>"tt" THEN GOSUB 3300
2730 IF C$="co" THEN GOSUB 3000:GOTO 2600
2745 IF C$="ti" THEN GOSUB 7300:GOTO 2600
2750 IF C$="cy" THEN GOSUB 3600:GOTO 2600
2760 IF C$="da" THEN GOSUB 6100:GOTO 2600
2765 IF C$="ex" THEN CLS:SYSTEM
2767 IF C$="shell" THEN GOSUB 2400:GOTO 2600
2770 IF C$="sd" THEN PO%=0:GOTO 2600
2775 IF C$="ld" THEN PO%=1:GOTO 2600
2785 IF C$="ddp" THEN SHELL "DIR *.*>prn":GOTO 2600
2787 IF C$="dds" THEN CLS:FILES DD$:GOSUB 9650:GOTO 2600
2790 IF C$="tr" THEN TR$="ds":GOTO 2600
2800 IF C$="tf" THEN TR$="":GOTO 2600
2805 IF C$="di" THEN GOSUB 3850:GOSUB 5200:GOSUB 2500:GOTO 2600
2810 IF C$="nr" THEN DN%=0:GOSUB 5200:GOTO 2600
2840 GOSUB 1500:IF C$="menu" THEN 2615
2860 ON ERROR GOTO 2890
2865 RESTORE:READ PC$:IF C$=PC$ THEN 2880
2870 GOSUB 9500
2872 IF LEN(C$)=0 THEN 2600
2873 A$=C$:IF LEN(C$)>8 THEN C$=LEFT$(C$,8)
2875 GOSUB 6000:CLOSE 1:OPEN C$+E$ FOR INPUT AS 1:CLOSE 1
2876 CHAIN MERGE C$+E$, 2880, ALL, DELETE 10000-65529
2880 ON ERROR GOTO 3100:GOSUB 10000:HD$="B"
2883 GOSUB 2500
2885 IF FG%<>1 THEN 2600:REM may return with new cmd
2887 FG%=0:ON ERROR GOTO 2890:GOTO 2873
2890 IF ERR<>53 THEN ON ERROR GOTO 0
2892 IF LEN(C$)>2 THEN RESUME 3500:REM cmd seq from menu
2895 CLS:LOCATE 10,10:PRINT "Command file not found --- continuing"
2896 X=1:GOSUB 7220:RESUME 2897
2897 ON ERROR GOTO 3100:GOTO 2600
2899 :
2900 REM test selection
2910 FLAG=0
2911 IF C$="as" THEN UC%=0:FLAG=1:GOTO 2990
2912 IF C$="b0" THEN GOSUB 9896:FLAG=1:GOTO 2990
2913 IF C$="b1" THEN GOSUB 9892:FLAG=1:GOTO 2990
2914 IF C$="b2" THEN GOSUB 9894:FLAG=1:GOTO 2990
2915 IF ((C$="ddp") OR (C$="dd")) AND (FP%<>1) THEN FLAG=1:GOTO 2990
2916 IF C$="dd" THEN SHELL "DIR "+DD$+"*.*>prn":FLAG=1:GOTO 2990
2917 IF C$="pd" THEN CLOSE 4:FP%=2:OPEN DD$+"D"+JD$+YF$+"."+NO$ FOR APPEND AS 4: FLAG=1:GOTO 2990
2918 IF C$="pf" THEN CLOSE 4:FP%=0:OPEN "scrn:" FOR OUTPUT AS 4:FLAG=1:GOTO 2990
2919 IF C$="pn" THEN CLOSE 4:FP%=1:OPEN "lpt1:" FOR OUTPUT AS 4:FLAG=1:GOTO 2990
2920 IF C$="sleep" THEN GOSUB 4900:FLAG=1:GOTO 2990
2921 IF C$="sw" THEN MDD2$="":REM MKIV only
2922 IF C$="o3" OR C$="n2" THEN MDD2$=C$:C$="sw"
2923 IF C$="te" THEN GOSUB 2450:FLAG=1:GOTO 2990
2924 IF C$="ua" OR C$="uf" OR C$="uv" OR C$="ux" THEN MDD2$=C$:C$="uv"
2925 IF C$="pz" THEN GOSUB 3650:FLAG=1:GOTO 2990
2990 RETURN
2999 :
3000 REM comment
3005 PRINT CL$;"Enter comment (max 75 chars)"
3010 GOSUB 2000:PRINT CL$:B$=LEFT$(B$,75):PRINT#4,B$
3015 C$="user":GOSUB 3050
3020 RETURN
3049 :
3050 REM comment in buffer
3055 A1$(IO)=LF$+"co"+CR$+TIME$+CR$+C$+": "+B$:IO=IO+1
3060 GOSUB 3225
3065 RETURN
3099 :
3100 REM printer err
3110 IF ERR>23 AND ERR<28 THEN 3130
3120 ON ERROR GOTO 0
3130 FP%=0:CLOSE 4
3140 OPEN "scrn:" FOR OUTPUT AS 4
3150 RESUME
3199 :
3200 REM output buffer
3210 IF IO<10 THEN RETURN 
3215 IF IO<5 THEN RETURN 
3220 IF IO<2 THEN RETURN 
3225 IF IO=0 THEN RETURN 
3230 IF DN%=0 THEN IO=0:RETURN 
3231 WR=CSRLIN:WC=POS(0):LOCATE 25,40:PRINT "*** Recording data ***";
3235 LOCATE WR,WC:A$=DD$+HD$+JD$+YF$+"."+NO$
3240 I=0:ON ERROR GOTO 3285
3241 CLOSE 1:OPEN A$ FOR INPUT AS 1
3245 CLOSE 1:OPEN A$ FOR APPEND AS 1
3247 ON ERROR GOTO 3100
3250 IF I=1 AND FP% THEN PRINT#4,A$;"  data file opened at ";TIME$
3254 FOR I=0 TO IO-1:PRINT#1,A1$(I);CR$;:BF#=BF#-LEN(A1$(I))-1:NEXT:CLOSE 1
3260 FOR I=0 TO IO:A1$(I)="":NEXT:PRINT FRE(0):IO=0
3262 IF IO<20 THEN A1$(20)=""
3270 GOSUB 2550
3280 LOCATE 25,40:PRINT SPACE$(30);:ON ERROR GOTO 3100:RETURN
3285 IF ERR<>53 AND ERR<>76 THEN 3291
3290 CLOSE 1:OPEN A$ FOR OUTPUT AS 1:GOSUB 4500:I=1:RESUME NEXT
3291 IF ERR=61 THEN BEEP:LOCATE ,10:PRINT "DISK FULL -- Recording OFF":GOTO 3297
3292 IF ERR<70 THEN ON ERROR GOTO 0
3293 IF ERR>70 THEN 3296
3295 BEEP:LOCATE ,10:PRINT "WRITE PROTECTED DISK -- Recording OFF"
3296 DN%=0:IO=0:RESUME NEXT
3297 DN%=0:IO=0:RESUME 3280
3299 :
3300 REM set clock from Brewer
3302 IF Q12%=1 OR Q6%=0 THEN RETURN:REM NOBREW mode
3303 LE%=0:IF Q14%=1 THEN 3390
3309 GOSUB 3360:COMP$=I$
3321 GOSUB 3360
3324 IF LE%>5 THEN 3354:REM fail
3327 IF COMP$=I$ THEN LE%=LE%+1:GOTO 3321:REM do again if reads same
3333 REM good read if we reach here
3336 FL=0:T$="":FOR XI=2 TO 12 STEP 5:GOSUB 3363:NEXT:REM hex to dec
3339 T$=LEFT$(T$,8):IF FL=0 THEN TIME$=T$:RETURN
3342 REM clock err
3345 PRINT#4,"Brewer time record is invalid -- battery dead or"
3348 PRINT#4,SPC(30);"-- clock not properly set"
3351 GOTO 3356
3354 PRINT#4,"Brewer clock read did not succeed"
3355 PRINT#4,"[";BC;"]","(";I$;")"
3356 TFAILS=TFAILS+1:IF TFAILS>=5 THEN PRINT#4,"Five clock failures - turning off clock":TFAILS=0:Q6%=0:GOSUB 5200
3357 RETURN
3360 O1$="L,59385,0:D,59388,59387,59386":GOSUB 9450:RETURN
3363 N=VAL(MID$(I$,XI,5)):IF XI=2 THEN N=(N AND 63):REM hex to dec
3366 IF N>153 THEN N=153
3369 IF N<0 THEN N=0
3372 N=N-INT(N/16)*6:T$=T$+RIGHT$(STR$(N+100),2)+":"
3375 IF N>23 AND XI=2 THEN FL=FL+1
3378 IF N>59 THEN FL=FL+1
3381 RETURN
3390 O1$="?TIME":GOSUB 9450:I=INSTR(I$,":"):IF I<2 THEN LE%=LE%+1:IF LE%<5 THEN 3390 ELSE 3354
3392 T$=MID$(I$,I-2,8):IF LEN(T$)<>8 THEN 3354
3394 TIME$=T$:RETURN
3399 :
3400 REM exec cmd seq
3405 ZABEFORE=ZA:GOSUB 5800
3410 REM this can be called as a subroutine and not return
3420 RM%=1:IJ=0
3430 IJ=IJ+1:JJ=0
3431 JJ=JJ+1:GOSUB 3300
3432 C$=G$(JJ):A$=C$:IF LEN(C$)>8 THEN C$=LEFT$(C$,8)
3433 GOSUB 9500:RESTORE:READ PC$:IF C$=PC$ THEN RESTORE:GOTO 3454
3434 GOSUB 2900:IF FLAG=1 THEN 3455
3437 IF (C$="ul" OR C$="qs" OR C$="xl") AND (SK$<>"") THEN 3460
3449 GOSUB 6000:FL=FRE(0):FL=FRE(""):ON ERROR GOTO 3493
3450 CLOSE 1:OPEN C$+E$ FOR INPUT AS 1:CLOSE 1
3451 IF TR$="fm" OR TR$="ma" THEN TR$="ma" ELSE TR$="sa"
3453 CHAIN MERGE C$+E$, 3454, ALL, DELETE 10000-65529
3454 READ PC$:ON ERROR GOTO 3100:GOSUB 10000:HD$="B"
3455 GOSUB 5800
3457 IF SGN(ZABEFORE)<>SGN(ZA) THEN ZABEFORE=ZA :GOSUB 2500
3460 IF SK$<>"" THEN IF (ZC>ZF) OR (JJ=QC AND IJ=QR) THEN JJ=-1:IJ=1:GOTO 3431
3465 IF HF%<>0 THEN 3490:REM abort
3470 IF JJ<QC THEN 3431
3480 IF IJ<QR THEN 3430
3485 IF JJ=QC AND ED%=2 AND G$(1)="sum" THEN PRINT#4,CHR$(12)
3490 HF%=0:GOSUB 9840:SK$="":DI$="menu":ZS=-200:ZF=0:G$(0)="":RM%=0:GOSUB 2080
3492 GOSUB 2500:GOSUB 5200:GOTO 2600
3493 IF ERR<>53 THEN ON ERROR GOTO 0
3494 RESUME 3495
3495 BEEP: ON ERROR GOTO 3100
3496 IF SK$<>"" THEN B$="Illegal command in schedule "+SK$+", ignoring":GOSUB 3050:GOTO 3455
3497 CLS:LOCATE 10,10:PRINT#4,"Command file not found --- continuing":GOTO 3490
3499 :
3500 REM setup seq from user
3510 ON ERROR GOTO 3100
3520 LE%=INT(LEN(B$)/2):QC=0:QR=1:FG=0
3530 FOR I=1 TO LE%:G$(I)=LEFT$(B$,2):REM next cmd
3540 B$=RIGHT$(B$,LEN(B$)-2)
3550 IF (QC=0) AND (VAL(B$)<>0) THEN QR=VAL(B$):QC=I
3555 IF G$(I)="ul" OR G$(I)="qs" OR G$(I)="xl" THEN FG=1
3560 NEXT:IF QC=0 THEN QC=LE%
3562 IF FG<>0 THEN GOSUB 3700
3565 PRINT CL$:LOCATE 5,SP:PRINT "There are ";QC;" commands :";CR$
3570 FOR I=1 TO QC:LOCATE ,SP:PRINT "Command # ";I;" is ";G$(I):NEXT
3575 PRINT:LOCATE ,SP:PRINT "Repeat command sequence ";QR;" times"
3580 LOCATE ,SP:PRINT "Press HOME if not correct":TB=300:GOSUB 7080
3585 GOSUB 2090:IF HF%=1 THEN 2600
3590 TI=TIMER*60:IF TI<TB AND A$<>CR$ THEN 3585
3595 GOTO 3400
3599 :
3600 REM update cycles
3610 PRINT CL$;"Enter cycle update (";CY$;" now)"
3620 GOSUB 2000:IF VAL(B$)<>0 THEN CY$=B$
3630 RETURN 
3649 :
3650 REM point zenith
3660 M1$=STR$(ER%/2+HC%):GOSUB 9870
3670 RETURN
3699 :
3700 REM lamp info for uv scan
3701 IF MDD$="o3" THEN 3710
3702 B$=C$+" is not an NO2 routine":PRINT#4,B$:PRINT B$
3703 X=2:GOSUB 7220:RETURN
3710 PRINT CL$;"Enter UV lamp to use ("+LM$+" now):"
3720 GOSUB 2000:IF B$<>"" THEN LM$=B$
3730 PRINT CL$;"Enter lamp to instrument distance (";DI;" cm now):"
3740 GOSUB 2000:IF VAL(B$)>0 THEN DI=VAL(B$)
3750 QSF%=0:RETURN
3799 :
3850 REM set up recording
3860 DN%=8
3870 PRINT CL$;"Load formatted disk in drive ";DD$:GOSUB 9650
3880 RETURN 
3899 :
3900 CLOSE 1: OPEN DD$+"TODAY.TMP" FOR OUTPUT AS 1
3910 IF JD$<>JDAY$ THEN GOSUB 6050
3920 PRINT#1,JD$;CR$;MDS;CR$;MZS;CR$;MSO2;CR$;NDS;CR$;NZS;CR$;UTIME$;CR$;ULAST;
3930 PRINT#1,CR$;STIME$;CR$;SLAST;CR$;HTIME$;CR$;HLAST%;CR$;CDS;CR$;CZS;CR$;CSO2
3940 JDAY$=JD$:CLOSE 1:RETURN
4200 REM print results
4205 GOSUB 8100:T0=S(1):GOSUB 8600:T0=TIMER/60:IF S(0)<2 THEN RETURN
4210 PRINT#4,"************* data summary *************"
4215 PRINT#4,H$;"  ";MP$;DB$;YE$;" ";S(2);S(3),TE%;"c deg",C$;AF%
4220 IF C$<>"sl" THEN 4250
4225   FOR I=10 TO 11:PRINT#4,USING "########";S(I);:NEXT:IF MDD$="o3" THEN SLAST=S(9):STIME$=TIME$:GOSUB 3900
4230   FOR I=4 TO 9:PRINT#4,USING   "########";S(I);:NEXT:PRINT#4,
4235   FOR I=25 TO 26:PRINT#4,USING "########";S(I);:NEXT
4240   FOR I=19 TO 24:PRINT#4,USING "########";S(I);:NEXT:PRINT#4,
4245   GOTO 4295
4250 FOR I=4 TO 9:PRINT#4,USING "########";S(I);:NEXT
4255 IF MDD$="o3" THEN 4270
4260   PRINT#4,USING "    F =####.#  NO2=##.##";S(10);S(11)
4265   GOTO 4285
4270   PRINT#4,USING "       SO2=####.#   O3=####.#";S(10);S(11)
4275   IF C$="ds" AND S(26)<=2.5 THEN CDS=S(11):MDS=MDS*NDS+CDS:CSO2=S(10):MSO2=MSO2*NDS+CSO2:NDS=NDS+1:MDS=MDS/NDS:MSO2=MSO2/NDS:GOSUB 3900
4280   IF C$="zs" AND S(26)<=2.5 THEN CZS=S(11):MZS=MZS*NZS+CZS:NZS=NZS+1:MZS=MZS/NZS:GOSUB 3900
4285 FOR I=19 TO 24:PRINT#4,USING "########";S(I);:NEXT
4290 PRINT#4,USING "#########.#";S(25);S(26)
4295 GOSUB 4300:RETURN
4299 :
4300 REM sum in output buffer
4310 A$="":IF MDD$="n2" THEN A$=MDD$
4315 A1$(IO)=R$+A$+"summary"+CR$+H$+CR$+MP$+CR$+DB$+CR$+YE$+CR$+STR$(S(2))+CR$
4320 A1$(IO)=A1$(IO)+STR$(S(3))+CR$+STR$(TE%)+CR$
4325 A1$(IO)=A1$(IO)+A$+C$+CR$+STR$(AF%)+CR$
4330 FOR I=4 TO 9:A1$(IO)=A1$(IO)+STR$(INT(S(I)+.5))+CR$:NEXT
4340 A1$(IO)=A1$(IO)+STR$(INT(S(10)*10+.5)/10)+CR$
4350 A1$(IO)=A1$(IO)+STR$(INT(S(11)*10+.5)/10)+CR$
4360 FOR I=19 TO 24:A1$(IO)=A1$(IO)+STR$(INT(S(I)+.5))+CR$:NEXT
4370 A1$(IO)=A1$(IO)+STR$(INT(S(25)*10+.5)/10)+CR$
4380 A1$(IO)=A1$(IO)+STR$(INT(S(26)*10+.5)/10):IO=IO+1
4385 PRINT#4,"########################################":PRINT#4,:RETURN
4399 :
4400 REM setup sl ds fm or zs output
4410 IF MDD$="o3" THEN A1$(IO)=R$ ELSE A1$(IO)=R$+MDD$
4420 A1$(IO)=A1$(IO)+C$+CR$+"a"+CR$+M5$+CR$+T0$+CR$+WL$+CR$+WU$+CR$+CZ$+CR$
4430 FOR I=WL TO WU:A1$(IO)=A1$(IO)+STR$(F(I))+CR$:NEXT:RETURN
4440 A1$(IO)=A1$(IO)+"rat":FOR I=4 TO 7:A1$(IO)=A1$(IO)+CR$+STR$(MS(I))
4445 NEXT:IO=IO+1:RETURN
4449 :
4450 GOSUB 3225:REM data header
4460 A1$(0)="dh"+CR$+DA$+CR$+MO$+CR$+YE$+CR$+LC$+CR$+STR$(LA)+CR$+STR$(LO)+CR$
4470 A1$(0)=A1$(0)+TE$+CR$+"pr"+CR$+PZ$:RETURN
4499 :
4500 REM write ic to B file
4505 PRINT#1,"version=2";CR$;
4510 PRINT#1,"dh";CR$;DA$;CR$;MO$;CR$;YE$;CR$;LC$;CR$;LA;CR$;LO;CR$;TE$;CR$;
4515 PRINT#1,"pr";CR$;PZ$;R$;"inst";CR$;:FOR I=2 TO 6:PRINT#1,TC(I);CR$;:NEXT
4520 PRINT#1,PC;CR$;A1;CR$;A2;CR$;A3;CR$;B1;CR$;B2;CR$;T1;CR$;MC$;CR$;
4525 PRINT#1,DL$;CR$;UO$;CR$;:FOR I=0 TO 5:PRINT#1,AF(I);CR$;:NEXT
4530 PRINT#1,ER$;CR$;TYP$;CR$;CP$;CR$;TC(7);CR$;NTC(7);CR$;
4535 FOR I=2 TO 6:PRINT#1,NTC(I);CR$;:NEXT
4540 PRINT#1,MZ%;CR$;MY%;CR$;MX%;CR$;NA1;CR$;NB1;CR$;NB2;CR$;NMZ%;CR$;NMX%;CR$;
4545 PRINT#1,SWITCH%;CR$;GS;CR$;GI;CR$;ZERO;CR$;IRIS;CR$;DELAY;CR$;
4550 PRINT#1,NOFW1;CR$;OZFW1;CR$;POFW2;CR$;UF$;CR$;ZO%;R$;"disp";CR$;
4555 FOR I=1 TO 6:FOR J=1 TO 3:PRINT#1,DC(I,J);CR$;:NEXT:NEXT
4560 FOR I=1 TO 6:FOR J=1 TO 3:PRINT#1,NDC(I,J);CR$;:NEXT:NEXT
4565 PRINT#1,LF$;"zeni";CR$;:FOR I=1 TO 9:PRINT#1,ZSC(I);CR$;:NEXT
4570 RETURN
4599 :
4900 REM sleep
4910 O1$="M,1,1172:M,3,75:M,4,320:M,5,320:B,0":GOSUB 9450:ZE%=1172
4920 GOSUB 9500:CLS:LOCATE 10,SP:PRINT "Press C to restart"
4930 X=FRE(0):A$=INKEY$:IF A$<>"c" THEN 4930
4940 RETURN 
4999 :
5000 REM error
5005 CLS:PRINT CL$:LOCATE 4,1
5010 PRINT "Cannot find ";DD$;NO$;"\OP_ST.";NO$:PRINT
5015 PRINT "Please check the following:":PRINT
5020 PRINT "  1. Contents of ";BREWDIR$;"\OP_ST.FIL"
5025 PRINT "  2. Instrument constants in ";DD$;NO$:PRINT
5030 GOTO 3100
5099 :
5100 REM tracking setup
5110 UC%=0:GOSUB 9740:M5$="192":M4$="320"
5115 IF TR$="fm" OR TR$="ma" THEN M5$="0":M4$="128"
5120 GOSUB 9790:GOSUB 9800:GOSUB 7750:O1$="R,1,1,1":GOSUB 9450
5130 RETURN
5199 :
5200 REM write op state
5205 GOSUB 2550
5210 PRINT CL$:CLOSE 8:OPEN DD$+NO$+"\op_st."+NO$ FOR OUTPUT AS 8
5220 PRINT#8,NO$;R$;DD$;R$;ICF$;R$;ZSF$;R$;DCF$;R$;UVR$;R$;DA$;R$;MO$;R$;YE$;R$;
5230 PRINT#8,LO$;R$;L1$;R$;L2$;R$;L3$;R$;
5240 PRINT#8,TE$;R$;NC%;R$;HC%;R$;SR%;R$;
5250 PRINT#8,Q1%;R$;Q2%;R$;Q3%;R$;Q4%;R$;
5260 PRINT#8,Q5%;R$;Q6%;R$;Q7%;R$;Q8%;R$;Q9%;R$;
5262 PRINT#8,Q10%;R$;Q11%;R$;0;R$;Q13%;R$;Q14%;R$;Q15%;R$;
5265 PRINT#8,DI$;R$;MDD$;R$;SK$;R$;
5270 CLOSE 8
5280 OPEN "op_st.fil" FOR OUTPUT AS 8:PRINT#8,NO$;R$;DD$;R$;:CLOSE 8
5290 RETURN
5299 :
5300 REM read op state
5310 CLOSE 8:IF NO$<>"-1" THEN GOSUB 2550:GOSUB 5280
5320 IF NO$= "-1" THEN OPEN "op_st.fil" FOR INPUT AS 8:INPUT#8,NO$,DD$:CLOSE 8:GOSUB 5395:GOSUB 2500:CL$=CHR$(12)
5330 ON ERROR GOTO 5000
5340 PRINT CL$:OPEN DD$+NO$+"\op_st."+NO$ FOR INPUT AS 8
5350 INPUT#8,A$,A$,ICF$,ZSF$,DCF$,UVR$,A$,B$,C$,LO$,L1$,L2$,L3$
5355 I$=DATE$:MO$=MID$(I$,1,2):DA$=MID$(I$,4,2):YE$=MID$(I$,9,2)
5360 INPUT#8,TE$,NC%,HC%,SR%,Q1%,Q2%,Q3%,Q4%,Q5%,Q6%,Q7%,Q8%,Q9%,Q10%
5370 INPUT#8,Q11%,I,Q13%,Q14%,Q15%,DI$,MDD$,SK$
5375 N9$="6":SE$=":":IF TYP$="mkiii" AND Q14%=1 THEN N9$="9":SE$="&"
5380 CLOSE 8:ON ERROR GOTO 3100:GOSUB 2300:GOSUB 5395
5390 RETURN
5395 IF RIGHT$(DD$,1)<>"\" THEN DD$=DD$+"\"
5396 RETURN
5399 :
5400 REM read ic file
5402 REM  I=0 reads all
5404 REM  I=1 reads all except dsp/zs values
5410 ON ERROR GOTO 5000
5412 CLOSE 8:OPEN DD$+NO$+"\"+ICF$+"."+NO$ FOR INPUT AS 8
5414 ON ERROR GOTO 3100
5420 REM get O3 TC's
5422 FOR J=2 TO 6:INPUT#8,TC(J):NEXT
5424 TC(0)=TC(2)-TC(5)-3.2*(TC(5)-TC(6)):TQ(0)=TC(0)
5426 TC(1)=TC(3)-TC(5)-.5*(TC(4)-TC(5))-1.7*(TC(5)-TC(6)):TQ(1)=TC(1)
5430 REM get other values
5432 INPUT#8,PC,A1,A2,A3,B1,B2,T1,MC$,DL$,UO$
5434 FOR J=0 TO 5:INPUT#8,AF(J):NEXT
5436 INPUT#8,ER$,TYP$,CP$
5437 N9$="6":SE$=":":IF TYP$="mkiii" AND Q14%=1 THEN N9$="9":SE$="&"
5438 OF%=148-VAL(MC$):M8$=MC$:ER%=VAL(ER$):CP%=VAL(CP$)
5440 AD=1019:IF CP%=2 THEN AD=AD-256
5442 CP$=RIGHT$(STR$(CP%),1):REM must be 1 char
5450 REM get other TC's
5452 INPUT#8,TC(7),NTC(7)
5454 FOR J=2 TO 6:INPUT#8,NTC(J):NEXT
5456 NTC(0)=NTC(2)-NTC(5)-3.2*(NTC(5)-NTC(6)):NTQ(0)=NTC(0)
5458 NTC(1)=.1*NTC(2)-.59*NTC(3)+.11*NTC(4)+1.2*NTC(5)-.82*NTC(6):NTQ(1)=NTC(1)
5460 REM get rest
5462 INPUT#8,MZ%,MY%,MX%,NA1,NB1,NB2,NMZ%,NMX%,SWITCH%,GS,GI
5464 INPUT#8,ZERO,IRIS,DELAY,NOFW1,OZFW1,POFW2,UF$,ZO%
5466 INPUT#8,ZU%:IF ZU%<999 THEN ZU%=ER%*3/4
5470 REM get DSP/ZE coeffs
5472 IF I<>0 THEN 5484
5474 CLOSE 8:OPEN DD$+NO$+"\"+DCF$+"."+NO$ FOR INPUT AS 8
5476 FOR I=1 TO 6:FOR J=1 TO 3:INPUT#8,DC(I,J):DC(0,J)=DC(6,J):NEXT:NEXT
5478 FOR I=1 TO 6:FOR J=1 TO 3:INPUT#8,NDC(I,J):NDC(0,J)=NDC(6,J):NEXT:NEXT
5480 CLOSE 8:OPEN DD$+NO$+"\"+ZSF$+"."+NO$ FOR INPUT AS 8
5482 FOR I=1 TO 9:INPUT#8,ZSC(I):NEXT
5484 CLOSE 8:RETURN
5499 :
5600 REM set LD%/LY%
5605 IF YE%=INT(YE%/4)*4 THEN LY%=1 ELSE LY%=0
5610 IF DA%>30 THEN LD%=1
5620 IF DA%=30 THEN IF VAL(MO$)=4 OR VAL(MO$)=6 OR VAL(MO$)=9 OR VAL(MO$)=11 THEN LD%=1
5630 IF VAL(MO$)=2 AND ((DA%=28 AND LY%=0) OR (LY%=1 AND DA%=29)) THEN LD%=1
5640 RETURN 
5649 :
5650 REM set new month/year
5655 DA%=1:MO$=RIGHT$(STR$(VAL(MO$)+101),2)
5660 IF MO$="13" THEN MO$="01":YE$=RIGHT$(STR$(YE%+101),2):YE%=VAL(YE$)
5670 YE$=RIGHT$(STR$(YE%+100),2):MM=VAL(MO$)*3-2:MO%=VAL(MID$(MD$,MM,3))
5680 MP$=MID$(MN$,MM,3)+" "
5690 RETURN 
5699 :
5700 REM set PC clock from Brewer
5710 REM IF Q12%=0 THEN DATE$=MO$+"/"+DA$+"/"+YE$:REM Not in NOBREW mode
5720 RETURN
5800 REM add gmtday (+suntrack)
5810 GOSUB 5600:A$=INKEY$:FLAG=1
5820 IF T0>720 THEN T0%=1:REM set if pm gmt
5830 IF (T0%=0) OR (T0>710) THEN GOSUB 7800:GOTO 5900:REM no new gmtday
5840 T0%=0:DA%=DA%+1:IF LD%=1 THEN GOSUB 5650:LD%=0
5845 DA$=RIGHT$(STR$(DA%+100),2):DB$=DA$+"/"
5850 PRINT#4,"    Observations for ";MP$;DB$;YE$:GOSUB 3225
5860 GOSUB 2500:GOSUB 7700:GOSUB 5200:GOSUB 7800
5899 :
5900 REM add local day
5905 IF A$="" THEN A$=INKEY$
5910 IF ZC>0 THEN ZA%=1:REM set if local pm
5920 IF (ZA%=0) OR (ZC>0) THEN GOSUB 7800:RETURN:REM if no new local day
5924 ZA%=0:ZS=-200:IF ED%=1 THEN C$="ed":GOTO 3453
5927 IF ED%=2 THEN RETURN
5930 GOSUB 6200:GOSUB 3900:GOSUB 3230:GOSUB 5200
5940 IF ED%=3 THEN ED%=1
5950 GOSUB 7800:RETURN
5999 :
6000 REM merge msg
6010 PRINT CL$:LOCATE 10,20:PRINT "   ***  Merging "+C$+E$+"  ***"
6020 LOCATE 12,32:PRINT TIME$:X=.5:GOSUB 7220
6030 RETURN
6050 REM stats
6060 MDS=0:MZS=0:MSO2=0:CDS=0:CZS=0:CSO2=0:NDS=0:NZS=0
6070 UTIME$="":ULAST=0:STIME$="":SLAST=0:HTIME$="":HLAST%=0:RETURN
6100 REM read in date
6105 LD%=0:ZA%=0:T0%=0:FLAG=0:CLS:PRINT CL$
6110 LOCATE 8,SP-2:PRINT "DATE: - (C.U.T.) - ";MP$;DB$;YE$:PRINT
6119 LOCATE ,SP:PRINT "If correct press RETURN"
6121 LOCATE ,SP:PRINT "If incorrect enter new date"
6130 PRINT:LOCATE ,SP:PRINT "  -- DAY   - DD ";:B$="":X$="3":GOSUB 6180
6132 IF A$=CR$ THEN FLAG=0:GOTO 6172
6134 X$="9":GOSUB 6180:DA$=B$:IF VAL(B$)>31 THEN FLAG=-1
6140 PRINT:LOCATE ,SP:PRINT "  -- MONTH - MM ";:B$="":X$="1":GOSUB 6180
6142 X$="9":GOSUB 6180:MO$=B$:IF VAL(B$)<1 OR VAL(B$)>12 THEN FLAG=-1
6150 PRINT:LOCATE ,SP:PRINT "  -- YEAR  - YY ";:B$="":X$="9":GOSUB 6180
6152 X$="9":GOSUB 6180:YE$=B$
6154 IF VAL(YE$)=INT(VAL(YE$)/4)*4 THEN LY%=1 ELSE LY%=0
6156 IF INSTR("04,06,09,11",MO$)<>0 AND DA$>"30" THEN FLAG=-1
6160 IF LY%=0 AND MO$="02" AND DA$>"28" THEN FLAG=-1
6162 IF NOT FLAG THEN 6170
6164 LOCATE 18,SP:PRINT "Some parameter is wrong in New Date, try again "
6166 DA$="01":MO$="01":YE$="90":MP$="jan ":DB$="01/":X=2:GOSUB 7220
6169 GOTO 6100
6170 GOSUB 6200:GOSUB 5700:IF R%=0 THEN 6110
6172 IF Q14%=1 THEN GOSUB 7400
6174 GOSUB 5200:RETURN
6180 GOSUB 2030:IF A$>="0" AND A$<=X$ THEN B$=B$+A$ ELSE FLAG=-1
6181 RETURN
6199 :
6200 REM update date vars
6210 DA%=VAL(DA$):YE%=VAL(YE$):MO%=VAL(MO$):GOSUB 5600
6220 MM=MO%*3-2:MO%=VAL(MID$(MD$,MM,3)):MP$=MID$(MN$,MM,3)+" "
6230 YF$=RIGHT$(STR$(YE%+100),2):DB$=RIGHT$(STR$(DA%+100),2)+"/"
6240 TI=TIMER*60:T0=TI/3600:GOSUB 8600:FLAG=1:GOSUB 7800
6250 JD%=MO%+DA%:IF LY%=1 AND MM>5 THEN JD%=JD%+1
6260 A$=MP$:B$=LEFT$(DB$,2):IF VAL(L2$)<=0 OR RA<0 OR H%>11 THEN 6330
6270   JD%=JD%-1:IF DA%<>1 THEN A$=MP$:B$=RIGHT$(STR$(DA%+99),2):GOTO 6330
6280     IF VAL(MO$)=1 THEN JD%=365:A$=MID$(MN$,12*3-2,3):YF$=RIGHT$(STR$(YE%+99),2):IF (YE%-1)/4=INT((YE%-1)/4) THEN JD%=366
6290     IF VAL(MO$)<>1 THEN A$=MID$(MN$,MM-3,3)
6300     A$=A$+" ":IF INSTR("05,07,10,12",MO$)<>0 THEN B$="30":GOTO 6330
6310       IF VAL(MO$)<>3 THEN B$="31" ELSE IF LY%=1 THEN B$="29" ELSE B$="28"
6330 IF VAL(L2$)>=0 OR RA>0 OR H%<13 THEN 6380
6340   JD%=JD%+1:IF LD%<>1 THEN A$=MP$:B$=RIGHT$(STR$(DA%+101),2):GOTO 6380
6350     B$="01":IF VAL(MO$)=12 THEN JD%=1:A$=MID$(MN$,1*3-2,3)+" ":YF$=RIGHT$(STR$(YE%+101),2)
6360     IF VAL(MO$)<>12 THEN A$=MID$(MN$,MM+3,3)+" "
6380 DC$=A$+B$+"/"+YF$:IF R%<>2 THEN PRINT#4,MDD$;" Observations for ";MP$;DB$;YF$
6390 JD$=RIGHT$(STR$(JD%+1000),3):GOSUB 3900:GOSUB 2550:GOSUB 7700:RETURN
6499 :
6500 REM motor pos
6505 Y=0:IF Q14%=0 THEN RETURN
6510 O1$="?MOTOR.POS["+M1$+"]":GOSUB 9450:Y=VA
6515 RETURN
6549 :
6550 REM motor init
6555 Y=0:IF Q12%=1 OR Q14%=0 THEN RETURN
6560 O1$="?MOTOR.ZERO.POS["+M1$+"]":GOSUB 9450:YY=VA
6565 O1$="?MOTOR.ORIGIN["+M1$+"]":GOSUB 9450:YY=YY-VA
6570 O1$="?MOTOR.SLOPE["+M1$+"]":GOSUB 9450:IF VA<>0 THEN YY=INT(YY/VA+.5)
6575 IF FLAG=1 AND M1$<>"2" THEN O1$="I,"+M1$:GOSUB 9450:GOSUB 6500:IF Y<0 THEN B$=TIME$+" Motor "+M1$+" appears to be stuck - Check the instrument":PRINT#4,B$
6580 IF FLAG=1 AND M1$="2" THEN GOSUB 2300:IF FLAG=0 THEN GOSUB 2350
6585 O1$="?MOTOR.DISCREPANCY["+M1$+"]":GOSUB 9450:Y=YY-VA
6590 RETURN
6599 :
6600 REM scrn msgs
6610 PRINT CL$:LOCATE ,SP:PRINT;"** Measurement Procedure **"
6615 LOCATE ,SP:PRINT "     Check:-":RETURN
6620 GOSUB 6640:PRINT "0":GOSUB 9780:RETURN
6630 GOSUB 6640:PRINT "1":GOSUB 9770:RETURN
6635 GOSUB 6640:PRINT "3":GOSUB 9785:RETURN
6636 GOSUB 6640:PRINT "4":GOSUB 9784:RETURN
6637 GOSUB 6640:PRINT "5":GOSUB 9787:RETURN
6640 LOCATE ,SP:PRINT "1 - Filter #1 to position # ";:RETURN
6650 GOSUB 6680:PRINT VAL(M5$)/64:GOSUB 9800:RETURN
6660 GOSUB 6640:PRINT (320-VAL(M4$))/64:GOSUB 9790:RETURN
6680 LOCATE ,SP:PRINT "2 - Filter #2 to position #";:RETURN
6690 LOCATE ,SP:PRINT "3 - Close iris":GOSUB 9740:RETURN
6700 LOCATE ,SP:PRINT "3 - Open iris":GOSUB 9750:RETURN
6799 :
6800 REM lamp warmup
6810 GOSUB 2090:IF HF%=1 THEN RETURN 
6815 GOSUB 9891:GOSUB 50
6820 IF TA%=1 THEN TA%=0:RETURN
6825 TI=TIMER*60:IF (TI<216000!) AND (TA>4968000!) THEN TA=0:RETURN
6830 IF TI<TA THEN 6810
6840 RETURN 
6845 :
6850 TA=300:GOSUB 7050
6860 TI=TIMER*60:IF TI>TA THEN RETURN
6870 IF IS%=42 THEN 6860
6880 RETURN 
6899 :
6900 REM is lamp on?
6910 WL$="0":WU$="7":CZ$="1":GOSUB 9700:GOSUB 9900
6915 WL$="1":WU$="1":CZ$="1":GOSUB 9700
6920 LO%=0:IF (F(4)-F(1))>498 THEN RETURN
6925 LO%=1:PRINT#4,"Lamp not on... ";C$;" test terminated"
6930 RETURN 
6949 :
7000 REM delay time
7010 TI=TIMER*60:IF TI>(5183880!-TD) THEN 7010
7030 TD=TD+TI:RETURN
7045 :
7050 REM wait time
7060 TI=TIMER*60:IF TI>(5183880!-TA) THEN 7060
7070 TA=TA+TI:RETURN
7076 :
7080 REM wait time
7085 TI=TIMER*60:IF TI>(5183880!-TB) THEN 7085
7090 TB=TB+TI:RETURN
7100 :
7110 :
7120 REM short wait
7130 TI=TIMER*60:TA=TA+TI
7140 TI=TIMER*60:IF TI<TA THEN 7140
7150 RETURN
7200 :
7220 REM short wait
7230 TI=TIMER*60:TB=TI+X*60
7240 TI=TIMER*60:IF TI<TB THEN 7240
7250 RETURN
7298 :
7300 REM set PC/Brewer time
7305 LD%=0:T0%=0:ZA%=0:GOSUB 3300:IF FP%<>0 THEN PRINT#4,"Clock checked at ";TIME$
7307 PRINT CL$:IF Q6%=1 THEN 7310
7308 PRINT "Brewer clock is off.  Do you want it turned back on? ";
7309 GOSUB 2035:IF A$="Y" OR A$="y" THEN Q6%=1:GOSUB 5200
7310 LOCATE 10,PP:PRINT HO$;"time = ";TIME$;:LOCATE 12,PP
7315 PRINT "If correct press return":LOCATE 14,PP:PRINT " hhmmss  ";
7318 B$=INKEY$:IF (B$="") AND (B$<>CR$) THEN 7310
7320 PRINT B$;:IF B$=CR$ THEN GOSUB 7400:RETURN
7322 GOSUB 2010
7324 BADTIME%=0
7326 IF LEN(B$)<>6 THEN 7339
7328 FOR CHARPOS%=1 TO 6
7330   IF INSTR("1,2,3,4,5,6,7,8,9,0",MID$(B$,CHARPOS%,1))=0 THEN BADTIME%=-1
7332 NEXT
7334 IF VAL(LEFT$(B$,2))>23 OR VAL(MID$(B$,3,2))>59 THEN BADTIME%=-1
7336 IF VAL(RIGHT$(B$,2))>59 THEN BADTIME%=-1
7338 IF NOT BADTIME% THEN TIME$=LEFT$(B$,2)+":"+MID$(B$,3,2)+":"+RIGHT$(B$,2):GOTO 7307
7339 LOCATE 18,PP:PRINT "Wrong time try again"
7340 X=2:GOSUB 7220
7344 GOTO 7307
7345 IF R%=0 THEN 7310
7350 RETURN
7399 :
7400 REM set Brewer clock from PC
7405 IF Q6%=0 THEN RETURN
7410 A=1900+YE%:IF YE%<50 THEN A=2000+YE%
7415 A$="!TIME"+STR$(A)+","+STR$(JD%)+","
7420 CLS:TI=TIMER*60:T0=(TI+180)/3600:GOSUB 8600:O1$="L,59391,23"
7430 X=H%:GOSUB 7470:O1$=O1$+",59388,"+N$:A$=A$+STR$(H%)+","
7440 X=M%:GOSUB 7470:O1$=O1$+",59387,"+N$:A$=A$+STR$(M%)+","
7450 X=S%:GOSUB 7470:O1$=O1$+",59386,"+N$:A$=A$+STR$(S%)
7460 IF Q14%=1 THEN O1$=A$
7465 GOSUB 9400:GOSUB 6200:RETURN
7470 IF X>99 THEN X=99
7480 IF X<0 THEN X=0
7490 N$=STR$(X+INT(X/10+.01)*6):RETURN
7499 :
7500 REM test and adjust attn
7510 SO$=""
7520 WL$="5":WU$="5":CZ$="1":GOSUB 9700:GOSUB 9900:FS=F(5):WL$="0":WU$="6"
7530 IF FS>100000! THEN A=INT(1+LOG(FS/100000!)*2/CO):GOTO 7570
7540 IF FS=0 THEN FS=1
7550 IF FS<25000 THEN A=INT(LOG(FS/25000)*2/CO):IF M5$<>"0" THEN 7570
7560 RETURN
7570 N$=SO$:SO$=M5$
7580 A=A*64+VAL(M5$):IF A<0 THEN A=0
7590 IF A>320 THEN A=320
7600 PRINT CL$:A$=STR$(A):M5$=RIGHT$(A$,LEN(A$)-1)
7610 IF N$=M5$ THEN PRINT#4,"Supressed oscillation of filter wheel.":GOTO 7640
7620 GOSUB 9800:LOCATE ,SP
7630 PRINT "Change filter #2 to position #";INT(A/64+.5):GOSUB 9650:GOTO 7520
7640 IF VAL(SO$)>VAL(M5$) THEN M5$=SO$:GOSUB 9800:REM higher
7645 GOSUB 9800
7650 RETURN
7699 :
7700 REM calc year # from 1965
7710 T=0:IF MO%<35 AND YE%/4=INT(YE%/4) THEN T=-1
7720 IF YE%<50 THEN T=(T+DA%+25+INT(YE%/4)+(YE%+35)*365-16+MO%)/365.2422
7725 IF YE%>50 THEN T=(T+DA%+INT(YE%/4)+(YE%-65)*365-16+MO%)/365.2422
7730 RETURN 
7749 :
7750 REM update tracker pos
7755 FLAG=2:IF TR$="ds" OR TR$="sa" THEN FLAG=1
7760 GOSUB 7800:GOSUB 7950
7765 RETURN
7799 :
7800 REM solar angles (FLAG=3/4 supply TI/T0; 1/3 sun; 2/4 moon)
7805 IF FLAG<3 THEN TI=TIMER*60:T0=TI/3600
7810 EP=.999999999#:I=(279.4574+360*T+T0/1460.97)*P0
7815 IF FLAG=2 OR FLAG=4 THEN GOSUB 8400:GOTO 7830
7818 E=4.2*SIN(3*I)-2*COS(2*I)+596.5*SIN(2*I)-12.8*SIN(4*I)+19.3*COS(3*I)
7820 E=E-(102.5+.142*T)*SIN(I)+(.033*T-429.8)*COS(I):RA=(T0+E/60+720-LO*4)*P0/4
7825 A=ATN(.4336*SIN(I-E*P0/240))
7830 E=COS(RA)*COS(A)*COS(LA*P0)+SIN(LA*P0)*SIN(A):IF E=>1 THEN E=EP
7831 IF E=<-1 THEN E=-EP
7835 AZ=(SIN(A)-E*SIN(LA*P0))/SQR(1-E*E)/COS(LA*P0):IF AZ<=-1 THEN AZ=-EP
7836 IF AZ=>1 THEN AZ=EP
7840 IF AZ=0 THEN AZ=.0000001
7841 IF E=0 THEN E=.0000001
7845 AZ=ATN(SQR(1-AZ*AZ)/AZ)/P0:E=ATN(SQR(1-E*E)/E):IF E<0 THEN E=E+PI:REM CONVERSION ERROR
7850 IF FLAG=2 OR FLAG=4 THEN E=E+PL*P0*SIN(E):REM paralax correction
7855 RA=SIN(RA):IF AZ=<0 AND RA=<0 THEN AZ=180+AZ
7860 IF RA>0 THEN IF AZ=<0 THEN AZ=180-AZ ELSE AZ=360-AZ
7865 M2=.999216*SIN(E):GOSUB 7920:M3=M2:M2=.99656*SIN(E):GOSUB 7920
7870 IF E>90.5*P0 THEN 7885:REM correct refraction
7875   C1=COS(E):D1=1/(.955+(20.267*C1))-.047121
7880   C1=C1+.0083*D1:IF C1>-EP AND C1<EP THEN E=PI/2-ATN(C1/SQR(1-C1*C1))
7885 ZA=INT(P3%*E/P0+.5)/P3%:ZC=ZA:IF RA<0 THEN ZC=-ZA
7890 AZC%=SR%*AZ/360+.5+NC%+UC%:IF AZC%>SR% THEN AZC%=AZC%-SR%
7891 IF AZC%<0 THEN AZC%=AZC%+SR%
7895 ZEC%=ER%*(1-ZA/180)/2+.5+HC%:RETURN
7920 M2=1/COS(ATN(M2/SQR(1-M2*M2)))
7930 M2=INT(P3%*M2+.5)/P3%:RETURN
7949 :
7950 REM move az/ze to AZC%/ZEC%
7955 IF TR$="" THEN RETURN
7960 O1$="":IF Q2%=0 THEN 7980
7965 O1$="M,2,"+STR$(AZC%):AZ%=AZC%
7970 IF ABS(AZC%-AP%)>1000 AND Q14%=0 THEN O1$="L,19414,120:"+O1$+":L,19414,255"
7975 AP%=AZC%:IF Q1%=1 AND (TR$="fm" OR TR$="ds") THEN O1$=O1$+":"
7980 IF Q1%=1 AND (TR$="fm" OR TR$="ds") THEN O1$=O1$+"M,1,"+STR$(ZEC%):ZE%=ZEC%
7985 IF O1$<>"" THEN GOSUB 9400
7990 RETURN
7999 :
8000 REM zero s(i)
8005 S(0)=0
8010 FOR I=1 TO 40:MS(I)=0:S(I)=0:NEXT
8015 RETURN
8024 :
8025 REM zero z(i)
8030 Z(0)=0
8035 FOR I=1 TO 40:MZ(I)=0:Z(I)=0:NEXT
8040 RETURN
8049 :
8050 REM sum s(i)
8055 S(0)=S(0)+1
8060 FOR I=1 TO MS(0):S(I)=S(I)+MS(I):S(I+15)=S(I+15)+MS(I)^2:NEXT
8065 RETURN
8074 :
8075 REM sum z(i)
8080 Z(0)=Z(0)+1
8085 FOR I=1 TO MZ(0):Z(I)=Z(I)+MZ(I):Z(I+15)=Z(I+15)+MZ(I)^2:NEXT
8090 RETURN
8099 :
8100 REM calc mean,stddev
8105 FOR I=1 TO MS(0)
8110   IF S(0)<2 THEN E=0:GOTO 8120
8115     E=(S(0)*S(I+15)-S(I)*S(I))/S(0)/(S(0)-1):IF E<0 THEN E=0
8120   S(I+15)=INT(10*SQR(E)+.5)/10
8125   IF S(0)<>0 THEN S(I)=INT(P3%*S(I)/S(0))/P3%
8130 NEXT:RETURN
8149 :
8150 REM calc mean,stddev
8155 FOR I=1 TO MZ(0)
8160   IF Z(0)<2 THEN E=0:GOTO 8170
8165     E=(Z(0)*Z(I+15)-Z(I)*Z(I))/Z(0)/(Z(0)-1):IF E<0 THEN E=0
8170   Z(I+15)=INT(10*SQR(E)+.5)/10
8175   IF Z(0)<>0 THEN Z(I)=INT(P3%*Z(I)/Z(0))/P3%
8180 NEXT:RETURN
8199 :
8200 REM print counts,corr logs
8205 AF%=INT(VAL(M5$)/64+.5):T0=TQ:GOSUB 8600
8210 PRINT#4,USING "\          \ ###";C$+RIGHT$(STR$(AF%),1)+" "+H$;CY;
8215 FLAG=4:IF TR$="ds" OR TR$="sa" THEN FLAG=3
8220 GOSUB 7800:PRINT#4,USING " ###.##";ZA;
8225 FOR I=WL TO WU
8230   IF I=1 THEN PRINT#4,USING " ####";F(I);:GOTO 8240
8235   PRINT#4,USING " #######";F(I);
8240 NEXT:PRINT#4,:MS(10)=F(2):MS(11)=F(6):REM SL needs raw counts
8245 GOSUB 8300:IF PO%<>1 THEN 8270
8250   PRINT#4,SPC(30);
8255   FOR I=2 TO 6:PRINT#4,USING "########";F(I);:NEXT
8260   PRINT#4,:PRINT#4,SPC(30);
8265   FOR I=2 TO 6:PRINT#4,USING "########";10^(F(I)/P4%);:NEXT:PRINT#4,
8270 GOSUB 8700:GOSUB 4440
8275 IF MDD$="o3" THEN GOSUB 8800 ELSE GOSUB 8900
8280 GOSUB 8050:RETURN
8299 :
8300 REM calc corr F's
8305 FOR I=WL TO WU:IF I=1 THEN 8335
8310   VA=F(I):GOSUB 8350
8315   F(I)=LOG(VA)/CO*P4%:J=I:IF J=0 THEN J=7
8320   IF MDD$="o3" THEN X=TC(J) ELSE X=NTC(J)
8325   F(I)=F(I)+X*TE%+AF(AF%)
8330   IF C$="ds" OR C$="fz" OR C$="sc" OR C$="fm" THEN F(I)=F(I)+BE(I)*M3*PZ%/1013:REM rayleigh
8335 NEXT:RETURN
8349 :
8350 REM correct VA for dark/dead time
8355 VA=(VA-F(1))*2/CY/IT:IF VA>1E+07 THEN VA=1E+07
8360 IF VA<2 THEN VA=2
8365 F1=VA:FOR J=0 TO 8:VA=F1*EXP(VA*T1):NEXT
8370 RETURN
8399 :
8400 REM calc lunar position
8402 TT=(YE%-84)*365+INT((YE%-80)/4):IF YE%<50 THEN TT=(YE%+16)*365+INT((YE%+20)/4)
8403 IF MO%<35 AND YE%/4=INT(YE%/4) THEN TT=TT-1
8404 TT=(TT-5845.5+MO%+DA%+T0/1440)/36525!
8406 T2=TT*TT:T3=T2*TT:E=(84381.448#-46.815*TT-.00059*T2+.001813*T3)/3600
8408 C=36000.7701#:GOSUB 8462:DT=100.460618#+C+(.093104*T2-.0000062*T3)/240+T0/4
8410 XX=DT:GOSUB 8464:DT=XX
8412 C=481267.883#:GOSUB 8462:LB=218.32+C
8414 C=477198.85#:GOSUB 8462:LB=LB+6.29*SIN((134.9+C)*P0)
8416 C=413335.38#:GOSUB 8462:LB=LB-1.27*SIN((259.2-C)*P0)
8418 C=890534.23#:GOSUB 8462:LB=LB+.66*SIN((235.7+C)*P0)
8420 C=954397.7#:GOSUB 8462:LB=LB+.21*SIN((269.9+C)*P0)
8422 C=35999.05:GOSUB 8462:LB=LB-.19*SIN((357.5+C)*P0)
8424 C=966404.05#:GOSUB 8462:LB=LB-.11*SIN((186.6+C)*P0)
8426 XX=LB:GOSUB 8464:LB=XX
8428 C=483202.03#:GOSUB 8462:BT=5.13*SIN((93.3+C)*P0)
8430 C=960400.87#:GOSUB 8462:BT=BT+.28*SIN((228.2+C)*P0)
8432 C=6003.18:GOSUB 8462:BT=BT-.28*SIN((318.3+C)*P0)
8434 C=407332.2#:GOSUB 8462:BT=BT-.17*SIN((217.6-C)*P0)
8436 C=477198.85#:GOSUB 8462:PL=.9508+.0518*COS((134.9+C)*P0)
8438 C=413335.38#:GOSUB 8462:PL=PL+.0095*COS((259.2-C)*P0)
8440 C=890534.23#:GOSUB 8462:PL=PL+.0078*COS((235.7+C)*P0)
8442 C=954397.7#:GOSUB 8462:PL=PL+.0028*COS((269.9+C)*P0)
8444 SD=.2725*PL:R=1/SIN(PL*P0)
8446 L=COS(BT*P0)*COS(LB*P0)
8448 M=COS(E*P0)*COS(BT*P0)*SIN(LB*P0)-SIN(E*P0)*SIN(BT*P0)
8450 N=SIN(E*P0)*COS(BT*P0)*SIN(LB*P0)+COS(E*P0)*SIN(BT*P0)
8452 DC=ATN(N/SQR(1-N*N))/P0:LF=SGN(L*M)*90
8454 IF L<>0 THEN LF=ATN(M/L)/P0
8456 IF L<0 THEN LF=LF+180
8458 HA=DT-LF:XX=HA:GOSUB 8464:HA=XX
8460 RA=(HA-LO)*P0:A=DC*P0:RETURN
8462 C=C*(TT-INT(TT*C/360)*360/C):RETURN
8464 XX=XX-INT(XX/360)*360:RETURN
8499 :
8500 REM WV,SQ,GS,GI -> M1,M2
8505 M1=DC(SQ,2)*DC(SQ,2)-4*(DC(SQ,1)-WV)*DC(SQ,3)
8510 IF MDD$="n2" THEN M1=NDC(SQ,2)*NDC(SQ,2)-4*(NDC(SQ,1)-WV)*NDC(SQ,3)
8515 IF M1<0 THEN M1=0:M2=0
8520 IF M1>0 AND MDD$="o3" THEN M1=(-DC(SQ,2)+SQR(M1))/2/DC(SQ,3)
8525 IF M1>0 AND MDD$="n2" THEN M1=(-NDC(SQ,2)+SQR(M1))/2/NDC(SQ,3)
8530 IF M1>0 THEN M2=M1*GS+GI
8535 M1=INT(M1+.5):M2=INT(M2+.5)
8540 RETURN
8549 :
8600 REM t0 -> h% m% s%
8610 H%=INT(T0/60):M%=INT(T0-H%*60):S%=INT((T0-H%*60-M%)*60)
8615 IF H%>23 THEN H%=H%-24
8620 H$=RIGHT$(STR$(H%+100),2)
8625 H$=H$+":"+RIGHT$(STR$(M%+100),2)+":"+RIGHT$(STR$(S%+100),2):RETURN
8699 :
8700 REM calc ratios
8705 IF MDD$="n2" THEN 8730
8710   FOR I=4 TO 6:MS(I)=F(5)-F(I-2):NEXT:MS(7)=F(6)-F(5):REM single ratios
8715   MS(8)=MS(4)-3.2*MS(7):REM SO2 ratio
8720   MS(9)=MS(5)-.5*MS(6)-1.7*MS(7):REM O3 ratio
8725   GOTO 8740
8730   FOR IM=4 TO 8:MS(IM)=F(IM-2):NEXT
8735   MS(9)=.1*F(2)-.59*F(3)+.11*F(4)+1.2*F(5)-.82*F(6):REM F ratio
8740 IF PO%=1 THEN PRINT#4,SPC(30);
8745 FOR I=4 TO 9:MS(I)=INT(MS(I)+.5)
8750 IF PO%=1 THEN PRINT#4,USING "########";MS(I);
8755 NEXT:IF PO%=1 THEN PRINT#4,
8760 MS(1)=T0:MS(2)=ZA:MS(3)=M2:IF MDD$="o3" THEN MS(12)=F(2):MS(13)=F(6)
8790 RETURN 
8799 :
8800 REM calc ozone
8805 IF C$="sl" OR C$="um" OR C$="up" THEN RETURN
8810 IF C$="zb" OR C$="zc" OR C$="zp" OR C$="zs" THEN 8830
8815   MS(11)=(MS(9)-B1)/A1/M2:REM direct data
8820   MS(10)=((MS(8)-B2)/A3/M2-MS(11))/A2
8825   GOTO 8860
8830   A=ZSC(1)+ZSC(2)*M2+ZSC(3)*M2*M2-(MS(9)-B1)/P4%:REM zenith data
8835   B=ZSC(4)+ZSC(5)*M2+ZSC(6)*M2*M2
8840   C=ZSC(7)+ZSC(8)*M2+ZSC(9)*M2*M2
8845   A=B*B-4*A*C:IF A<0 THEN A=0
8850   MS(11)=(-B+SQR(A))/2/C*P4%
8855   MS(10)=((MS(8)-B2)/A3/M2-MS(11))/A2
8860 MS(10)=INT(MS(10)+.5)/10:MS(11)=INT(MS(11)+.5)/10
8865 PRINT#4,USING SPACE$(19)+"O3 =####.#  SO2 =####.#";MS(11);MS(10)
8870 RETURN
8899 :
8900 REM calc NO2
8905 IF C$="sl" OR C$="um" THEN RETURN
8910 IF C$="zb" OR C$="zc" OR C$="zp" OR C$="zs" THEN 8930
8915   MS(11)=(MS(9)-NB1)/NA1/M2:REM direct data
8920   MS(10)=MS(9)
8925   GOTO 8940
8930   MS(11)=(MS(9)-NB2)/NA1/M2:REM zenith data
8935   MS(10)=MS(9)
8940 MS(10)=INT(10*MS(10)+.5)/10:MS(11)=INT(MS(11)+.5)/10
8945 PRINT#4,USING SPACE$(19)+"NO2=####.#    F =####.#";MS(11);MS(10)
8950 RETURN
8999 :
9000 REM start up rs232
9030 OS%=32:IS%=32:REM I/O status
9040 IN%=0:DEF SEG=&HB000:REM input len
9060 CR$=CHR$(13):CLOSE 7:R$=CR$+CHR$(10)
9082 IF A$<>"p" THEN OPEN "com"+CP$+":9600,n,8,1,rs,ds,cs,cd" AS 7
9095 RETURN
9099 :
9100 REM get i$
9190 I$=LEFT$(I1$,IN%):VA=VAL(I$)
9195 RETURN
9199 :
9200 REM set rs232 speed
9250 IF Q14%=1 THEN RETURN
9255 COM(CP%) OFF:O1$="F,0,2;V,30,1":GOSUB 9450
9260 CLOSE 7:OPEN "com"+CP$+":300,n,8,1,rs,ds,cs,cd" AS 7
9265 RETURN
9270 IF Q14%=1 THEN RETURN
9275 COM(CP%) OFF:O1$="F,0,2;V,120,1":GOSUB 9450
9280 CLOSE 7:OPEN "com"+CP$+":1200,n,8,1,rs,ds,cs,cd" AS 7
9290 RETURN
9299 :
9300 REM input routine
9301 COM(CP%) OFF
9303 TDTMP=TD:TD=900:GOSUB 7000
9306 I1$="":ON ERROR GOTO 9345
9307 SF=TIMER*60
9308 TI=TIMER*60:IF TI<SF+DELAY*60 THEN 9308
9309 BC=LOC(7):IF BC=0 THEN 9324: REM bufcount
9312 I2$=INPUT$(BC,7):REM bufcontents
9315 I1$=I1$+I2$
9318 RX=INSTR(I1$,EL$)
9321 IF RX>0 THEN IS%=32:GOTO 9327
9324 TI=TIMER*60:IF TI<TD THEN 9309
9327 COM(CP%) OFF:ATTEMPT%=0
9330 I2$="":IN%=LEN(I1$)-3:IF IN%<1 THEN IN%=0
9333 I$=LEFT$(I1$,IN%):VA=VAL(I$)
9336 WX=CSRLIN:WY=POS(0)
9339 LOCATE 2,60:PRINT CHR$(IS%):LOCATE WX,WY
9342 ON ERROR GOTO 3100:TD=TDTMP:RETURN
9345 COM(CP%) OFF:WR=CSRLIN:WC=POS(0):LOCATE 24,40:PRINT "COM error at ";TIME$;
9346 IF FP%<>0 THEN PRINT#4,"COM error ";ERR;" at ";TIME$;" during ";C$
9348 IF FP%<>0 THEN PRINT#4,"[";BC;"]","(";I2$;")","(";O1$;")":REM cnt, rx/tx
9351 LOCATE WR,WC:TD=TDTMP:RESUME 9357:REM ack COM error, reset TP
9357 ATTEMPT%=ATTEMPT%+1
9360 REM reXmit
9363 IF ATTEMPT%=6 THEN COM (CP%) OFF:GOTO 9610:REM restart after 5 tries
9364 ON COM(CP%) GOSUB 9300:COM(CP%) ON
9366 O1$="T":RETURN
9375 REM flush buf
9380 IF LOC(7)=0 THEN RETURN
9382 SF=TIMER*60
9384 TI=TIMER*60:IF TI<SF+DELAY*60 THEN 9384
9385 I1$=INPUT$(LOC(7),7)
9390 RETURN
9399 :
9400 REM send o1$ to brewer
9405 GOSUB 9500
9410 WR=CSRLIN:WC=POS(0):LOCATE 25,40:PRINT SPACE$(30);
9415 OS%=42:LOCATE 2,66:PRINT CHR$(OS%):LOCATE WR,WC:OS%=32
9420 IS%=42:IF Q12%=1 THEN IS%=32:GOTO 9430:REM Disabled in NOBREW mode
9425 ON COM(CP%) GOSUB 9300:COM(CP%) ON:REM ip on
9429 O$=O1$+CR$:PRINT#7,O$;
9430 WR=CSRLIN:WC=POS(0):LOCATE 2,60:PRINT CHR$(IS%)
9435 LOCATE 2,66:PRINT CHR$(OS%):LOCATE WR,WC
9440 O1$="":TD=3500:GOSUB 7000:RETURN:REM set wait time
9445 :
9450 ATTEMPT%=0:GOSUB 9400:GOSUB 9500:RETURN:REM send, wait for response
9455 :
9460 BV=INP(AD):REM set break bit on UART
9470 OUT AD,(BV OR 64):TA=60:GOSUB 7100
9480 OUT AD,(INP(AD) AND 191):REM reset break
9490 RETURN
9499 :
9500 REM brewer ready?
9510 IF HF%=0 THEN GOSUB 2090:IF HF%=1 THEN GOSUB 9460:RETURN
9520 GOSUB 9891:WR=CSRLIN:WC=POS(0):IF IS%=42 THEN GOSUB 9550:GOTO 9510
9530 IF PF%<>0 THEN LOCATE 25,40:PRINT "Brewer responded at ";TIME$;
9535 IF C$<>"re" AND INSTR(I$,"SCI-TEC")>0 THEN PRINT#4,"Brewer reset at ";TIME$:RUN
9540 PF%=0:LOCATE WR,WC:RETURN
9549 :
9550 REM prompt Brewer
9560 TI=TIMER*60:IF TI<TD THEN RETURN:REM still waiting
9565 REM prompt flag++
9570 COM(CP%) OFF:PF%=PF%+1:IS%=42:IF Q12%=1 THEN IS%=32:REM NOBREW mode
9575 ON COM(CP%) GOSUB 9300:COM(CP%) ON
9580 O$=CR$:PRINT#7,O$;:TD=2000:GOSUB 7000:REM prompt, set delay
9590 WR=CSRLIN:WC=POS(0):LOCATE 25,40:PRINT "Waiting for Brewer... (";PF%;")";
9600 LOCATE WR,WC:IF PF%<5 THEN RETURN
9610 CLS:BEEP:LOCATE 8,SP:PRINT "Brewer failed to respond 5 times"
9620 LOCATE 9,SP:PRINT "Check Brewer power & cables etc."
9630 GOSUB 9650
9640 PRINT#4,"Brewer reset at ";TIME$:RUN
9650 LOCATE ,SP:PRINT "  Press return when ready"
9654 IF RM%=0 THEN GOSUB 2030
9660 PRINT:RETURN
9670 PRINT CL$:LOCATE ,SP
9672 WR=CSRLIN:WC=POS(0)
9673 LOCATE 2,60:PRINT CHR$(IS%):LOCATE WR,WC
9674 LOCATE 2,66:PRINT CHR$(OS%):LOCATE WR,WC
9675 PRINT "*** Taking ";C$;" measurement ***"
9676 LOCATE ,SP:PRINT "   Press HOME to stop":GOSUB 50:RETURN
9699 '
9700 REM misc cmds; update pos (TR$)
9710 GOSUB 9880:WT=(WU-WL+1)*VAL(CZ$)*18/7*IT:FLAG=4:IF TR$="ds" OR TR$="sa" THEN FLAG=3
9712 T0=TIMER/60+WT/120:GOSUB 7800:GOSUB 7950
9715 O1$="R,"+WL$+","+WU$+","+CZ$
9720 GOSUB 9400:TI=TIMER*60:TS=TI/7200:REM start obs at ts
9730 TD=200+WT*60:GOSUB 7000:RETURN:REM wait time
9739 :
9740 M3$="0":GOTO 9760
9750 M3$=STR$(IRIS)
9760 O1$="M,3,"+M3$:GOSUB 9400:RETURN:REM iris
9770 M4$="256":GOTO 9790
9780 M4$="320":GOTO 9790
9784 M4$="64":GOTO 9790
9785 M4$="128":GOTO 9790
9787 M4$="0"
9790 O1$="M,4,"+M4$:GOSUB 9400:RETURN:REM FW#1
9800 O1$="M,5,"+M5$:GOSUB 9400:RETURN:REM FW#2
9805 GOSUB 9810:IF TYP$="mkiii" THEN GOSUB 9815:RETURN ELSE RETURN
9810 O1$="M,10,"+M8$:GOSUB 9400:TD=3000:GOSUB 7000:RETURN
9815 O1$="M,"+N9$+","+M8$:GOSUB 9400:TD=3000:GOSUB 7000:RETURN
9820 M9$="2":GOTO 9850:REM std lamp
9830 M9$="1":GOTO 9850:REM merc lamp
9840 GOSUB 2080:M9$="0":O1$="B,0":GOSUB 9450:RETURN:REM lamps off
9850 O1$="B,"+M9$:TA=18000:GOSUB 7050:GOSUB 9450:RETURN
9860 M1$="0":REM ze to lamps
9870 O1$="M,1,"+M1$:ZE%=VAL(M1$):GOSUB 9450:RETURN
9880 WL=VAL(WL$):WU=VAL(WU$):CY=VAL(CZ$):RETURN
9888 IF Q14%=1 AND Q15%=1 THEN LOCATE 2,42:PRINT USING"###.# g/m3 ##";AH;SH
9889 LOCATE 2,68:PRINT USING"####.##";ZA:LOCATE 2,1:PRINT " ";C$;"    ";SK$;"  ";:IF JJ<QC AND RM%=1 THEN PRINT G$(JJ+1) ELSE PRINT "--      "
9890 LOCATE 1,50:PRINT " C.U.T. ";CT$(Q6%);TIME$;" 3.75c":RETURN
9891 WR=CSRLIN:WC=POS(0):GOSUB 9888:LOCATE WR,WC:RETURN
9892 GOSUB 9830:IF TI>5130000! THEN RETURN
9893 PRINT#4,"**** HG lamp on at ";TIME$;" ****":RETURN
9894 GOSUB 9820:IF TI>5130000! THEN RETURN
9895 PRINT#4,"**** Standard lamp on at ";TIME$;" ****":RETURN
9896 GOSUB 9840:IF TI>5130000! THEN RETURN
9897 PRINT#4,"**** Both lamps off at ";TIME$;" ****":RETURN
9899 :
9900 REM get last obs numbers
9910 GOSUB 9500:TI=TIMER*60:TQ=TS+TI/7200:IF TQ>TI/3600 THEN TQ=TQ+720
9920 IF (T0%=1) AND (TQ<720) THEN TQ=TQ+1440
9930 T0$=STR$(INT(100*TQ+.5)/100)
9932 O1$="O":IF Q12%=1 THEN TA=0:GOTO 7120:REM NOBREW mode
9935 GOSUB 9450:O1$="T":IF IN%<10 THEN 9935
9940 O1$="":CA=0:XN=LEN(I$)
9945 CA=CA+1:IF CA>XN THEN XN=1:PRINT#4,"bad data":PRINT#4,I$:RETURN
9950 IF (VAL(I$)=0) AND (ASC(MID$(I$,CA,1))<>32) THEN 9945
9960 MM=WL
9965 XI=CA
9970 IF XI>XN THEN 9995
9975 CA=CA+1
9977 IF CA>=XN THEN 9985
9980 IF MID$(I$,CA,1)<>"," THEN 9975
9985 F(MM)=VAL(MID$(I$,XI,CA-XI))
9986 CA=CA+1
9990 MM=MM+1:IF MM=<WU THEN 9965
9995 XN=0:RETURN
9999 :
10000 REM dummy routine
10001 REM
10005 DATA dummy
65529 REM  **** proper last line *****
