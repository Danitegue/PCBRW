10000 REM ****************** fu routine 16/08/95 **********************
10010 REM                MKII/MKIII/MKIV Fast UV Scan
10020 REM
10030 REM             (SCI-TEC Instruments, August 1995)
10040 REM
10050 REM  NOTE: This is a test routine, for research purposes ONLY.
10060 REM *************************************************************
10070 REM
10200 REM  Side Effects:
10210 REM
10220 REM  Global Variables Referenced:
10230 REM    TBD
10250 REM
10260 REM  Exits: 11020, 13020
10270 REM
10280 REM  Uses:  TBD
10310 REM
10500 REM **************************************************************
10510 REM  History: dd/mm/yy
10520 REM  16/08/95 - Some small changes
10530 REM  07/04/95 - Initial release, constants specific to #107 are
10540 REM  found on line 33070
10550 REM **************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA fu
11020 IF Q8%=0 OR DN%<>8 THEN RETURN
11030 TR$="s"+"a":UC%=0:PRINT#4,:PRINT#4,"****  FAST UVB Scan  ****"
11040 GOSUB 2450                'Temperature
11050 GOSUB 6610:GOSUB 6635         'Filter#1 to 3
11060 M5$=UF$:GOSUB 6650            'Filter#2 to UF$
11070 GOSUB 6700                'Iris open
11080 GOSUB 7750:M1$=STR$(ER%*3/4):GOSUB 9870   'AZ/ZE to UVB
11090 LOCATE ,SP:PRINT "4 - Director prism to UVB sensor"
11100 TD=1500:GOSUB 7000:GOSUB 9650     'Wait until ready
12000 '
12001 ' *** Take FAST UVB Observations ***
12002 '
12010 CX$="4":CZ$=CX$:WL$="0":WU$="6":GOSUB 9700:GOSUB 8000:MS(0)=4
12020 GOSUB 4450:OPEN DD$+"dum.dta"FOR OUTPUT AS 8
12030 PRINT#8,"Int. time is 0.2294 sec. ";CR$;
12040 PRINT#8,"dt ";T1;CR$;"cy ";CX$;CR$;A1$(0);
12050 PRINT#8,"fu";CR$;" a";CR$;M5$;CR$;WL$;CR$;WU$;CR$;
12060 PRINT#4,"  TIME     ZA       S0      S1       S2       S3       S4       S5"
12070 GOSUB 20000
12080 B$=JD$+YF$+"."+NO$
12090 SHELL "copy "+DD$+"fu"+B$+"+"+DD$+"dum.dta "+DD$+"fu"+B$
12100 PRINT#4,"FU";B$;" data file extended at ";TIME$
13000 '
13001 ' *** Clean Up and Exit ***
13002 '
13010 GOSUB 4200:GOSUB 3220
13020 RETURN
20000 '
20001 ' *** Fast UVB Observation ***
20002 '
20010 GOSUB 50:GOSUB 9670:GOSUB 9700:GOSUB 9900     'Take a reading
20020 TI=TIMER*60:TM=TI:GOSUB 9450:TM=INT((TM+TI)/72)/100
20030 PRINT#8,TM,ZA,F(0),F(1),F(2),F(3),F(4),F(5),F(6);
20040 PRINT#4,USING "####.# ####.##";TM;ZA;
20050 PRINT#4,USING " ########";F(0),F(2),F(3),F(4),F(5),F(6)
20060 FOR I=WL TO WU:IF I=1 THEN 20090
20070   VA=F(I):IF I<>1 THEN GOSUB 8350
20080   FR3(I)=VA
20090 NEXT
25000 ' read Cal Response for each slit
25010 RESP(1)=1365:RESP(2)=9664:RESP(3)=8557:RESP(4)=7951:RESP(5)=6107:RESP(6)=4160
25015 FR3(1)=FR3(0)
25020 FOR I=1 TO 6:ERG(I)=FR3(I)/RESP(I):NEXT
25030 PRINT#4,"CORR. COUNTS  ";
25040 PRINT#4,USING " ########"; FR3(1),FR3(2),FR3(3),FR3(4),FR3(5),FR3(6)
25050 PRINT#4,"INT mw/m**2/nm";
25060 PRINT#4,USING "   ###.##"; ERG(1),ERG(2),ERG(3),ERG(4),ERG(5),ERG(6)
25070 PRINT#8,"end":CLOSE 8
25080 RETURN
65529 REM *** dummy line ***
