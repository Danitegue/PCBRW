10000 REM **************** testfw2 routine 08/04/96 *******************
10010 REM          MKII/MKIII/MKIV Polarimetric Measurements
10020 REM
10030 REM              (SCI-TEC Instruments, April 1996)
10040 REM
10050 REM *************************************************************
10060 REM
10070 REM  This routine tests and records the neutral densities at
10080 REM  the various filterwheel positions to the file FW2TEST.###.
10090 REM
10200 REM  Side Effects:
10210 REM
10220 REM  Global Variables Referenced:
10230 REM    SP  TD
10240 REM    CZ$ M5$ NO$ O1$ WL$ WU$
10250 REM    HF%
10260 REM
10270 REM  Exits: 13020
10280 REM
10290 REM  Uses: 6610, 6635, 6650, 6690, 6900, 7000, 8300, 9450,
10300 REM        9650, 9700, 9840, 9860, 9900
10310 REM
10500 REM *************************************************************
10510 REM  History: dd/mm/yy
10520 REM  08/04/96 - Output file is now appended
10530 REM  13/02/95 - Original release
10540 REM *************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA f3
11020 IF M9$<>"2" THEN GOSUB 9820   'Standard Lamp
11030 GOSUB 6610:GOSUB 6630     'Filter#1 to 1
11040 M5$="64":GOSUB 6650        'Filter#2 to 0
11050 GOSUB 6700:TD=1500:GOSUB 7000 'Iris open
11060 LOCATE ,SP:PRINT "4 - Rotate director prism to lamps":GOSUB 9860
11070 GOSUB 9650:T0=TA/3600:GOSUB 8600  'ENTER when ready
11080 PRINT CL$:LOCATE ,SP:PRINT"Waiting until ";H$;" for lamp warmup":GOSUB 6900
11090 IF LO%=1 THEN 13000       'End if Lamp is Off
11100 GOSUB 6800:IF HF%=1 THEN 13000    'End if Aborted
11110 GOSUB 2450            'Get Temperature
12000 '
12001 ' *** Take Set of Observations ***
12002 '
12010 CLS:PRINT CL$:GOSUB 9670
12020 CLOSE 8:OPEN DD$+"f3"+JD$+YE$+"."+NO$ FOR APPEND AS 8
12030 PRINT#8,"  slit to scan conversion - Brewer #";NO$;" - ";DC$;" (";JD$;")"
12070 DS%=1:CZ$="10"
12100 GOSUB 20000
12110 M8$="-"+MC$:GOSUB 9805:M8$=STR$(-ZERO):GOSUB 9805
12120 SQ=1:SW=3000:EW=3250:DW=10:DQ$=STR$(SQ+1):CX$="10"
12122 FOR WV=SW TO EW STEP DW
12124   GOSUB 32000:IF HF%=1 THEN WV=EW
12126 NEXT
12128 M8$=STR$(ZERO+1):GOSUB 9805:M8$="-1":GOSUB 9805:M8$=MC$:GOSUB 9805:GOSUB 9500
12155 DS%=1:GOSUB 20000
13000 '
13001 ' *** Clean Up and Exit ***
13002 '
13010 PRINT#8,:CLOSE 8:GOSUB 9840
13020 RETURN
20000 '
20001 ' *** Filterwheel #2 Observation ***
20002 '
20010 O1$="M,5,"+MID$(STR$(DS%*64),2):GOSUB 9450    'set FW#2
20020 WL$="0":WU$="6":GOSUB 9700:GOSUB 9900    'get data
20030 IF HF%=1 THEN PRINT CL$:PRINT "TESTFW#2 measurement terminated":RETURN
20040 REM GOSUB 8300                    'correct data for dark/dead/temperature
20045 I=(320-VAL(M4$))/64:GOSUB 21000
20050 RETURN
21000 '
21001 ' *** Print Out Data ***
21002 '
21030 PRINT#8,I;DS%;F(0);" ";F(1);" ";F(2);" ";F(3);" ";F(4);" ";F(5);" ";F(6)
21040 RETURN
32000 '
32001 ' *** Single Scan ***
32002 '
32020 GOSUB 8500:GOSUB 2090:IF HF%=1 OR M1=0 OR (M2=0 AND TYP$="mkiii") THEN 32500
32030 IF TYP$="mkiii" THEN O1$=SE$+"M,"+N9$+","+STR$(M2) ELSE O1$=""
32040 O1$="M,10,"+STR$(M1)+O1$+":R,"+DQ$+","+DQ$+","+CX$+":O"
32050 VA=0:GOSUB 9450:TI=TIMER*60:TM=TI:TM=INT((TM+TI)/72)/100
32060 IF TS=0 AND WV=3250 THEN TS=TM                    'start time
32090 PRINT#8,TM;CR$;WV;CR$;STR$(M1);CR$;VA        'record to file
32110 PRINT USING "####.##  ####  #####   #######.##";TM;WV;M1;VA
32500 REM
32510 TTIM=TTIM+TINC:RETURN
65529 REM proper last line
