10000 REM ***************** hg routine 12/08/98 *****************
10010 REM      MKII/MKIII/MKIV Mercury Lamp Calibration (O3)
10020 REM        
10030 REM           (SCI-TEC Instruments, August 1998)
10040 REM        
10060 REM *******************************************************
10070 REM
10080 REM  This routine performs a mercury lamp calibration.
10090 REM
10120 REM  Global Variables Referenced:
10130 REM    A1$() F() G$() MS()
10140 REM    OZFW1 PC POFW2 TA TD TI
10150 REM    HF% LO% MY% MZ% NMZ% OF% Q13% R% RM% TE%
10160 REM    C$ CL$ CZ$ H$ M8$ MC$ MDD$ O1$ TYP$ WL$ WU$
10170 REM      
10180 REM  Exits: 11020, 11350
10190 REM
10200 REM  Uses:  50, 1990, 2080, 2090, 2450, 3200, 3230, 3900,
10210 REM         6610, 6630, 6650, 6700, 6800, 6850, 6900, 8000,
10220 REM         8600, 9450, 9650, 9670, 9710, 9805, 9830, 9840,
10230 REM         9860, 9900, 9940
10240 REM
10500 REM ********************************************************
10510 REM  History: dd/mm/yy
10520 REM  22/05/98 - Q9% support added; fixed Q14% support
10530 REM  05/05/96 - Added more diagnostic info; fixed reset
10540 REM  21/07/95 - Corrected MIOAVG file output; reduced size
10550 REM  19/01/95 - Now supports new Brewer electronics
10560 REM  04/01/95 - HG now writes comments to B file
10570 REM  05/10/94 - HG sequence is HG-FR-HG-abort
10580 REM ********************************************************
11000 '
11010 IF MDD$="o3" THEN 11100
11020 B$="HG is not an NO2 Routine":PRINT B$:PRINT#4,B$:X=1:GOSUB 7220:RETURN
11100 '
11101 ' *** Main Loop ***
11102 '
11110 DATA hg
11120 IF Q13%=1 THEN 11200
11130   REM Narrow HG slit spectrum
11131   SS(9) =96  :SS(10)=588 :SS(11)=1223:SS(12)=1850:SS(13)=2419:SS(14)=2610
11132   SS(15)=2742:SS(16)=2477:SS(17)=1937:SS(18)=1362:SS(19)=686 :SS(20)=373
11133   REM Narrow HG slit spectrum for 2967line
11140   SS(9) =24  :SS(10)=227 :SS(11)=1242:SS(12)=2451:SS(13)=3593:SS(14)=4150
11150   SS(15)=4182:SS(16)=4029:SS(17)=3036:SS(18)=1887:SS(19)=705 :SS(20)=57
11160   GOTO 11300
11200   REM Wide HG slit spectrum
11210   SS(9) =923 :SS(10)=1321:SS(11)=1695:SS(12)=2101:SS(13)=2518:SS(14)=2665
11220   SS(15)=2719:SS(16)=2559:SS(17)=2276:SS(18)=1917:SS(19)=1527:SS(20)=1162
11230 REM 27 6 01 julian new slitfunction:#163
11240   SS(9)=4120  :SS(10)=6290:SS(11)=8301:SS(12)=10209:SS(13)=12077:SS(14)=13212
11250   SS(15)=13342:SS(16)=12460:SS(17)=10712:SS(18)=8793:SS(19)=6725:SS(20)=4644
11300 SUMSS=0:SUMS=0:FOR I=9 TO 20:SUMSS=SUMSS+SS(I)^2:SUMS=SUMS+SS(I):NEXT:SA=SUMSS
11310 PRINT#4,:PRINT#4,"**** HG Calibration ****"
11320 PRINT CL$:GOSUB 20000
11330 GOSUB 2080:GOSUB 3230:IF RM%=0 OR HF%=1 OR FH%=1 THEN GOSUB 9840
11340 FH%=0:IF G$(JJ+1)<>"lo" THEN GOSUB 9840
11350 RETURN
20000 '
20001 ' *** HG Wavelength Calibration ***
20002 '
20010 IF M9$<>"1" THEN GOSUB 9830
20016 IF TE%>16 THEN TA=1800:GOSUB 7050
20018 IF TE%>13 THEN TA=3600:GOSUB 7050
20020 GOSUB 6610:M4$=STR$(OZFW1):GOSUB 6660:M5$=STR$(POFW2):GOSUB 6650:GOSUB 9860
20030 GOSUB 6700:TI=TIMER*60:TD=TI+1500
20040 LOCATE ,SP:PRINT "4 - Rotate director prism to lamps":GOSUB 9650
20050 T0=TA/3600:GOSUB 8600:PRINT CL$:LOCATE ,SP
20060 PRINT "Waiting until ";H$;" for lamp warmup":GOSUB 6900
20070 IF LO%=0 THEN 20110
20080   LO%=0:B$="Lamp not detected - resetting micrometer(s)"
20090   PRINT#4,B$:GOSUB 3050:GOSUB 22000:IF TYP$="mkiii" THEN GOSUB 9830
20100   GOSUB 6900:IF LO%=1 THEN HF%=1:RETURN
20110 GOSUB 6800:IF HF%=1 THEN RETURN 
20120 GOSUB 2450:HLAST%=TE%:HTIME$=TIME$:GOSUB 3900:GOSUB 50
20500 REM
20501 REM Begin
20502 REM
20510 D7 = 4: REM the max passes looking for convergence before mic reset
20520 WL$="0":WU$="0":CZ$="4":REM set to hg wavelength
20530 IF HF%=1 THEN GOSUB 2090: PRINT CL$;C$;" terminated":RETURN
20540 M8$="-"+MC$:GOSUB 9805:GOSUB 8000:REM move micrometer back and zero sums
20550 W1=50:W2=240:WS=10:GOSUB 30000:REM scan forward
20560 IF HF%=1 THEN GOSUB 2090:PRINT CL$;C$;" terminated":M8$=MC$:GOSUB 9805:RETURN
20570 W1=240:W2=50:WS=-10:GOSUB 30000:REM scan backward
20580 IF HF%=1 THEN GOSUB 2090:PRINT CL$;C$;" terminated":M8$=MC$:GOSUB 9805:RETURN
20590 REM
20591 REM Compare measured scan to standard
20592 REM
20600 NOISE=1.0E10
20610 FOR J=0 TO 4
20620   SUMMM=0:SUMM=0:SUMSM=0
20630   FOR I=9 TO 20
20640     K=I+2*J-4:IF MS(K) > 0 AND NOISE > MS(K) THEN NOISE = MS(K)
20650     SUMSM=SUMSM+SS(I)*MS(K):SUMMM=SUMMM+MS(K)*MS(K):SUMM=SUMM+MS(K)
20660   NEXT
20670   RNOISE=(SUMS*SUMMM-SUMSM*SUMM)/(SUMS*SUMM-12*SUMSM)
20680   IF RNOISE<NOISE AND RNOISE>10 THEN NOISE=RNOISE
20690   S(J)=(SUMSM-NOISE*SUMS)^2/(SUMSS*(SUMMM-2*NOISE*SUMM+12*NOISE^2))
20700   IF J=2 THEN SNR = SUMM/(12*ABS(NOISE))
20710   LOCATE JC+2+J,JR:PRINT S(J)
20720 NEXT
20730 REM
20731 REM Find best fit
20732 REM
20740 D3=0
20750 FOR I=0 TO 2
20760   D1=S(I)-S(I+1):D2=S(I+1)-S(I+2):IF D1<0 AND D2>0 THEN D3=D1:D4=D2:CA=I+1
20770 NEXT
20780 IF D3=0 OR SNR<4 THEN 20820
20790   CC=(D3-D4)/2:BC=-D3-CC*(2*CA-1):AC=S(CA)-BC*CA-CC*CA*CA
20800   CA=-BC/2/CC:C5=AC+BC*CA+CC*CA*CA
20810   C5=INT(P4%*C5)/P4%:D5=CA*20+VAL(MC$)-40:GOTO 20860
20820   D5=0:D6=0:C5=0
20830   FOR I=5 TO 28
20840     IF MS(I)>D6 THEN D6=MS(I):D5=10*I-OF%
20850   NEXT
20860 M8$=STR$(INT(D5+0.5)):GOSUB 9805:GOSUB 6850
20870 LOCATE 23,5:PRINT USING"est step####.## target \  \";D5;MC$;
20880 PRINT#4,USING"\      \ ( #.#### ) ";TIME$;SQR(C5);
20890 PRINT#4,USING" corr est for step####.## target is step \  \";D5;MC$;
20900 PRINT#4,USING"####### ###.##";MS(15);SNR
20905 PRINT#4,USING" micro is moved by ### steps";-VAL(MC$)+INT(D5+.5)
20910 TG%=TE%:TH%=1:IF R%=1 THEN RETURN
20920 A1$(IO)=LF$+"hg"+CR$+TIME$+CR$+STR$(C5)+CR$+STR$(D5)+CR$+M8$+CR$
20930 A1$(IO)=A1$(IO)+STR$(MS(15))+CR$+STR$(TE%)+CR$
20940 IF IO>15 THEN GOSUB 3200
20950 IO=IO+1
20960 IF C5>.9 AND SNR>4 AND ABS(VAL(M8$)-VAL(MC$))<1.5 THEN GOSUB 3200:GOSUB 9805:RETURN
20970 IF D7<1 THEN M8$=MC$:GOSUB 9805:GOSUB 6850:GOSUB 31000:GOSUB 3200:GOTO 20510
20980 D7=D7-1: GOTO 20520
22000 '
22001 ' *** Perform Search and Reset Micrometer (#1) ***
22002 '
22010 B$="Reset for "+TYP$+" "+MDD$+" - Searching for micrometer #1 reference"
22020 PRINT#4,:PRINT#4,B$:PRINT B$:Y=0:VA=0
22030 IF Q14%=1 THEN FLAG=1:M1$="10":GOSUB 6550:GOTO 22210
22040 O1$="R,1,1,1:G,544":GOSUB 9450:GOSUB 9190:IF (VA AND 4)<>4 THEN GOTO 22070
22050 O1$="M,10,-500;G,544":Y=Y-500:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22060 IF Y<=-3000 THEN B$= "-3000 steps - QUIT - Micrometer #1 jammed at MIN. end ?":PRINT#4,B$:GOSUB 3050:GOTO 23000
22070 IF (VA AND 4)=4 THEN 22050
22080 O1$="M,10,-1:M,10,501:G,544":Y=Y-1+501:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22090 IF Y>12000 THEN B$= "12000 steps - QUIT - Micrometer #1 reference not found":PRINT#4,B$:GOSUB 3050:GOTO 23000
22100 IF (VA AND 4)=0 THEN 22080
22110 O1$="M,10,-500":GOSUB 9450
22120 Y=Y-500
22130 O1$="M,10,-1:M,10,51:G,544":Y=Y-1+51:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22140 IF (VA AND 4)=0 THEN GOTO 22130
22150 O1$="M,10,-100":GOSUB 9450
22160 Y=Y-100
22170 YY%=0
22180 YY%=YY%+1
22190 O1$="M,10,"+MID$(STR$(YY%),2)+":G,544":Y=Y+1:LOCATE 22,30:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22200 IF (VA AND 4)=0 THEN GOTO 22180
22210 IF MDD$="o3" THEN 22220 ELSE 22300
22220    B$="Micrometer #1 reference found at step"+STR$(Y)+"(+CAL)="+STR$(INT(Y)+VAL(MC$)):IF Q14%=1 THEN B$="Micrometer #1 discrepancy="+STR$(-Y)
22230    VAR4$=STR$(INT(MZ%-PC*TE%+.5)):B$=B$+" Now set to "+MDD$+" offset ="+VAR4$
22240    PRINT#4,B$:PRINT B$
22250    IF Q14%=0 THEN M8$="-"+VAR4$:GOSUB 9810
22260    IF Q14%=1 THEN M8$="0":GOSUB 9810:M8$=STR$(1-INT(MZ%-PC*TE%+.5)):GOSUB 9810:M8$="-1":GOSUB 9810
22270    M8$=MC$:GOSUB 9810:GOSUB 3050:GOTO 22400
22300    B$="Micrometer #1 reference found at step"+STR$(Y)+"(+CAL)="+STR$(INT(Y)+VAL(MC$)):IF Q14%=1 THEN B$="Micrometer #1 discrepancy="+STR$(-Y)
22310    VAR4$=STR$(INT(NMZ%-PC*TE%+.5)):B$=B$+" Now set to "+MDD$+" offset ="+VAR4$
22320    PRINT#4,B$:PRINT B$
22330    IF Q14%=0 THEN M8$="-2000":GOSUB 9810:M8$="-"+STR$(INT(NMZ%-2000-PC*TE%+.5)):GOSUB 9810
22340    IF Q14%=1 THEN M8$="0":GOSUB 9810:M8$=STR$(1-INT(NMZ%-PC*TE%+.5)):GOSUB 9810:M8$="-1":GOSUB 9810
22350    M8$=MC$:GOSUB 9810:GOSUB 3050
22400 PRINT#4," ":VAR3$=STR$(Y+VAL(M8$)):IF Q14%=1 THEN VAR3$=STR$(Y)
23000 IF (VA AND 8)=8 THEN B$= "MICROMETER #1 APPEARS TO BE JAMMED AT MAX. END, You must re-position by HAND":PRINT#4,B$:GOSUB 3050
23010 IF TYP$="mkiii" THEN GOSUB 24000
23020 IF TYP$<>"mkii" AND Q9%=1 THEN GOSUB 26000
23030 RETURN
24000 '
24001 ' *** Reset Micrometer #2 ***
24002 '
24010 B$="Searching for micrometer #2 reference":PRINT#4,B$:PRINT B$:Y=0:VA=0
24015 IF Q14%=1 THEN FLAG=1:M1$=N9$:GOSUB 6550:GOTO 24200
24020 O1$="R,1,1,1:G,800":GOSUB 9450:GOSUB 9190:IF (VA AND 32)<>0 THEN 24060
24030   O1$="M,6,-500;G,800":Y=Y-500:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24040   IF Y<-3000 THEN B$= "-3000 steps - QUIT - Micrometer #2 jammed at MIN end ?":PRINT#4,B$:GOSUB 3050:GOTO 25000
24050   IF (VA AND 32)=0 THEN GOTO 24030
24060 O1$="M,6,-1:M,6,501:G,800":Y=Y-1+501:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24070 IF Y>12000 THEN B$= "12000 steps - QUIT - Micrometer #2 reference not found":PRINT#4,B$:GOSUB 3050:GOTO 25000
24080 IF (VA AND 32)=32 THEN 24060
24090 O1$="M,6,-500":GOSUB 9450
24100 Y=Y-500
24110 O1$="M,6,-1:M,6,51:G,800":Y=Y-1+51:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24120 IF (VA AND 32)=32 THEN 24110
24130 O1$="M,6,-100":GOSUB 9450
24140 Y=Y-100
24150 YY%=0
24160 YY%=YY%+1
24170 O1$="M,6,"+MID$(STR$(YY%),2)+":G,800":Y=Y+1:LOCATE 22,30:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24180 IF (VA AND 32)=32 THEN 24160
24200 B$="Micrometer #2 reference found at step"+STR$(Y):IF Q14%=1 THEN B$="Micrometer #2 discrepancy="+STR$(-Y)
24210 VAR6$=STR$(INT(MY%-PC*TE%+.5)):B$=B$+" Now set to offset ="+VAR6$
24220 PRINT#4,B$:PRINT B$
24230 IF Q14%=0 THEN M8$="-"+VAR6$:GOSUB 9815:M8$="-"+MC$:GOSUB 9815
24240 IF Q14%=1 THEN M8$="0":GOSUB 9815:M8$=STR$(1-INT(MY%-PC*TE%+.5)):GOSUB 9815:M8$="-1":GOSUB 9815
25000 M8$=MC$:GOSUB 9815:GOSUB 3050:PRINT#4," ":VAR5$=STR$(Y)
25010 RETURN
26000 '
26001 ' *** Zero Filterwheel #3 ***
26002 '
26010 IF Q14%=1 THEN FLAG=1:M1$="6":GOSUB 6550:GOTO 26200
26020 IF TYP$="mkiii" THEN O1$="M,10,-"+MC$:GOSUB 9450:REM Remember pos of first micrometer
26030 B$="Zeroing filterwheel #3":PRINT CL$;B$:PRINT#4,B$:Y=9
26040 IF TYP$="mkiv" THEN O1$="M,6,-1:M,6,10:G,800" ELSE O1$="B,3:M,10,-1:M,10,10:G,544"
26050 GOSUB 9450:GOSUB 9190
26060 YY=VA:PRINT Y:O1$="-25":GOSUB 28000
26070 IF Y<-600 THEN PRINT#4,"Filterwheel #3 reference not found":GOTO 27000
26080 IF(YY-VA)<>32 THEN 26060
26090 O1$="25":GOSUB 28000
26100 YY=VA:PRINT Y:O1$="-10":GOSUB 28000:IF(YY-VA)<>32 THEN 26100
26110 O1$="10":GOSUB 28000
26120 YY=VA:PRINT Y:O1$="-1" :GOSUB 28000:IF(YY-VA)<>32 THEN 26120
26200 B$="Found reference at step "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
26210 IF Q14%=1 THEN B$="FW#3 discrepancy "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
26220 IF MDD$="o3" THEN B$=B$+STR$(MX%) ELSE B$=B$+STR$(NMX%)
26230 PRINT B$:PRINT#4,B$:GOSUB 3050:PRINT#4,
27000 '
27010 VAR7$=STR$(Y)                 'Step for FW#3
27020 IF TYP$="mkiii" AND Q14%=0 THEN 27030 ELSE 27070
27030   VAR8$=STR$(MX%)                 'FW#3 Position
27040   O1$="M,10,"+VAR8$:GOSUB 9450:GOSUB 9840:O1$="M,10,-1;M,10,2;M,10,-1:M,10,"+MC$
27060   GOSUB 9450:GOTO 27200:REM Restore first micrometer pos
27070 IF MDD$="o3" THEN 27080 ELSE 27100
27080   VAR8$=STR$(MX%)                 'FW#3 Position
27090   O1$="M,6,"+VAR8$:GOSUB 9450:GOTO 27200
27100   VAR8$=STR$(NMX%)                'FW#3 Position
27110   O1$="M,6,"+VAR8$:GOSUB 9450
27200 RETURN
28000 Y=Y+VAL(O1$)
28010 IF TYP$="mkiv" THEN O1$="M,6,"+O1$+":G,800" ELSE O1$="B,3:M,10,"+O1$+":G,544"
28020 GOSUB 9450:GOSUB 9190:RETURN
30000 '
30001 ' *** Wavelength Scan Routine ***
30002 '
30010 GOSUB 9670:M8$=STR$(W1):GOSUB 9805:GOSUB 9710
30020 JN=0:JR=2:FOR MI=W1+WS TO W2 STEP WS
30030   GOSUB 2090:IF HF%=1 THEN MI=W2:GOTO 30120
30040   LI=(MI-WS)/ABS(WS)
30050   O1$="O:M,10,"+MID$(STR$(MI),2):IF TYP$="mkiii" THEN O1$=O1$+SE$+"M,"+N9$+","+MID$(STR$(MI),2)
30060   O1$=O1$+":R":GOSUB 1990:GOSUB 9450:GOSUB 9940
30070   IF XN<>1 THEN 30100
30080     O1$="O:M,10,"+MID$(STR$(LI*ABS(WS)),2):IF TYP$="mkiii" THEN O1$=O1$+SE$+"M,"+N9$+","+MID$(STR$(LI*ABS(WS)),2)
30090     O1$=O1$+":R":GOSUB 9450:GOTO 30030
30100   JC=10+JN:IF JC=22 THEN JR=JR+15:JN=0:JC=10
30110   MS(LI)=MS(LI)+F(WL):LOCATE JC,JR:PRINT LI;MS(LI):JN=JN+1
30120 NEXT:IF HF%=1 THEN RETURN
30130 GOSUB 9900:MS(W2/ABS(WS))=MS(W2/ABS(WS))+F(WL)
30140 JC=10:JR=JR+15:RETURN
31000 '
31001 ' *** Measure Offset ***
31002 '
31010 IF MZ%<>0 THEN 31100
31020   B$="HG failed - no offset constant":PRINT#4,B$:GOSUB 3050
31030   HF%=1:IF RM%=0 THEN GOSUB 9805:GOSUB 3230:FH%=0
31040   IF G$(JJ+1)<>"lo" THEN GOSUB 9840
31050   RETURN
31100 T0=TIMER/60:GOSUB 8600:IF FH%<1 THEN 31200
31110   HF%=1:B$= "*** Micrometer reset attempted - HG cal still failed":PRINT#4,B$:GOSUB 3050
31120   GOSUB 32000:RETURN
31200 B$="HG cal too far off limits - resetting micrometer(s)"
31210 FH%=FH%+1:PRINT#4,B$:GOSUB 3050
31220 GOSUB 22000
31230 RETURN
32000 '
32001 ' *** Diagnostic Message ***
32002 '
32010 B$="HG failed - standard offset apparently incorrect"
32020 PRINT#4,B$:GOSUB 3050
32030 PRINT#4,"Check micrometer mechanism and reset by hand **":RETURN
65529 REM proper last line
