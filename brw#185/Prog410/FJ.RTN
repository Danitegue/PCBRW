10000 REM ****************** ds routine 15/09/05 **********************
10010 REM            MKII/MKIII/MKIV Direct Sun Observations
10020 REM
10030 REM            (SCI-TEC Instruments, September 1995)
10040 REM
10060 REM *************************************************************
10070 REM
10080 REM  Direct sun scan
10090 REM
10220 REM  Global Variables Referenced:
10230 REM    MS() DS% HF% UC% ND TD XN
10240 REM    CL$ CY$ CZ$ M5$ MDD$ SQ$ TR$ TYP$ ZS$
10250 REM
10260 REM  Exits: 13020
10270 REM
10280 REM  Uses:  2450, 3200, 3220, 4200, 6610, 6630, 6636, 6650,
10290 REM         6690, 7000, 7500, 7750, 8000, 8200, 9650, 9670,
10300 REM         9700, 9900
10310 REM
10500 REM **************************************************************
10510 REM  History: dd/mm/yy
10515 REM  15/09/05 AOD measurement added by Volodya
10520 REM  27/09/95 - Aborts if counts are too high, fixed home key
10530 REM  abort and temperature check
10540 REM  20/12/94 - Rewritten by TM
10550 REM **************************************************************
11000 '
11001 ' *** Setup ***
11010 DATA fj
11020 TR$="d"+"s":UC%=0:C$="fj"
11030 IF VAL(SQ$)<128 AND M2<2 THEN SQ$="128"
11050 GOSUB 6610:IF MDD$="o3" THEN GOSUB 6630 ELSE GOSUB 6636   'Filter#1 to 1 or 4
11060 M5$=SQ$:GOSUB 6650:GOSUB 2450                    'Filter#2 to SQ$
11070 GOSUB 6690:GOSUB 7750                 'Iris closed, AZ/ZE to sun
11080 LOCATE ,SP:PRINT "4 - Point Brewer at sun"
11090 GOSUB 9650:GOSUB 7500:SQ$=M5$:          'Wait until ready, test intensity
12000 '
12001 ' *** Take Set of Observations ***
12002 '
12003 DS%=0
12010 IF(VAL(SQ$)>0) THEN M5$ = STR$(VAL(SQ$)-64)
12012 GOSUB 6650:CZ$=CY$:WL$="0":WU$="7":GOSUB 9700
12030 DS%=DS%+1:GOSUB 20000
12032 M5$ = SQ$: GOSUB 6650:gosub 9700:GOSUB 20000
12034 IF(VAL(SQ$)<320) THEN M5$ = STR$(VAL(SQ$)+64)
12038 GOSUB 6650:gosub 9700:GOSUB 20000
12040 M5$ = SQ$: GOSUB 6650:gosub 9700:GOSUB 20000
12050 IF HF%=1 OR DS%=3 THEN 13000
12060 GOTO 12010
13000 '
13001 ' *** Clean Up and Exit ***
13010 'GOSUB 4200:GOSUB 3220
13020 RETURN
20000 '
20001 ' *** Direct Sun Observation ***
20002 '
20010 GOSUB 9670:GOSUB 9900                 'Get data when ready
20020 IF F(6)>4000000! OR HF%=1 THEN DS%=3:RETURN    'Terminate measurement
20022 PRINT#4, "FW2 at position ", M5$," ";:FOR S%=0 TO 7: PRINT#4, F(S%);:NEXT:PRINT#4,
20030 rem IF XN=1 OR DS%<5 THEN GOSUB 9700              'Take a reading
20040 IF XN=1 THEN 20010:REM restart obs
20060 GOSUB 3200                        'Output buffer if needed
20070 ND=ND+1:RETURN 
65529 REM *** dummy line ***
