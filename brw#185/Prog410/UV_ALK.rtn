10000 REM ****************** uv routine 04/04/2002 *******************
10010 REM                                        MKIII UV Scan
10020 REM Changes by Alkis to match the old sofware. Marked as 'AB
10030 REM            (SCI-TEC Instruments, October 1995)
10040 REM
10050 REM ************************************************************
10070 REM
10080 REM  This routine performs a UV-B scan covering the range as
10090 REM  found in the following chart:
10100 REM
10110 REM  Model   Slit #1 Range   Slit #5 Range
10120 REM
10130 REM
10140 REM  MKIII   290.0 - 351.5 nm  352 - 365.5 nm
10150 REM
10200 REM
10210 REM  The variable MDD2$ is used as a flag to indicate "standard",
10220 REM  "extended", "fast", or "timed" scan mode.
10300 REM
10310 REM  Global Variables Referenced:
10320 REM    IO MU PC SP TIMER T1 ULAST VA ZA ZERO
10330 REM    A1$() C$ CR$ DD$ DQ$ JD$ MC$ M8$ NO$ O1$ TE$
10340 REM    TIME$ TR$ UF$ UTIME$ UVR$ YF$
10350 REM    DN% DV% ER% HF% MX% MZ% Q8% TE% UC%
10360 REM
10370 REM  Exits:11020, 11120, 11220, 20020
10380 REM
10390 REM  Uses:50,   2090, 2450, 3100, 3900, 4450, 6610, 6635,
10400 REM        6650, 6700, 7000, 7750, 9190, 9450, 9500, 9650,
10410 REM        9800, 9810, 9870, response file
10420 REM        9770, 9740, 9785, 9750
10500 REM ************************************************************
10501 REM  History:dd/mm/yy
15010 REM  30/11/2004 - Alkis 34050 M1$ = ZU% instead of ER%*3/4  
10511 REM  04/09/2002 - Alkis Direct sampling used ND 1 for WV >3100 and at 2900 defaults to 0
10512 REM  06/05/2002 - Alkis Direct sun sampling is chosen by the operator
10513 REM  15/02/2001 - Alkis UZ for UX scans to start at next integer SZA
10514 REM  22/06/2000 - Alkis changes the switch wavelength from 3530 to 3515 A
10515 REM  01/06/97 - Calls fr.rtn (and hp, hg) if micrometer is at diodes
10520 REM  28/09/95 - Supports all Brewer and response file types+speedup
10530 REM  11/04/95-Fixed all DUV calculations; improved UA scans
10540 REM  31/03/95-Added missing UVA correction to MKIII Brewer
10550 REM  02/02/95-Now supports new Brewer electronics
10560 REM  12/01/95-Uses UVR$ for response file name
10570 REM  08/11/94-Combined UA, UV and UX into one routine; writes
10580 REM  all data to single UVdddyy.nnn file
10590 REM ************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA uv: IF micf% = 1 THEN READ pc$
11011 GOSUB 8025: MZ(0) = 0: dsflg = 1
11015 nd0 = -.27083: nd1 = .0003261#: nd2 = -.000000043243#:  ' Constants for ND filter #1
11020 IF Q8% = 0 OR DN% <> 8 THEN RETURN
11021 IF rm% = 1 THEN 11029
11022 PRINT "Direct sun sampling? (Y/N)"
11023 a$ = INKEY$: IF a$ = "" THEN 11023
11024 IF a$ = "Y" OR a$ = "y" THEN MZ(0) = 0: dsflg = 1 ELSE MZ(0) = 8: dsflg = 0: GOTO 11030
11029 '
11030 IF LEN(MDD2$) <> 2 THEN MDD2$ = "uv"
11040 c$ = MDD2$: IF c$ = "ux" THEN MDD2$ = "extended "
11050 IF c$ = "ua" THEN MDD2$ = "timed "
11055 IF c$ = "uc" THEN MDD2$ = "timed_extended "
11060 IF c$ = "uv" THEN MDD2$ = "standard "
11070 IF c$ = "uf" THEN MDD2$ = "fast "
11075 IF c$ = "uz" THEN MDD2$ = "sza_extended "
11080 IF TYP$ = "mkii" OR (TYP$ = "mkiv" AND ZERO = 3469) THEN XR = 0 ELSE XR = 1
11100 IF MDD$ = "o3" THEN 11200
11110   B$ = "UV scans cannot be used in NO2 mode": PRINT #4, B$: PRINT B$
11120   X = 2: GOSUB 7220: RETURN
11200 IF c$ = "ua" OR c$ = "uc" OR c$ = "uf" OR c$ = "uv" OR XR = 1 THEN 12000
11210   B$ = "Extended range scans cannot be used on this Brewer": PRINT #4, B$: PRINT B$
11220   X = 2: GOSUB 7220: RETURN
12000 '
16000 '
16001 ' *** Set Up Data Header ***
16002 '
16010 UC% = 0: cx$ = "1": TR$ = "s" + "a": X$ = "6": IF Q14% = 1 THEN X$ = "9"
16020 GOSUB 2450: GOSUB 4450
16030 CLOSE 8: OPEN DD$ + "dum.dta" FOR OUTPUT AS 8
16040 PRINT #8, c$; CR$; "Integration time is 0.2294 seconds per sample"; CR$; "dt "; T1; CR$;
16050 IF c$ = "uv" OR c$ = "uf" THEN PRINT #8, "cy "; cx$; CR$; A1$(0); : GOTO 16070
16060   PRINT #8, "cy 1"; CR$; A1$(0);
16070 GOSUB 6610: GOSUB 6635: M5$ = UF$: GOSUB 6650: GOSUB 6700
16080 LOCATE , SP: PRINT "4-Brewer azimuth to sun"
16090 GOSUB 7750
16100 LOCATE , SP: PRINT "5-Director prism to UVB sensor"
16110 M1$ = STR$(ZU%): GOSUB 9870: GOSUB 9650
17000 '
17001 ' *** Begin Scanning Procedure ***
17002 '
17005 O1$ = "L,19414,90": GOSUB 9450: 'speed up the motors
17010 M8$ = "-" + MC$: GOSUB 9805: M8$ = STR$(-ZERO): GOSUB 9805: SW = 2900
17020 IF c$ <> "ua" AND c$ <> "uc" THEN 17100: 'AB
17030   SQ = 1: wv = SW: GOSUB 8500
17040   O1$ = "X," + STR$(M1) + "," + STR$(M2)
17045   tinc = 3: IF c$ = "uc" THEN tinc = 1
17050   GOSUB 9450: stim = VAL(LEFT$(TIME$, 2)) + .5: IF VAL(MID$(TIME$, 4, 2)) > 29 THEN stim = stim + .5
17060   PRINT CL$: LOCATE 5, 1: PRINT "UV "; MDD2$; "scan will begin at "; stim; " hours C.U.T."
17065   DRKOK = 0
17070 GOSUB 50: GOSUB 9891: GOSUB 2090: IF HF% = 1 THEN 18000
17075   IF TA% = 1 THEN GOSUB 33000: stim = (TIMER - 20) / 3600: GOTO 17100
17080   X = VAL(LEFT$(TIME$, 2)) + VAL(MID$(TIME$, 4, 2)) / 60 + VAL(RIGHT$(TIME$, 2)) / 3600: IF X < 1 THEN X = X + 24
17084   IF X < (stim - 1 / 60) THEN 17070' Wait for dark current measurement time
17085   IF DRKOK = -1 THEN 17090
17086 GOSUB 33000: DRKOK = -1'dark current 1 min before ua scan
17090   IF X + 2 / 3600 < stim THEN 17070
17100 B$ = "UV " + MDD2$ + "scan started at " + TIME$: CLS : PRINT CL$: LOCATE 4, 2
17110 PRINT #4, B$: PRINT B$: GOSUB 50
17115 IF c$ <> "ua" AND c$ <> "uc" THEN GOSUB 33000
17200 TTIM = stim * 3600: TS = 0: SQ = 1: SW = 2900: EW = 3250: DW = 5 'scan on slit 1
17210 IF c$ = "ua" OR c$ = "uc" OR c$ = "ux" OR c$ = "uz" THEN EW = 3515: SW1 = SW
17220 GOSUB 30000: IF HF% = 1 THEN 18000     'forward
17230 IF c$ = "uv" THEN GOSUB 31000: IF HF% = 1 THEN 18000'dark+reverse
17240 IF c$ = "uf" OR c$ = "uv" OR XR = 0 THEN 18000
17300 SQ = 5: SW = 3520: EW = 3655: DW =5  'scan on slit 5
17340 REM TTIM=TTIM+16-TINC:
17345 GOSUB 30040: IF HF% = 1 THEN 18000'forward
18000 '
18001 ' *** End Scanning Procedure ***
18002 '
18010 PRINT #8, "end"
18012 PRINT #8, "dr"; MZ(0); CR$; : FOR i% = 1 TO MZ(0): PRINT #8, MZ(i%); : NEXT: PRINT #8, : ' AB
18013 CLOSE 8
18015 IF HF% = 1 THEN PRINT "Aborted": PRINT #4, "UV "; MDD2$; "scan aborted at "; TIME$
18020 M8$ = STR$(ZERO + 1): GOSUB 9805: M8$ = "-1": GOSUB 9805: M8$ = MC$: GOSUB 9805: GOSUB 9500
18025 O1$ = "L,19414,255": GOSUB 9450: 'speed down the motors
18100 IF HF% = 0 THEN 19000
18120   X = 1: GOSUB 7220: GOTO 20000
19000 '
19001 ' *** Record all data ***
19002 '
19200 B$ = JD$ + YF$ + "." + NO$
19210 PRINT #4, "UV "; MDD2$; "scan finished at "; TIME$
19220 SHELL "copy " + DD$ + "uv" + B$ + "+" + DD$ + "dum.dta " + DD$ + "uv" + B$
19230 PRINT #4, "UV"; B$; " data file extended at "; TIME$
20000 '
20001 ' *** Clean Up and Exit ***
20002 '
20010 UR(1) = 1: ERASE UR: ON ERROR GOTO 3100
20020 RETURN
30000 '
30001 ' *** Perform Forward Scan and Display Dark Count ***
30002 '
30003 IF c$ <> "uz" GOTO 30010
30004 zal = CINT(ZC / 1 + .5) * 1: PRINT USING "Waiting until ZA = ### to start scanning"; zal
30005 LOCATE 12, 24: PRINT USING "ZA = ####.## now"; ZA: GOSUB 9891: GOSUB 50
30006 FLAG = 1: GOSUB 7800
30007 GOSUB 2090: IF TA% = 1 THEN 30010
30008 IF ZC < zal THEN 30005
30010 LOCATE 6, 1: PRINT " UV "; MDD2$; "scan-Forward direction-Slit"; SQ
30020 LOCATE 8, 1: PRINT " Dark Count =", DARK
30030 LOCATE 10, 1: PRINT "   TIME    WV    STEP    INTENSITY       DS"
30040 DQ$ = STR$(SQ + 1): K = -1
30050 GOSUB 2090
30070 FOR wv = SW TO EW STEP DW
30080   GOSUB 32000: IF HF% = 1 THEN wv = EW
30090 NEXT: RETURN
31000 '
31001 ' *** Perform Reverse Scan and Dark Count ***
31002 '
31010 LOCATE 6, 1: PRINT " UV "; MDD2$; "scan-Reverse direction-Slit"; SQ
31020 GOSUB 33000: LOCATE 10, 1: PRINT "  TIME    WV    STEP    INTENSITY"
31030 DQ$ = STR$(SQ + 1): K = -1
31040 FOR wv = EW TO SW STEP -DW
31050   GOSUB 32000: IF HF% = 1 THEN wv = SW
31060 NEXT: RETURN
32000 '
32001 ' *** Perform Single Scan ***
32002 '
32004 IF c$ <> "ua" GOTO 32017
32005   IF tinc = 0 GOTO 32017
32010   IF wv < 3510 THEN cx$ = "3" ELSE cx$ = "2"
32012   IF dsflg = 1 AND wv < 3100 THEN cx$ = "5"
32015   IF wv < 3000 THEN cx$ = "5"
32016 GOTO 32020
32017   IF wv < 3100 THEN cx$ = "5" ELSE cx$ = "2"
32018   IF wv < 3000 THEN cx$ = "10"
32020 GOSUB 8500: GOSUB 2090: IF HF% = 1 OR M1 = 0 OR (M2 = 0 AND TYP$ = "mkiii") THEN VA = 0: GOTO 32050
32030 O1$ = "X," + STR$(M1) + "," + STR$(M2): GOSUB 9450
32034 O1$ = ":R," + DQ$ + "," + DQ$ + "," + cx$ + ":O": VA = 0
32035 IF TIMER < TTIM - 2.2 AND c$ = "ua" THEN 32035
32040 GOSUB 9450
32050 TI = TIMER * 60: TM = INT(TI / 36) / 100
32070 X = VAL(cx$)
32080 VA = INT(VA * 100 / X) / 100  'normalize output
32090 PRINT #8, TM; CR$; wv; CR$; STR$(M1); CR$; VA  'record to file
32100 K = K + 1: IF K >= 12 THEN K = 0
32110 LOCATE 12 + K, 1
32114 IF c$ = "ua" THEN PRINT USING " ####.##"; (stim * 60 + (wv - SW1) / 5 * tinc / 60 - TI / 3600) * 60;  ELSE PRINT TIME$;
32115 PRINT USING "  ####  #####  ######.#"; wv; M1; VA
32120 LOCATE 13 + K, 1: PRINT "                                          ";
32190 W = wv / 100: IF INT(W) = W AND dsflg = 1 THEN GOSUB 34000
32500 '
32510 TTIM = TTIM + tinc: RETURN
33000 '
33001 ' *** Get Dark Count ***
33002 '
33010 O1$ = "R,1,1,20:O": GOSUB 9450: GOSUB 9190: DARK = VA / 20: IF c$ = "uv" THEN DARK = DARK * VAL(cx$)
33020 LOCATE 8, 1: PRINT " Dark Count =", DARK
33030 PRINT #8, "dark"; CR$; DARK: RETURN
34000 '
34001 ' *** Take a direct-sun measurement ***      Alkis
34002 '
34003 IF wv = 2900 THEN MZ(0) = MZ(0) + 1: MZ(MZ(0)) = 0: RETURN
34005 O1$ = "L,19414,200": GOSUB 9450: dsc = 1
34010 TR$ = "d" + "s": TD = 1500: GOSUB 7000: GOSUB 7750
34020 GOSUB 9770: GOSUB 9740: IF wv <= 3100 GOTO 34030
34025     M5$ = STR$(VAL(UF$) + 64): GOSUB 9800
34026     dsc = nd0 + nd1 * wv + nd2 * wv ^ 2
34030 O1$ = "R;O": GOSUB 9450: MZ(0) = MZ(0) + 1: MZ(MZ(0)) = VA / VAL(cx$) / dsc
34035 LOCATE 12 + K, 35: PRINT USING "######.#"; VA / VAL(cx$)
34040 TR$ = "s" + "a": GOSUB 9785: GOSUB 9750
34050 M1$ = STR$(ZU%): GOSUB 9870
34051 IF wv > 3100 THEN M5$ = UF$: GOSUB 9800
34060 O1$ = "L,19414,90": GOSUB 9450
34100 RETURN
65529 REM proper last line

