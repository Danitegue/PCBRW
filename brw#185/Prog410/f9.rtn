10000 REM **************** testfw2 routine 08/04/96 *******************
10010 REM          MKII/MKIII/MKIV Polarimetric Measurements
10020 REM
10030 REM              (SCI-TEC Instruments, April 1996)
10040 REM
10050 REM *************************************************************
10060 REM
10070 REM  This routine tests and records the neutral densities at
10080 REM  the various filterwheel positions to the file FW2TEST.###.
10090 REM
10200 REM  Side Effects:
10210 REM
10220 REM  Global Variables Referenced:
10230 REM    SP  TD
10240 REM    CZ$ M5$ NO$ O1$ WL$ WU$
10250 REM    HF%
10260 REM
10270 REM  Exits: 13020
10280 REM
10290 REM  Uses: 6610, 6635, 6650, 6690, 6900, 7000, 8300, 9450,
10300 REM        9650, 9700, 9840, 9860, 9900
10310 REM
10500 REM *************************************************************
10510 REM  History: dd/mm/yy
10520 REM  08/04/96 - Output file is now appended
10530 REM  13/02/95 - Original release
10540 REM *************************************************************
11000 '
11001 ' *** Setup ***
11002 '
11010 DATA tstfw2
11015 f2t(1,1) = 0: erase f2t: dim f2t(10,8):for i=1 to 9:for j=0 to 6: f2t(i,j)=0: next: next
11016 fw2cy$(1)="3":fw2cy$(2)="4":fw2cy$(3)="5":fw2cy$(4)="12":fw2cy$(5)="20":fw2cy$(6)="5":fw2cy$(7)="4":fw2cy$(8)="3":fw2cy$(9)="2"
11020 IF M9$<>"2" THEN GOSUB 9820   'Standard Lamp
11030 GOSUB 6610:GOSUB 6630     'Filter#1 to 1
11040 M5$="0":GOSUB 6650        'Filter#2 to 0
11050 GOSUB 6700:TD=1500:GOSUB 7000 'Iris open
11060 LOCATE ,SP:PRINT "4 - Rotate director prism to lamps":GOSUB 9860
11070 GOSUB 9650:T0=TA/3600:GOSUB 8600  'ENTER when ready
11080 PRINT CL$:LOCATE ,SP:PRINT"Waiting until ";H$;" for lamp warmup":GOSUB 6900
11090 IF LO%=1 THEN 13000       'End if Lamp is Off
11100 GOSUB 6800:IF HF%=1 THEN 13000    'End if Aborted
11110 GOSUB 2450            'Get Temperature
12000 '
12001 ' *** Take Set of Observations ***
12002 '
12010 CLS:PRINT CL$:GOSUB 9670
12020 CLOSE 8:OPEN DD$+"FW2TST."+NO$ FOR APPEND AS 8: Print#8, time$
12030 PRINT#8,"  Filterwheel #2 Test - Brewer #";NO$;" - ";DC$;" (";JD$;")"
12040 PRINT#8,:PRINT#8,"FW Pos  Dark                        Neutral Densities"
12050 PRINT#8,:PRINT#8,"SLIT0   DC        Slit 1     Slit 2     Slit 3     Slit 4     Slit 5"
12070 nrep = 5:for irep=1 to nrep
12100 ds4%=1: for ds%=0 to 4:fw2i = ds%+1: cz$=fw2cy$(fw2i): GOSUB 20000 : next: IF HF%=1 THEN 13000
12150 ds4%=3: for ds%=5 to 2 step -1: fw2i = 11 - ds%: cz$=fw2cy$(fw2i): GOSUB 20000 : next: IF HF%=1 THEN 13000
12155 ds4%=3: for ds%=2 to 5:fw2i = 11 - ds%: cz$=fw2cy$(fw2i): GOSUB 20000 : next: IF HF%=1 THEN 13000
12160 ds4%=1: for ds%=4 to 0 step -1:fw2i = ds%+1: cz$=fw2cy$(fw2i): GOSUB 20000 : next: IF HF%=1 THEN 13000
12170 next
12200 ' Finish
12205 for i=1 to 9: CY=val(fw2cy$(i)): print#8, using "# ## ##"; i, cy, nrep;: for j=0 to 6: print#8, using " ########"; f2t(i,j);:
12210 va = (f2t(i,j)-f2t(i,1))*2/cy/IT/2/nrep: IF VA>1E+07 THEN VA=1E+07
12220 IF VA<2 THEN VA=2
12225 F1=VA:FOR Jdt=0 TO 8:VA=F1*EXP(VA*T1):NEXT
12230 f2t(i,j) = log(va)/co*p4%+tc(j)*te%: print#8, using " ########"; f2t(i,j);: next:print#8,: next
12240 '
13000 '
13001 ' *** Clean Up and Exit ***
13002 '
13010 PRINT#8,: Print#8, time$:CLOSE 8:GOSUB 9840
13020 RETURN
20000 '
20001 ' *** Filterwheel #2 Observation ***
20002 '
20010 O1$="M,5,"+MID$(STR$(DS%*64),2):GOSUB 9450    'set FW#2
20015 O1$="M,4,"+MID$(STR$((5-DS4%)*64),2):GOSUB 9450    'set FW#1
20020 WL$="0":WU$="6":GOSUB 9700:GOSUB 9900    'get data
20030 IF HF%=1 THEN PRINT CL$:PRINT "TESTFW#2 measurement terminated":RETURN
20040 REM GOSUB 8300                    'correct data for dark/dead/temperature
20045 rem I=(320-VAL(M4$))/64:GOSUB 21000
20047 for i=0 to 6: f2t(fw2i,i)=f2t(fw2i,i)+f(i): next
20050 RETURN
21000 '
21001 ' *** Print Out Data ***
21002 '
21030 PRINT#8,I;DS%;F(0);" ";F(1);" ";F(2);" ";F(3);" ";F(4);" ";F(5);" ";F(6)
21040 RETURN
65529 REM proper last line
