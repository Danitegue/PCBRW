10000 REM ***************** ss routine 08/10/97 *********************
10010 REM         MKII/MKIII/MKIV Extended UV Direct Sun Scan
10020 REM Changed by Alkis to change match old software 8/2/97
10030 REM             (SCI-TEC Instruments, July 1995)
10040 REM
10050 REM  See Also: UV.RTN
10060 REM ***********************************************************
10070 REM
10080 REM  This routine performs a direct-sun UV-B scan, covering a
10090 REM  specific range based on the Brewer model:
10092 REM    MKII:         290 to 340 nm
10094 REM    MKIII:        290 to 366 nm :'AB
10096 REM    MKIV O3/SO2:  290 to 363 nm
10100 REM
10101 REM  Checks the instensity and inserts the next ND filter when
10102 REM  it is greater than 100000 counts
10103 REM
10110 REM  Slit #1 and Slit #5 are used to complete the full scan.
10120 REM  FW3 also switches position when needed on the MKIII/IV.
10130 REM
10160 REM  Global Variables Referenced:
10170 REM    DC(5,3) GI GS PC SP TD TIMER T1 VA ZERO
10180 REM    A1$() CR$ DD$ DQ$ JD$ MC$ M5$ M8$ NO$ O1$ SQ$ TIME$ TR$ YF$
10190 REM    DN% HF% MX% MZ% TE% UC%
10200 REM
10210 REM  Exits: 10520, 10550, 14720, 14930
10220 REM
10230 REM  Uses: 2090, 2450, 3100, 4450, 6610, 6630, 6650, 6690,
10240 REM        7000, 7500, 7750, 9190, 9400, 9450, 9500, 9650,
10250 REM        9670, 9805, 9800
10260 REM
10300 REM ************************************************************
10310 REM  History: dd/mm/yy
10311 REM  22/06/2000 - Alkis Changes the switch wavelength from 3430 to 3515 A
10312 REM  06/05/99 - Moves micrometer before measuring dark current
10315 REM  01/06/97 - Calls fr.rtn (and hp, hg) if micrometer is at diodes
10320 REM  14/07/95 - Updated to handle MKII/MKIII/MKIV operation,
10330 REM  test added to disable NO2 mode operation.
10340 REM  28/02/94 - Modified for MKIV, slit and FW#3 switching
10350 REM  added, micrometer position check added, subroutine 33000
10360 REM  added, CR$ added to end of PRINT#8
10370 REM  15/12/93 - Routine was revived during installation of #100
10380 REM ************************************************************
10500 DATA ss
10510 REM
10520 IF DN% <> 8 THEN RETURN       'if no disk recording
10530 IF MDD$ = "o3" THEN 10600
10540   PRINT #4, "SS is not an NO2 routine": PRINT "SS is not an NO2 routine"
10550   X = 2: GOSUB 7220: RETURN
10600 '
10601 ' *** Set up ranges ***
10602 '
10610 WV = 3400: IF TYP$ = "mkiii" THEN WV = 3515: REM *** Note that this is ignored on a MKII: 'AB
10620 SQ = 1: GOSUB 33000: SWITCH.1TO5 = STEP.NUMBER
10625 '
10630 WV = 3515: IF TYP$ = "mkii" OR ZERO = 3469 THEN WV = 3400: 'AB
10640 SQ = 1: GOSUB 33000: LIMIT.OF1 = STEP.NUMBER
10645 '
10650 WV = 3655: IF TYP$ = "mkii" OR ZERO = 3469 THEN WV = 3400:  'AB
10660 SQ = 5: GOSUB 33000: LIMIT.OF5 = STEP.NUMBER
11700 '
11701 ' *** Check Initial Micrometer Position ***
11702 '
11730 Y = 0: VA = 0: LOCATE 15, 10: PRINT "Checking Position of Micrometer": GOTO 14000: 'AB 17-10-98
11731 O1$ = "R,1,1,1:G,544": GOSUB 9450: GOSUB 9190: IF (VA AND 8) <> 8 AND (VA AND 4) <> 4 THEN GOTO 14000
11732 alkflg% = 1: C$ = "fr": JJ = JJ - 1: RETURN
14000 '
14001 ' *** Take UV-B Direct Sun Observation ***
14002 '
14010 MN = 2900: MX = 3655: DD = 5: UC% = 0: IF TYP$ = "mkii" THEN MX = 3400:  'AB
14020 CX$ = "1": TR$ = "d" + "s"    'cycle count and track mode
14030 GOSUB 2450: GOSUB 4450: OPEN DD$ + "dum.dta" FOR OUTPUT AS 8
14040 PRINT #8, "Integration time is 0.2294 seconds per sample"; CR$;
14050 PRINT #8, "dt "; T1; CR$;
14060 PRINT #8, "cy "; CX$; CR$;
14070 PRINT #8, A1$(0);
14080 :
14610 GOSUB 6610: GOSUB 6630: M5$ = "192": GOSUB 6650
14620 TD = 1500: GOSUB 7000: GOSUB 6690
14630 LOCATE , SP: PRINT "4 - Brewer azimuth to sun"
14640 GOSUB 9650
14650 REM
14660 REM
14680 GOSUB 9670: M8$ = "-" + MC$: GOSUB 9805: M8$ = STR$(-ZERO): GOSUB 9805
14690 PRINT #4, "SS scan started at "; TIME$
14695 W1 = MN: W2 = MX: DW = DD: SQ = 1: IF TYP$ = "mkii" AND ZERO = 4269 THEN SQ = 5: 'AB
14696 GOSUB 33200: PRINT #8, CR$; "nd" + M5$; CR$; ' AB
14698 GOSUB 31000: IF HF% = 1 THEN 14700
14700 M8$ = STR$(ZERO + 1): GOSUB 9805: M8$ = "-1": GOSUB 9805: M8$ = MC$: GOSUB 9805: GOSUB 9500
14710 PRINT #8, "end": CLOSE 8
14720 IF HF% = 1 THEN PRINT #4, "SS scan aborted at "; TIME$: RETURN: REM home flag
14900 '
14901 ' *** Record data on ss file ***
14602 '
14910 IF TYP$ = "mkiv" THEN O1$ = "M,6," + STR$(MX%): GOSUB 9450: REM * reset FW3 to original position
14920 B$ = JD$ + YF$ + "." + NO$
14930 PRINT #4, "UV Direct-Sun scan finished at "; TIME$
14940 SHELL "copy " + DD$ + "SS" + B$ + "+" + DD$ + "dum.dta " + DD$ + "SS" + B$
14950 PRINT #4, "SS"; B$; " data file extended at "; TIME$: RETURN
30000 '
30001 ' *** Switch to Extended Range Operation (Slit #5) ***
30002 '
30010 SQ = 5: B = DC(SQ, 2): C = DC(SQ, 3)
30020 DQ$ = STR$(SQ + 1)
30025 O1$ = ";R," + DQ$ + "," + DQ$ + "," + CX$: 'AB
30030 IF TYP$ = "mkiv" THEN O1$ = O1$ + ";M,6,50": 'AB
30035 GOSUB 9450
30040 RETURN
31000 '
31001 ' *** Scan w1 to w2 step dw on slit sq ***
31002 '
31010 O1$ = "R,1,1,40;O": GOSUB 9450: GOSUB 9190: DARK = VA / 40: GOSUB 7750
31020 PRINT #8, "DARK": PRINT #8, DARK: PRINT "dark=", DARK: 'AB
31025 CX$ = "10"
31030 B = DC(SQ, 2): C = DC(SQ, 3): DQ$ = STR$(SQ + 1)
31035 O1$ = ";R," + DQ$ + "," + DQ$ + "," + CX$: GOSUB 9450: 'AB ->
31040 FOR WV = W1 TO W2 STEP DW
31050   GOSUB 2090: IF HF% = 0 THEN 31065
31060   PRINT "aborted": PRINT #8, "aborted"; CR$; : WV = W2: RETURN
31065   GOSUB 33020: SN$ = STR$(STEP.NUMBER)
31070   IF TYP$ = "mkii" THEN 31090: REM *** No switching needed on a MKII Brewer ***
31075   SNN$ = STR$(STEP.NUMBER * GS + GI): REM *** Used only for MKIII ***
31080   IF STEP.NUMBER > SWITCH.1TO5 THEN IF SQ = 1 THEN GOSUB 30000: GOTO 31065
31085   IF STEP.NUMBER > LIMIT.OF1 AND TYP$ <> "mkiii" THEN GOSUB 30000: GOTO 31065
31090   IF STEP.NUMBER > LIMIT.OF5 THEN IF SQ = 5 THEN WV = W2: RETURN
31095   IF STEP.NUMBER > LIMIT.OF1 THEN IF TYP$ = "mkii" THEN WV = W2: RETURN
31100   IF STEP.NUMBER < 0 GOTO 31190
31110   IF WV = 3005 THEN CX$ = "5": O1$ = ";R," + DQ$ + "," + DQ$ + "," + CX$: GOSUB 9450
31120   IF WV = 3105 THEN CX$ = "2": O1$ = ";R," + DQ$ + "," + DQ$ + "," + CX$: GOSUB 9450
31130   IF TYP$ = "mkiii" THEN O1$ = "X," + SN$ + "," + SNN$ + ";R;O" else O1$ = "M,10," + SN$ + ";R;O"
31140   TI = TIMER * 60: TM = TI: GOSUB 9450: TM = INT((TM + TI) / 72) / 100
31150   REM
31160   VA = INT(VA * 100 / VAL(CX$)) / 100'normalized output
31170   PRINT #8, TM; CR$; WV; CR$; SN$; CR$; VA 'record to file
31180   PRINT USING " ####  #######.##  #######.##"; WV; VA; VA - DARK
31185   IF INT(WV / 25) = WV / 25 THEN GOSUB 7750
31186   IF VA > 100000 THEN GOSUB 32000: 'AB <--
31190 NEXT: RETURN
32001 '
32000 ' *** Change Neutral Density filter *** :' AB
32002 '
32010 A = 64 + VAL(M5$): IF A > 320 THEN A = 320
32020 A$ = STR$(A): M5$ = RIGHT$(A$, LEN(A$) - 1): GOSUB 9800
32030 PRINT "Filter " + M5$: PRINT #8, "filter  " + M5$
32040 RETURN
33000 '
33001 ' *** Determine The Step Number Given SQ and WV ***
33002 '
33010 B = DC(SQ, 2): C = DC(SQ, 3)
33020 STEP.NUMBER = INT((-B + SQR(B * B - 4 * (DC(SQ, 1) - WV) * C)) / 2 / C + .5)
33030 RETURN
33040 REM
33200 'Move first the micrometer to short wavelength to avoid PMT overexposure: 'AB 6/5/99
33300 WV = MN: GOSUB 33010
33305 SN$ = STR$(STEP.NUMBER): SNN$ = STR$(STEP.NUMBER * GS + GI)
33310 IF TYP$ = "mkiii" THEN O1$ = "X," + SN$ + "," + SNN$
33320 IF TYP$ <> "mkiii" THEN O1$ = "M,10," + SN$
33330 PRINT "           Moving micrometer to "; MN / 10; " nm": GOSUB 9450
33340 M5$ = "0": GOSUB 6650
33350 RETURN
65529 REM proper last line

