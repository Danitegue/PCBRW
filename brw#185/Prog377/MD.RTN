10000 REM CREATES DSSUM TO FILE TO SEND OVER MODEM  **MD ROUTINE VER 3.0 20000620**
10001 REM IBM
10005 DATA md
10010 CLS:LOCATE 10, 11:PRINT "*** CREATING OBSERVATION FILE TO SEND VIA MODEM/TCP ***"
10011 ON ERROR GOTO 10013
10012 GOTO 10015
10013 PRINT "ENDED DUE TO ERROR AT 1":END
10015 GOSUB 34900:IF PD$="" THEN PD$=BREWDIR$+"\"
10016 ON ERROR GOTO 10018
10017 GOTO 10020
10018 PRINT "ENDED DUE TO ERROR AT 2":END
10020 GOSUB 33800:REM temp coeffs
10030 ON ERROR GOTO 11000
10040 GOTO 30000
11000 REM error handler on file open
11010 PRINT "No data (B) file available for this day.":REM if problem
11020 PRINT :RESUME 11030
11030 ON ERROR GOTO 3100
11040 CLOSE 8:RETURN
11060 :
30000 REM -30099 open data file and print header for summary sheet
30010 CLOSE 8:OPEN DD$+"B"+JD$+YF$+"."+NO$ FOR INPUT AS 8:REM open today's data file
30020 ON ERROR GOTO 3100
30023 CLOSE# 9
30025 OPEN PD$+"SID.FIL" FOR INPUT AS #9:INPUT# 9, SID$:CLOSE# 9
30030 TEL$="TELECOM.TXT"+""
30032 H%=INT(T0/60):M%=INT(T0 - H% * 60):S%=(T0 - H% * 60 - M%)*60
30035 TSP$ = RIGHT$(STR$(H%++100), 2)+RIGHT$(STR$(M%+100), 2)
30037 MIN$ = RIGHT$(STR$(M%+100), 2)
30040 OPEN DD$+TEL$ FOR OUTPUT AS #9
30050 PRINT #9, "SXCN10 ";SID$;" ";
30055 PRINT #9, USING"\\";DB$;
30060 PRINT #9, TSP$
30065 PRINT #9, "PRELIMINARY DIRECT SUN DATA"
30070 PRINT #9, " "
30075 PRINT #9,:PRINT #9,LO$;" OZONE - BREWER # ";NO$
30077 PRINT #9, SPC(14) "PRELIMINARY DATA   -   LOCAL DATE ";DCY2K$
30080 PRINT #9, SPC(14) "LATITUDE - ";L1$;"  :   LONGITUDE - ";L2$:
30085 PRINT #9, SPC(3)  "ETC'S - O3/SO2 -";B1;"/";B2;" :   ABSORPTION -";A1;"/";A3:PRINT #9,
30090 PRINT #9,"TYPE  TIME-GMT  TEMP  AIRMASS    OZONE    ERROR    SO2    ERROR"
30095 :
30098 :
30099 :
30100 REM -30399 print out summary
30105 CLS:LOCATE 10, :PRINT SPC(19) "COPYING GOOD DIRECT SUN OBSERVATIONS"
30110 S1=0:S2=0:S3=0:S4=0:S5=0:S6=0:S7=0
30111 Z1=0:Z2=0:Z3=0:Z4=0:Z5=0:Z6=0:Z7=0:Z8=0:Z9=0
30115 IF EOF(8) THEN 30400:REM test for end
30120 INPUT#8,A$:IF A$="ef" THEN 30400:REM test for end
30125 IF A$="tc" THEN GOSUB 33820:GOTO 30115
30130 IF A$<>"summary" THEN 30115:REM test for summary
30135 INPUT#8,H$:REM read in the time record
30140 INPUT#8,A$,A$,A$,A$,MU$,TQ$,TY$,FL$
30142 IF VAL(TQ$) = -30 THEN 30120
30144 TY1$=TY$: IF TY$="ds" THEN TY1$="DS": GOTO 30150
30146 GOTO 30115
30150 INPUT#8,A$,A$,A$,A$,R5$,R6$,SO$,OZ$
30155 OZ=INT((VAL(R6$)-B1-VAL(TQ$)*(TC(1)-TQ(1)))/VAL(MU$)/A1+.5)/10
30156 SO=(VAL(R5$)-B2-VAL(TQ$)*(TC(0)-TQ(0)))/VAL(MU$)/A3/10:SO=(SO-OZ)/A2*10
30157 OZ$=STR$(OZ):SO=INT(SO+.5)/10:SO$=STR$(SO)
30160 INPUT#8,A$,A$,A$,A$,A$,A$,ES$,EO$
30170 H$=RIGHT$("   "+H$,10)
30175 TQ$=RIGHT$("    "+TQ$,5)
30180 TY1$=RIGHT$(" "+TY1$,2)
30190 FL$=RIGHT$("  "+FL$,2):IF VAL(FL$) > 4 THEN 30115
30210 X$=MU$:X=3:Y=9:PS=0:GOSUB 30700:MU$=X$
30220 X$=OZ$:X=1:Y=10:GOSUB 30700:OZ$=X$
30230 X$=SO$:Y=8:GOSUB 30700:SO$=X$
30240 X$=EO$:PS=1:GOSUB 30700:EO$=X$
30250 X$=ES$:GOSUB 30700:ES$=X$
30260 GOSUB 7800:RA=0:GOSUB 7830:MUMIN=M2:GOSUB 7800:REM Minimum Mu for day
30262 MUMAX=3.2:IF MUMIN>3 THEN MUMAX=MUMIN+.2:IF MUMIN>4 THEN MUMAX=4.2
30265 IF VAL(MU$)>MUMAX THEN 30115
30268 S4=S4+1
30270 IF VAL(SO$)>5 OR VAL(OZ$)>600 OR VAL(OZ$)<150 OR VAL(EO$)>2.5 THEN 30115
30275 IF VAL(SO$)<-5 AND VAL(MU$)<3 THEN 30115
30278 PRINT #9,TY1$;FL$;H$;TQ$;MU$;OZ$;EO$;SO$;ES$
30280 S1=S1+1:S2=S2+VAL(OZ$):S3=S3+VAL(OZ$)*VAL(OZ$)
30290 S5=S5+VAL(SO$):S6=S6+VAL(SO$)*VAL(SO$):S7=S7+VAL(TQ$)
30291 Z1=Z1+1:Z2=Z2+1/VAL(MU$):Z3=Z3+1/VAL(MU$)/VAL(MU$)
30292 Z4=Z4+VAL(R6$)/VAL(MU$)/VAL(MU$):Z5=Z5+VAL(R6$)/VAL(MU$)
30293 Z7=Z7+VAL(R5$)/VAL(MU$)/VAL(MU$):Z8=Z8+VAL(R5$)/VAL(MU$)
30300 GOTO 30115
30398 :
30399 :
30400 REM -30499 print out daily mean values
30410 PRINT #9,:IF S1<1 THEN 30550
30415 IF S1=1 THEN S3=0:S6=0:GOTO 30440
30420 S3=(S1*S3-S2*S2)/S1/(S1-1):IF S3>0 THEN S3=SQR(S3):GOTO 30430
30425 S3=0
30430 S6=(S1*S6-S5*S5)/S1/(S1-1):IF S6>0 THEN S6=SQR(S6):GOTO 30440
30435 S6=0
30440 S2=INT(10*S2/S1+.5)/10:S5=INT(10*S5/S1+.5)/10
30450 S3=INT(10*S3+.5)/10
30460 S6=INT(10*S6+.5)/10
30461 S7=INT(S7/S1+.5):TQ$=RIGHT$("    "+STR$(S7),5)
30462 IF S1=1 THEN Z6=0:Z9=0:GOTO 30467
30465 Z6=Z1*Z3-Z2*Z2:IF Z6<=0 THEN Z6=0:ELSE Z6=INT(10*(Z1*Z4-Z2*Z5)/Z6+.5)/10
30466 if Z1*Z3-Z2*Z2 <> 0 then Z9=INT(10*(Z1*Z7-Z2*Z8)/(Z1*Z3-Z2*Z2)+.5)/10 else z9=0
30467 Z2=INT(1000*Z1/Z2+.5)/1000:Z2$=RIGHT$("       "+STR$(Z2),9)
30470 GOSUB 30600
30480 PRINT #9,"   DAILY MEANS";TQ$;Z2$;S2$;SPC(8);S5$
30485 PRINT #9,"   STANDARD DEVIATION       ";S3$;SPC(8);S6$
30490 PRINT #9,"                            ";S1;" / ";S4
30498 :
30499 :
30500 'REM -30599 close down summary printout and return
30505 CLOSE #8:GOSUB 32000:REM subroutine to send obs via modem
30510 GOSUB 34000:RETURN
30550 REM insufficient number of data points
30560 PRINT #9,
30570 PRINT #9,"***      INSUFFICIENT DATA TO CALCULATE      ***"
30580 PRINT #9,"***  STATISTICS FOR THIS DAY'S OBSERVATIONS. ***":PRINT #9, " "
30590 CLOSE #8:GOSUB 32000:GOSUB 34000:RETURN
30598 :
30599 :
30600 REM -30699 format the output
30610 X$=STR$(S5):X=1:Y=8:PS=0:GOSUB 30700:S5$=X$
30620 X$=STR$(S6):GOSUB 30700:S6$=X$
30630 X$=STR$(S2):Y=10:GOSUB 30700:S2$=X$
30640 X$=STR$(S3):PS=1:GOSUB 30700:S3$=X$
30650 S1$=RIGHT$("         "+ STR$(S1),10)
30660 RETURN
30698 :
30699 :
30700 REM -30799 format the output
30710 XX=VAL(X$):FOR I=1 TO X:XX=XX*10:NEXT :X$=STR$( INT(XX)):S$=LEFT$(X$,1)
30720 X$=MID$(X$,2):IF  LEN(X$)<X+1 THEN X$=RIGHT$("00000000"+X$,X+1)
30730 IF PS=1 THEN IF S$=" " THEN S$="+"
30740 X$=RIGHT$("          "+S$+ LEFT$(X$, LEN(X$)-X)+"."+ RIGHT$(X$,X),Y)
30750 RETURN
30800 :
30900 :
31000 REM error handler on file open
31010 PRINT#9,"NO RESPONSIVITY FILE AVAILABLE ON DATA DISK":GOTO 31040
31030 PRINT#9,"NO UV FILE AVAILABLE FOR THIS DAY.":REM if problem
31040 PRINT#9,"=":RESUME 31050
31050 ON ERROR GOTO 3100:CLOSE #8:CLOSE #9:RETURN
31060 :
31070 :
32000 REM -32400 print out summary
32010 CLS:LOCATE 10, :PRINT SPC(24)"COPYING UVB OBSERVATIONS"
32020 PRINT#9, " ":PRINT#9, " ":PRINT #9, SPC(5) "SUMMARY OF BREWER UVB SCAN MEASUREMENTS FOR ";DCY2K$
32120 FIRST%=1:ON ERROR GOTO 32121:OPEN DD$+"UV"+JD$+YF$+"."+NO$ FOR INPUT AS 8:CLOSE 8:GOTO 32129
32121 PRINT #9,"*** NO UV FILE FOR THIS DAY ***"
32122 PRINT "***NO UV FILE FOR THIS DAY":RESUME 32128
32128 ON ERROR GOTO 3100:GOTO 32400
32129 ON ERROR GOTO 3100
32210 SHELL "C:\O3UV\O3FROMUV.EXE "+PD$+"SET.TXT UV"+JD$+YF$+"."+NO$
32220 ON ERROR GOTO 32221:OPEN DD$+"UVDATA.DAT" FOR INPUT AS 8:GOTO 32229
32221 PRINT #9,"*** UV FILE PROBLEM ***":REM problem in O3FROMUV program, check PD$+"SET.TXT" and DD$+"LOG_UV.TXT"
32222 PRINT :RESUME 32228
32228 ON ERROR GOTO 3100:GOTO 32400
32229 ON ERROR GOTO 3100:Z1=0:Z2=0:Z3=0:Z4=0:Z5=0:Z6=0:Z7=0:Z8=0
32230 IF EOF(8) THEN 32300
32240 LINE INPUT#8, L$
32250 IF L$="" THEN 32230
32251 IF FIRST%=0 THEN 32260
32252 FIRST%=0
32253 NOON=((VAL(MID$(L$,43,2))+112-VAL(MID$(L$,22,2)))*60+(VAL(MID$(L$,46,2))-VAL(MID$(L$,25,2))))*60+(VAL(MID$(L$,49,2))-VAL(MID$(L$,28,2)))
32254 NOONM=INT(NOON/60):NOONS=NOON-NOONM*60:NOONH=INT(NOONM/60):NOONM=NOONM-NOONH*60:NOONH=NOONH-100:IF NOONH>24 THEN NOONH=NOONH-24
32255 PRINT #9,USING "     LOCAL SOLAR NOON = \\:\\:\\ GMT"; RIGHT$(STR$(NOONH+100),2), RIGHT$(STR$(NOONM+100),2), RIGHT$(STR$(NOONS+100),2)
32258 PRINT #9," TIME-GMT  TEMP  ZENITH ANGLE  AIRMASS  UV INDEX  OZONE ERROR  CLEARNESS"
32259 PRINT #9,
32260 PRINT #9, " ";MID$(L$,43,8);"   ";MID$(L$,108,3);"    ";MID$(L$,60,7);"     ";MID$(L$,52,7);"   ";MID$(L$,77,4);
32261 Z3=Z3+1:Z4=Z4+VAL(MID$(L$,108,3)):Z5=Z5+VAL(MID$(L$,52,7)):Z=VAL(MID$(L$,105,3)):Z6=Z6+Z:Z8=Z8+Z*Z
32262 IF VAL(MID$(L$,88,5))<99 THEN PRINT#9, "    ";MID$(L$,82,6);" ";MID$(L$,88,5);:Z1=Z1+1:Z=VAL(MID$(L$,82,6)):Z2=Z2+Z:Z7=Z7+Z*Z:ELSE PRINT#9, "                ";
32268 PRINT#9, "     ";MID$(L$,105,3)
32270 GOTO 32230
32300 IF FIRST%=0 THEN 32320
32310 PRINT #9,"*** NO UV DATA FOR THE DAY ***":GOTO 32400
32320 PRINT#9, " ":PRINT #9,USING "DAILY MEANS ###                ###.###";Z4/Z3,Z5/Z3;
32330 IF Z1>0 THEN PRINT #9,USING "           ####.#";Z2/Z1;:ELSE PRINT #9,"                 ";
32340 PRINT #9,USING "           ###";Z6/Z3
32350 PRINT #9,"STANDARD DEVIATION                                ";
32360 IF Z1>1 THEN Z=(Z7*Z1-Z2*Z2)/Z1/(Z1-1):IF Z>0 THEN Z=SQR(Z)
32370 IF Z1>1 THEN PRINT #9,USING "###.#";Z;:ELSE PRINT #9,"     ";
32380 IF Z3>1 THEN Z=(Z8*Z3-Z6*Z6)/Z3/(Z3-1):IF Z>0 THEN Z=SQR(Z)
32390 IF Z3>1 THEN PRINT #9,USING "           ###";Z:ELSE PRINT #9,"              "
32400 PRINT #9, "=":CLOSE #9
32440 CLOSE #8:RETURN
33200 :
33250 :
33800 REM read in inst tc's
33810 CLOSE 8
33820 OPEN DD$+NO$+"\"+ICF$+"."+NO$ FOR INPUT AS 8:REM open inst const file
33830 FOR I=1 TO 5:INPUT#8,TQ(I+1):NEXT:REM temperature coeffs
33840 TQ(0)=TQ(2)-TQ(5)-3.2*(TQ(5)-TQ(6))
33850 TQ(1)=TQ(3)-TQ(5)-.5*(TQ(4)-TQ(5))-1.7*(TQ(5)-TQ(6))
33860 RETURN
33915 :
33920 :
34000 REM - 34099 sends obs. file via TCP or modem
34010 SHELL "COPY "+DD$+TEL$+" "+DD$+BULFILE$
34020 REM SHELL "SENDBULL.BAT"
34070 RETURN
34080 :
34090 :
34900 REM dc$ is in format mmm dd/yy.  translate to yyyymmdd
34910 M$="JANFEBMARAPRMAYJUNJULAUGSEPOCTNOVDEC"
34920 M%=1:WHILE MID$(M$,M%,3)<>LEFT$(DC$,3):M%=M%+3:WEND
34930 MM%=(M%-1)/3+101
34940 YYYY%=VAL(MID$(DC$,8,2))+1900:IF YYYY%<1999 THEN YYYY%=YYYY%+100
34950 DD%=VAL(MID$(DC$,5,2))+100
34960 DCY2K$=RIGHT$(STR$(YYYY%),4)+"-"+RIGHT$(STR$(MM%),2)+"-"+RIGHT$(STR$(DD%),2)
34970 BULFILE$="BUL"+JD$+RIGHT$(STR$(YYYY%),2)+"."+NO$
34980 RETURN
34990 :
34992 :
65529 DATA proper last line
