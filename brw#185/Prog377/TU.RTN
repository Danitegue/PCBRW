10000 REM ******************** tu routine 14/09/98 ********************
10010 REM              MKII/MKIII/MKIV Test UVB Alignment
10020 REM
10030 REM    This routine checks the prism position on the UVB port
10040 REM    for maximum lamp intensity.
10050 REM *************************************************************
11000 IF Q8%=0 THEN RETURN
11010 IF MDD$="o3" THEN 12000
11020   PRINT#4,"TU is not an NO2 routine":PRINT "TU is not an NO2 routine"
11030   X=2:GOSUB 7220:RETURN
12000 '
12001 ' *** Setup ***
12002 '
12010 DATA tu
12020 PRINT CL$:LOCATE 4,29:PRINT "Turn on the external lamp"
12040 GOSUB 9650:S3=0:S4=0
13000 '
13001 ' *** Take UVB Observation ***
13002 '
13010 CX$="20":TR$="s"+"a":REM cycle count and track mode
13020 GOSUB 6610:GOSUB 6635:M5$=UF$:GOSUB 6650
13030 TD=1500:GOSUB 7000:GOSUB 6700:GOSUB 9650
14000 '
14001 ' *** Prism Scan on External Lamp ***
14002 '
14010 PRINT CL$:SQ=1:IF FP%<>0 THEN PRINT#4,
14020 LOCATE 4,27:B$="Prism Scan on External Lamp":PRINT B$:IF FP%<>0 THEN PRINT#4,B$
14030 LOCATE 5,27:B$="---------------------------":PRINT B$:IF FP%<>0 THEN PRINT#4,B$
14040 STEP1=ZU%-10:STEP2=STEP1+19
14050 REM
14060 REM *** Scan STEP1 to STEP2 on slit SQ+1
14070 REM
14080 N%=0:L%=21:MAXINT=0:MAXWV=STEP1:DQ$=STR$(SQ+1)
14090 FOR WV=STEP1 TO STEP2
14100   N%=N%+1:GOSUB 2090:IF HF%=1 THEN PRINT "Prism scan aborted at "; TIME$: RETURN
14110   M1$=STR$(WV):GOSUB 9870:O1$="R,"+DQ$+","+DQ$+","+CX$+";O":GOSUB 9450
14120   IF VA<2 THEN VA=2:REM prevent negative values
14130   IF VA>MAXINT THEN MAXINT=VA:MAXWV=WV
14140   LOCATE 8+N%,L%: PRINT USING "#####   ########";WV;VA
14150   IF FP%<>0 THEN PRINT#4,USING "#####   ########";WV;VA
14160   IF N%=10 THEN N%=0:L%=L%+20
14170 NEXT:PRINT:IF FP%<>0 THEN PRINT#4,
14180 IF HF%=1 THEN PRINT "Prism scan aborted at ";TIME$:RETURN
15000 '
15001 ' *** Clean Up and Exit ***
15002 '
15010 PRINT "Prism scan finished at ";TIME$
15020 PRINT "Maximum lamp intensity:";MAXINT;" found at step #";MAXWV
15030 IF FP%<>0 THEN PRINT #4,"Maximum lamp intensity:";MAXINT;" found at step #";MAXWV
15040 PRINT "Press a key to continue"
15050 SF=TIMER*60:AA$=""
15060 WHILE AA$="" AND TIMER*60<SF+120
15070   AA$=INKEY$
15080 WEND:RETURN
65529 REM proper last line
