10000 REM ******************* re routine 30/10/00 **********************
10010 REM     MKII/MKIII/MKIV Reset Brewer and Motor Initialization
10020 REM
10030 REM             (SCI-TEC Instruments, October 2000)
10040 REM
10060 REM **************************************************************
10070 REM
10080 REM  This routine performs a reset on all Brewer motors, except
10090 REM  the micrometer(s).
10100 REM
10130 REM  Global Variables Used:
10140 REM    AD   BV   IO   QC   QR   SP   TA   TD
10150 REM    AZ%  CP%  IS%  TH%  UC%  ZE%
10160 REM    A1$()CL$  CP$  G$() I$   I1$  M1$ - M5$  M9$  O1$  TR$
10170 REM
10180 REM  Exits: 11010, 15030
10190 REM
10200 REM  Uses: 3220, 3300, 3400, 7000, 7050, 9270, 9300, 9450, 9500,
10210 REM        9891
10230 REM
10500 REM **************************************************************
10510 REM  History: dd/mm/yy
10520 REM  30/10/00 - Check for motors to stop moving before continuing
10530 REM  16/11/98 - Q9% support added; FW#3 zeroed at standard speed,
10540 REM  micrometers positioned correctly on new Brewer electronics
10550 REM  20/08/97 - Supports new Brewer electronics
10560 REM  25/10/95 - Resets FW#3, Iris/FW1,2 better 
10570 REM  05/01/95 - Writes AZ/ZE information to B file
10580 REM  05/10/94 - Resets everything except MIC#1, (MIC#2), FW#3
10590 REM **************************************************************
11000 DATA re
11010 IF Q12%=1 THEN RETURN:REM Don't reset in NOBREW mode operation
11020 KEY OFF
12000 '
12001 ' *** Verify Communications ***
12002 '
12010 UC%=0:TH%=0:TI=TIMER*60:FH%=0:IF CL$="" THEN GOSUB 2500
13000 '
13001 ' *** Reset The Brewer ***
13002 '
13010 PRINT CL$:FH%=FH%+1
13015 IF Q14%=0 THEN CLOSE 7:OPEN "com"+CP$+":300,n,8,1,rs,ds,cs,cd" AS 7
13020 IF Q14%=1 THEN CLOSE 7:OPEN "com"+CP$+":1200,n,8,1,rs,ds,cs,cd" AS 7
13025 ON COM(CP%) GOSUB 9300
13030 IF Q14%=1 AND INT(FH%/3)=FH%/3 THEN GOSUB 13500:GOTO 13070
13040   BV=INP(AD):OUT AD,(BV OR 64):TA=420:GOSUB 7050
13050   GOSUB 9891:TI=TIMER*60:IF TI>=TA THEN 13070
13060   LOCATE 6,SP:PRINT "Reset in";INT(.9+(TA-TI)/60);"seconds (attempt";FH%;")      ":GOTO 13050
13070 LOCATE 8,SP:PRINT "Asking Brewer to respond";:TD=1000+8100*Q14%:IF Q2%=1 THEN PRINT " (Tracker should be ON)"
13080 GOSUB 7000:BV=INP(AD):OUT AD,(BV AND 191):IF Q14%=1 THEN X=1:GOSUB 7220
13090 IS%=42:COM(CP%) ON
13100 GOSUB 9891:IF TIMER*60>TD THEN 13000:REM retry
13110 LOCATE 10,SP:PRINT INT(.9+(TD-TIMER*60)/60);"seconds  ":IF IS%=42 THEN 13100
13120 I$="":IF(LEN(I1$)>3) THEN I$=LEFT$(I1$,LEN(I1$)-3):REM startup msg
13126 PRINT CL$:LOCATE 6,SP:PRINT I$:IF Q14%=0 OR INSTR(I$,"nitializing")=0 THEN 13140
13128   X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13130   X=1:GOSUB 7220:IS%=42:COM(CP%) ON
13132   GOSUB 9891:IF TIMER*60>TD THEN 13000:REM retry
13134   LOCATE 8,SP:PRINT INT(.9+(TD-TIMER*60)/60);"seconds  ":IF IS%=42 THEN 13132
13136   I$="":IF(LEN(I1$)>3) THEN I$=LEFT$(I1$,LEN(I1$)-3):REM startup msg
13138   PRINT CL$:LOCATE 6,SP:PRINT I$:X=5:GOSUB 7220
13140 IF FP%>0 THEN PRINT#4,I$
13150 IF Q14%=1 AND INT(FH%/3)=FH%/3 THEN GOSUB 13700
13160 X=1:GOSUB 7220:X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13170 PRINT "Please wait (this may take a minute)..."
13180 GOSUB 9270:O1$="B,0":GOSUB 9450:IF Q14%=0 THEN 13190
13182 REM COM(CP%) OFF:O1$="V,960,1":GOSUB 9450
13185 REM CLOSE 7:OPEN "com"+CP$+":9600,n,8,1,rs,ds,cs,cd" AS 7
13186 PRINT "DONE iT":PRINT #4,"DONE IT"
13187 GOTO 14000
13190 O1$="L,16905,90,18041,"+DL$+",16953,110,18057,64,16977,90":GOSUB 9450
13200 O1$="L,20372,137,20373,226,20374,115,20375,248,20376,22":GOSUB 9450
13210 O1$="L,20377,169,20378,52,20379,162,20380,41,20381,137":GOSUB 9450
13220 O1$="L,20382,58,20383,154,20384,18,20385,213,20386,18":GOSUB 9450
13230 O1$="L,20387,2,20388,169,20389,233,20390,192,20391,77":GOSUB 9450
13240 O1$="L,20392,101,19810,192,19811,79,19812,148":GOSUB 9450
13250 O1$="L,19413,0,19414,255":GOSUB 9450
13260 GOSUB 3300:GOTO 14000
13500 '
13501 ' *** Get Brewer into Cosmac Mode ***
13502 '
13510 LOCATE 6,SP:PRINT "Switching to Cosmac mode (attempt";FH%;")"
13520 IF INT(FH%/2)=FH%/2 THEN 13540
13530   CLOSE 7:OPEN "com"+CP$+":9600,n,8,1,rs,ds,cs,cd" AS 7:ON COM(CP%) GOSUB 9300
13540 X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13550 IS%=42:COM(CP%) ON:PRINT#7,CHR$(1);"ANY";CHR$(28);"2 ^L";CHR$(4);:X=0.5:GOSUB 7220
13560 X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13570 IS%=42:COM(CP%) ON:PRINT#7,CHR$(1);"ANY";CHR$(28);"2 ^L";CHR$(4);:X=0.5:GOSUB 7220
13580 X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13590 IS%=42:COM(CP%) ON:PRINT#7,CHR$(1);"ANY";CHR$(15);"C ^H";CHR$(4);
13600 IF INT(FH%/2)=FH%/2 THEN 13620
13610   CLOSE 7:OPEN "com"+CP$+":1200,n,8,1,rs,ds,cs,cd" AS 7:ON COM(CP%) GOSUB 9300
13620 RETURN
13700 '
13701 ' *** Fix Brewer Clock ***
13702 '
13710 MO$=LEFT$(DATE$,2):DA$=MID$(DATE$,4,2):YE$=RIGHT$(DATE$,2)
13720 GOSUB 6200:GOSUB 7400:RETURN
14000 '
14001 ' *** Initialize Motors ***
14002 '
14010 KEY OFF:IF Q14%=1 THEN 15000
14020 PRINT CL$:LOCATE ,SP:PRINT"*** Initializing Brewer motors ***"
14030 M3$="0"+"":M4$=M3$:M5$=M3$:M9$=M3$:M1$=M3$:M2$=M3$
14040 O1$="M,3,0:I,4:I,5:B,0:R,0,7,1:R,1,1,1":GOSUB 9450:GOSUB 9750
14050 O1$="M,4,324:M,4,-324:M,5,324:M,5,-324:M,4,3:M,4,-1:M,5,3:M,5,-1":GOSUB 9450
15000 '
15001 ' *** Zeroing Zenith Motor ***
15002 '
15003 ' Note: this subroutine is taken directly from ZE.RTN
15004 '
15010 IF Q1%=0 THEN 16000:REM No zenith motor present
15020 PRINT CL$:LOCATE ,SP:PRINT"Zeroing zenith":MS%=0:A$="101":IF ZE%>ER%*2/3 THEN A$="1"
15030 IF Q14%=1 THEN FLAG=0:M1$="1":GOSUB 6550:MS%=ZE%-Y:GOTO 15310
15040 O1$="M,1,-1:M,1,"+A$+":G,800":MS%=MS%-VAL(A$)+1:GOSUB 9450:GOSUB 9190:REM advance motor and test
15050 GOSUB 2090:IF (VA AND B6%)<>B6% OR TA%=1 THEN 15800
15060 :
15070 NLOOP%=0
15080 NLOOP%=NLOOP%+1:O1$="M,1,-50:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+50
15090 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15100 IF (VA AND B6%)=B6% THEN 15080
15110 O1$="M,1,51:M,1,-1":GOSUB 9450:MS%=MS%-50
15120 :
15130 NLOOP%=0
15140 NLOOP%=NLOOP%+1:O1$="M,1,-15:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+15
15150 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15160 IF (VA AND B6%)=B6% THEN 15140
15170 O1$="M,1,16:M,1,-1":GOSUB 9450:MS%=MS%-15
15180 :
15190 NLOOP%=0
15200 NLOOP%=NLOOP%+1:O1$="M,1,-4:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+4
15210 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15220 IF (VA AND B6%)=B6% THEN 15200
15230 O1$="M,1,5:M,1,-1":GOSUB 9450:MS%=MS%-4
15240 :
15250 NLOOP%=0
15260 NLOOP%=NLOOP%+1:O1$="M,1,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%+1
15270 GOSUB 2090:IF NLOOP%>44 OR TA%=1 THEN 15800
15280 IF (VA AND B6%)=B6% THEN 15260
15290 O1$="M,1,-"+STR$(ZO%):GOSUB 9450:MS%=MS%+ZO%:PRINT CL$:LOCATE ,SP
15300 :
15310 B$="Zenith discrepancy = "+STR$(ZE%-MS%):PRINT B$
15320 IF FP%>0 THEN PRINT#4,"Zenith zeroed at ";TIME$;" ";B$
15330 GOSUB 3050
15340 TA%=0:ZE%=0:GOTO 16000
15350 :
15800 REM *** Zenith zeroing (non-critical) failure
15810 LOCATE ,SP:ZE%=ZE%-MS%
15820 IF TA%=0 THEN B$="Zenith ZEROING FAILURE !!":PRINT B$:PRINT#4,B$:GOSUB 3050
15830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Zenith zeroing aborted"
15840 X=1:GOSUB 7220:GOTO 16000
16000 '
16001 ' *** Zeroing Azimuth Motor ***
16002 '
16003 ' Note: this subroutine is taken directly from AZ.RTN
16004 '
16010 GOSUB 2300:IF Q2%=0 THEN 17000
16020 PRINT CL$:LOCATE ,SP:PRINT"Zeroing azimuth":IF Q14%=1 THEN FLAG=0:M1$="2":GOSUB 6550:MS%=AZ%-Y:GOTO 16330
16030 MS%=0:O1$="L,19413,0,19414,200":GOSUB 9400:REM Speed up motor for azimuth
16040 IF AZ%>(SR%*.8) THEN O1$="M,2,-"+STR$(INT(SR%*.2)):GOSUB 9450:MS%=(SR%*.2)
16050 :
16060 O1$="M,2,-300:G,800":GOSUB 9450:MS%=MS%+300
16070 GOSUB 2090:IF MS%>18000 OR TA%=1 THEN 16800
16080 IF (VA AND B5%)<>B5% THEN 16060
16090 :
16100 NLOOP%=0
16110 NLOOP%=NLOOP%+1:O1$="M,2,51:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-50
16120 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN 16800
16130 IF (VA AND B5%)=B5% THEN 16110
16140 O1$="M,2,-51":GOSUB 9450:MS%=MS%+51
16150 :
16160 NLOOP%=0
16170 NLOOP%=NLOOP%+1:O1$="M,2,16:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-15
16180 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN 16800
16190 IF (VA AND B5%)=B5% THEN 16170
16200 O1$="M,2,-16":GOSUB 9450:MS%=MS%+16
16210 :
16220 NLOOP%=0
16230 NLOOP%=NLOOP%+1:O1$="M,2,5:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-4
16240 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN 16800
16250 IF (VA AND B5%)=B5% THEN 16230
16260 O1$="M,2,-5":GOSUB 9450:MS%=MS%+5
16270 :
16280 NLOOP%=0
16290 NLOOP%=NLOOP%+1:O1$="M,2,2:M,2,-1:G,800":GOSUB 9450:GOSUB 9190:MS%=MS%-1
16300 GOSUB 2090:IF NLOOP%>30 OR TA%=1 THEN 16800
16310 IF (VA AND B5%)=B5% THEN 16290
16315 PRINT CL$:LOCATE ,SP
16320 :
16330 B$="Azimuth discrepancy = "+STR$(AZ%-MS%):PRINT B$:PRINT "Positioning Tracker to sun"
16340 IF FP%>0 THEN PRINT#4,"Azimuth zeroed at ";TIME$;" ";B$
16350 GOSUB 3050
16360 TA%=0:GOTO 17000
16370 :
16800 REM *** Azimuth zeroing (non-critical) failure
16810 LOCATE ,SP:AZ%=AZ%-MS%
16820 IF TA%=0 THEN B$="Azimuth tracker ZEROING FAILURE !!":HF%=1:PRINT B$:PRINT#4,B$:GOSUB 3050
16830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Azimuth zeroing aborted"
16840 X=1:GOSUB 7220:GOTO 17000
17000 '
17001 ' *** Position Micrometers (New Brewers only) ***
17002 '
17010 NLOOP%=0:IF Q14%=0 THEN 18000
17020 O1$="?MOTOR.ALLSTILL":GOSUB 9450:NLOOP%=NLOOP%+1:IF INSTR(I$,"FALSE")>0 AND NLOOP%<200 THEN 17020
17030 FLAG=0:M1$="10":GOSUB 6550:B$="Micrometer #1 discrepancy = "+STR$(Y):PRINT B$
17040 IF FP%>0 THEN PRINT#4,"Micrometer #1 zeroed at ";TIME$;" ";B$
17050 M8$="0":GOSUB 9810:M8$=STR$(1-INT(MZ%-PC*TE%+.5)):IF MDD$="n2" THEN M8$=STR$(1-INT(NMZ%-PC*TE%+.5))
17060 GOSUB 9810:M8$="-1":GOSUB 9810:M8$=MC$:GOSUB 9810:GOSUB 3050
17070 IF TYP$<>"mkiii" THEN 18000
17080 FLAG=0:M1$=N9$:GOSUB 6550:B$="Micrometer #2 discrepancy = "+STR$(Y):PRINT B$
17090 IF FP%>0 THEN PRINT#4,"Micrometer #2 zeroed at ";TIME$;" ";B$
17100 M8$="0":GOSUB 9815:M8$=STR$(1-INT(MY%-PC*TE%+.5)):GOSUB 9815:M8$="-1":GOSUB 9815
17110 M8$=MC$:GOSUB 9815:GOSUB 3050
18000 '
18001 ' *** Quit ***
18002 '
18010 IF Q14%=0 THEN O1$="L,19413,0,19414,255":GOSUB 9450:REM slow motor
18020 IF TYP$<>"mkii" AND Q9%=1 THEN GOSUB 30000
18021 IF Q14%=0 THEN 18030
18022 GOSUB 9500:TA=120:GOSUB 7120
18023 PRINT#4,"changing baudrate":PRINT "changing baudrate"
18024 COM(CP%) OFF:O1$="V,960,1":PRINT#7,O1$+CR$
18025 TA=120:GOSUB 7120
18026 CLOSE 7:OPEN "com"+CP$+":9600,n,8,1,rs,ds,cs,cd" AS 7
18027 TA=120:GOSUB 7120
18028 ON COM(CP%) GOSUB 9300
18029 PRINT#4,"Finished changing baudrate"
18030 AP%=0:GOSUB 7950:GOSUB 3220:RETURN
30000 '
30001 ' *** Zero Filterwheel #3 ***
30002 '
30010 IF Q14%=1 THEN FLAG=0:M1$="6":GOSUB 6550:GOTO 30230
30020 IF TYP$="mkiii" THEN O1$="M,10,-" + MC$: GOSUB 9450:REM Remember pos of first micrometer
30030 PRINT CL$;"Zeroing filterwheel #3":PRINT#4,"Zeroing filterwheel #3":Y=9
30040 IF TYP$="mkiv" THEN O1$="M,6,-1:M,6,10:G,800" ELSE O1$="B,3:M,10,-1:M,10,10:G,544"
30050 GOSUB 9450:GOSUB 9190
30060 YY=VA:PRINT Y
30070 IF TYP$="mkiv" THEN O1$="M,6,-25:G,800" ELSE O1$="B,3:M,10,-25:G,544"
30080 Y=Y-25:GOSUB 9450:GOSUB 9190
30090 IF Y<-600 THEN PRINT#4, "Filterwheel #3 reference not found":GOTO 31000
30100 IF(YY-VA)<>32 THEN 30060
30110 IF TYP$="mkiv" THEN O1$="M,6,25:G,800" ELSE O1$="B,3:M,10,25:G,544"
30120 Y=Y+25:GOSUB 9450:GOSUB 9190
30130 YY=VA:PRINT Y
30140 IF TYP$="mkiv" THEN O1$="M,6,-10:G,800" ELSE O1$="B,3:M,10,-10:G,544"
30150 Y=Y-10:GOSUB 9450:GOSUB 9190
30160 IF(YY-VA)<>32 THEN 30130
30170 IF TYP$="mkiv" THEN O1$="M,6,10:G,800" ELSE O1$="B,3:M,10,10:G,544"
30180 Y=Y+10:GOSUB 9450:GOSUB 9190
30190 YY=VA:PRINT Y
30200 IF TYP$="mkiv" THEN O1$="M,6,-1:G,800" ELSE O1$="B,3:M,10,-1:G,544"
30210 Y=Y-1:GOSUB 9450:GOSUB 9190
30220 IF(YY-VA)<>32 THEN 30190
30230 B$="Found reference at step "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
30240 IF Q14%=1 THEN B$="FW#3 discrepancy "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
30250 IF MDD$="o3" THEN B$=B$+STR$(MX%) ELSE B$=B$+STR$(NMX%)
30260 PRINT B$:PRINT#4,B$:GOSUB 3050:PRINT#4,
31000 '
31010 VAR7$=STR$(Y)                                     'Step for FW#3
31020 IF TYP$="mkiii" AND Q14%=0 THEN 31030 ELSE 31070
31030   VAR8$=MID$(STR$(MX%),2)                         'FW#3 Position
31040   O1$="M,10,"+VAR8$:GOSUB 9450:O1$="B,0":GOSUB 9450:O1$="M,10,-1;M,10,2;M,10,-1:M,10,"+MC$
31060   GOSUB 9450:GOTO 31200:REM Restore first micrometer pos
31070 IF MDD$="o3" THEN 31080 ELSE 31100
31080   VAR8$=MID$(STR$(MX%),2)                         'FW#3 Position
31090   O1$="M,6,"+VAR8$:GOSUB 9450:GOTO 31200
31100   VAR8$=MID$(STR$(NMX%),2)                        'FW#3 Position
31110   O1$="M,6,"+VAR8$:GOSUB 9450
31200 RETURN
65529 REM proper last line
