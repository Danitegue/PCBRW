10000 REM ****************** hp routine 23/10/98 *********************
10010 REM            MKIII Alignment of Two Spectrometers
10020 REM        
10030 REM            (SCI-TEC Instruments, October 1998)
10040 REM
10050 REM  See also:
10060 REM ************************************************************
10070 REM
10080 REM  This routine aligns the two spectrometers in a MKIII
10090 REM  Brewer.
10120 REM
10130 REM  Side Effects:
10140 REM
10150 REM  Global Variables Referenced:
10160 REM    MY% NMZ% TE% PC M5$ M8$ M9$ MC$ O1$ TYP$
10200 REM
10210 REM  Exits: 11010, 14010
10220 REM
10230 REM  Uses:  2450, 3100, 3225, 6610, 6630, 6700, 7000, 9190,
10240 REM         9450, 9480, 9500, 9670, 9805, 9815, 9820, 9840
10260 REM
10270 REM ************************************************************
10280 REM  History: dd/mm/yy
10290 REM  23/10/98 - Q9%/N9$ support added; New electronics support
10300 REM  14/07/95 - Corrected MIOAVG file output
10310 REM  12/01/95 - Update to line fitting algorithm
10320 REM  05/10/94 - Alignment sequence is HP-FR-HP-abort
10330 REM  07/07/94 - Merged all FR and MR routines into one version
10340 REM  15/11/93 - Original 3.73 version
10350 REM ************************************************************
10500 DATA hp
11000 '
11001 ' *** Initialization ***
11002 '
11010 HP%=-1:IF TYP$<>"mkiii" THEN RETURN
12000 '
12001 ' *** Setup for HP Alignment ***
12002 '
12010 ON ERROR GOTO 3100
12020 X(1)=1:Y(1)=1:ERASE X,Y:DIM X(100),Y(100)
13000 '
13001 ' *** Perform HP Alignment ***
13002 '
13010 IF M9$<>"2" THEN GOSUB 9820
13020 PRINT#4,:PRINT#4,"**** 2nd Spectrometer Alignment - HP Test ****"
13030 CX$="4":TR$="s"+"a":REM cycle count and track mode
13040 GOSUB 2450:GOSUB 3225
13050 GOSUB 6610:GOSUB 6630:M5$=MID$(STR$(POFW2),2):GOSUB 6650
13060 TD=1500:GOSUB 7000:GOSUB 6700:GOSUB 9860
13070 LOCATE ,SP:PRINT "4 - Director prism to lamps"
13080 HP%=HP%+1:IF HP%=5 THEN GOSUB 9480:B$="Spectrometer alignment trouble, resetting micrometers":PRINT#4,B$:GOSUB 3050:GOSUB 22000:GOTO 12000
13090 IF HP%=10 THEN B$="Spectrometer alignment trouble, aborting":PRINT#4,B$:GOSUB 3050:HF%=1:GOTO 14000
13100 DW=10:SQ=1:DQ$=STR$(SQ+1):GOSUB 9670
13110 GOSUB 31000:GOSUB 32000
13120 O1$="M,"+N9$+","+STR$(INT(VAL(MAX$)+.5)):GOSUB 9450
13130 M8$="-"+MC$:GOSUB 9815:M8$=MC$:GOSUB 9815:GOSUB 9500
13140 IF HF%=1 THEN PRINT "Aborted": PRINT#4, "Aborted":GOTO 14000
13150 IF C1/C9<.97 OR ABS(VAL(MAX$)-80)>10 THEN 13080
14000 '
14001 ' *** Clean Up and Exit ***
14002 '
14010 ERASE A,D,TTT,W,X,Y,ZI#:GOSUB 9840:RETURN
22000 '
22001 ' *** Perform Search and Reset Micrometer (#1) ***
22002 '
22010 B$="Reset for "+TYP$+" "+MDD$+" - Searching for micrometer #1 reference"
22020 PRINT#4,:PRINT#4,B$:PRINT B$:Y=0:VA=0
22030 IF Q14%=1 THEN FLAG=1:M1$="10":GOSUB 6550:GOTO 22210
22040 O1$="R,1,1,1:G,544":GOSUB 9450:GOSUB 9190:IF (VA AND 4)<>4 THEN 22070
22050 O1$="M,10,-500;G,544":Y=Y-500:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22060 IF Y<=-3000 THEN PRINT#4, "-3000 steps - QUIT - Micrometer #1 jammed at MIN. end ?":GOTO 23000
22070 IF (VA AND 4)=4 THEN 22050
22080 O1$="M,10,-1:M,10,501:G,544":Y=Y-1+501:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22090 IF Y>11000 THEN PRINT#4, "11000 steps - QUIT - Micrometer #1 reference not found":GOTO 23000
22100 IF (VA AND 4)=0 THEN 22080
22110 O1$="M,10,-500":GOSUB 9450
22120 Y=Y-500
22130 O1$="M,10,-1:M,10,51:G,544":Y=Y-1+51:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22140 IF (VA AND 4)=0 THEN 22130
22150 O1$="M,10,-100":GOSUB 9450
22160 Y=Y-100
22170 YY%=0
22180 YY%=YY%+1
22190 O1$="M,10,"+STR$(YY%)+":G,544":Y=Y+1:LOCATE 22,30:PRINT Y,VA:GOSUB 9450:GOSUB 9190
22200 IF (VA AND 4)=0 THEN GOTO 22180
22210 IF MDD$="o3" THEN 22220 ELSE 22300
22220    B$="Micrometer #1 reference found at step"+STR$(Y)+"(+CAL)="+STR$(INT(Y)+VAL(MC$)):IF Q14%=1 THEN B$="Micrometer #1 discrepancy="+STR$(-Y)
22230    VAR4$=STR$(INT(MZ%-PC*TE%+.5)):B$=B$+" Now set to "+MDD$+" offset ="+VAR4$
22240    PRINT#4,B$:PRINT B$
22250    IF Q14%=0 THEN M8$="-"+VAR4$:GOSUB 9810
22260    IF Q14%=1 THEN M8$="0":GOSUB 9810:M8$=STR$(1-INT(MZ%-PC*TE%+.5)):GOSUB 9810:M8$="-1":GOSUB 9810
22270    M8$=MC$:GOSUB 9810:GOSUB 3050:GOTO 22400
22300    B$="Micrometer #1 reference found at step"+STR$(Y)+"(+CAL)="+STR$(INT(Y)+VAL(MC$)):IF Q14%=1 THEN B$="Micrometer #1 discrepancy="+STR$(-Y)
22310    VAR4$=STR$(INT(NMZ%-PC*TE%+.5)):B$=B$+" Now set to "+MDD$+" offset ="+VAR4$
22320    PRINT#4,B$:PRINT B$
22330    IF Q14%=0 THEN M8$="-2000":GOSUB 9810:M8$="-"+STR$(INT(NMZ%-2000-PC*TE%+.5)):GOSUB 9810
22340    IF Q14%=1 THEN M8$="0":GOSUB 9810:M8$=STR$(1-INT(NMZ%-PC*TE%+.5)):GOSUB 9810:M8$="-1":GOSUB 9810
22350    M8$=MC$:GOSUB 9810:GOSUB 3050
22400 PRINT#4," ":VAR3$=STR$(Y+VAL(M8$)):IF Q14%=1 THEN VAR3$=STR$(Y)
23000 IF (VA AND 8)=8 THEN PRINT#4,"MICROMETER APPEARS TO BE JAMMED AT MAX. END, You must re-position by HAND"
23010 IF TYP$="mkiii" THEN GOSUB 24000
23020 IF TYP$<>"mkii" AND Q9%=1 THEN GOSUB 26000
23030 RETURN
24000 '
24001 ' *** Reset Micrometer #2 ***
24002 '
24010 B$="Searching for micrometer #2 reference":PRINT#4,B$:PRINT B$:Y=0:VA=0
24015 IF Q14%=1 THEN FLAG=1:M1$=N9$:GOSUB 6550:GOTO 24200
24020 O1$="R,1,1,1:G,800":GOSUB 9450:GOSUB 9190:IF (VA AND 32)<>0 THEN 24060
24030   O1$="M,6,-500;G,800":Y=Y-500:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24040   IF Y<-3000 THEN PRINT#4, "-3000 steps - QUIT - Micrometer #2 jammed at MIN end ?":GOTO 25000
24050   IF (VA AND 32)=0 THEN GOTO 24030
24060 O1$="M,6,-1:M,6,501:G,800":Y=Y-1+501:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24070 IF Y>11000 THEN PRINT#4, "11000 steps - QUIT - Micrometer #2 reference not found":GOTO 25000
24080 IF (VA AND 32)=32 THEN 24060
24090 O1$="M,6,-500":GOSUB 9450
24100 Y=Y-500
24110 O1$="M,6,-1:M,6,51:G,800":Y=Y-1+51:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24120 IF (VA AND 32)=32 THEN 24110
24130 O1$="M,6,-100":GOSUB 9450
24140 Y=Y-100
24150 YY%=0
24160 YY%=YY%+1
24170 O1$="M,6,"+STR$(YY%)+":G,800":Y=Y+1:LOCATE 22,30:PRINT Y,VA:GOSUB 9450:GOSUB 9190
24180 IF (VA AND 32)=32 THEN 24160
24200 B$="Micrometer #2 reference found at step"+STR$(Y):IF Q14%=1 THEN B$="Micrometer #2 discrepancy="+STR$(-Y)
24210 VAR6$=STR$(INT(MY%-PC*TE%+.5)):B$=B$+" Now set to offset ="+VAR6$
24220 PRINT#4,B$:PRINT B$
24230 IF Q14%=0 THEN M8$="-"+VAR6$:GOSUB 9815:M8$="-"+MC$:GOSUB 9815
24240 IF Q14%=1 THEN M8$="0":GOSUB 9815:M8$=STR$(1-INT(MY%-PC*TE%+.5)):GOSUB 9815:M8$="-1":GOSUB 9815
25000 M8$=MC$:GOSUB 9815:GOSUB 3050:PRINT#4," ":VAR5$=STR$(Y)
25010 RETURN
26000 '
26001 ' *** Zero Filterwheel #3 ***
26002 '
26010 IF Q14%=1 THEN FLAG=1:M1$="6":GOSUB 6550:GOTO 26200
26020 IF TYP$="mkiii" AND Q14%=0 THEN O1$="M,10,-"+MC$:GOSUB 9450:REM Remember pos of first micrometer
26030 B$="Zeroing filterwheel #3":PRINT CL$;B$:PRINT#4,B$:Y=9
26040 IF TYP$="mkiv" THEN O1$="M,6,-1:M,6,10:G,800" ELSE O1$="B,3:M,10,-1:M,10,10:G,544"
26050 GOSUB 9450:GOSUB 9190
26060 YY=VA:PRINT Y:O1$="-25":GOSUB 28000
26070 IF Y<-600 THEN PRINT#4,"Filterwheel #3 reference not found":GOTO 27000
26080 IF(YY-VA)<>32 THEN 26060
26090 O1$="25":GOSUB 28000
26100 YY=VA:PRINT Y:O1$="-10":GOSUB 28000:IF(YY-VA)<>32 THEN 26100
26110 O1$="10":GOSUB 28000
26120 YY=VA:PRINT Y:O1$="-1" :GOSUB 28000:IF(YY-VA)<>32 THEN 26120
26200 B$="Found reference at step "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
26210 IF Q14%=1 THEN B$="FW#3 discrepancy "+STR$(Y)+" Moving filterwheel #3 to "+MDD$+" position at "
26220 IF MDD$="o3" THEN B$=B$+STR$(MX%) ELSE B$=B$+STR$(NMX%)
26230 PRINT B$:PRINT#4,B$:PRINT#4,
27000 '
27010 VAR7$=STR$(Y)                 'Step for FW#3
27020 IF TYP$="mkiii" AND Q14%=0 THEN 27030 ELSE 27070
27030   VAR8$=STR$(MX%)                 'FW#3 Position
27040   O1$="M,10,"+VAR8$:GOSUB 9450:GOSUB 9840:O1$="M,10,-1;M,10,2;M,10,-1:M,10,"+MC$
27060   GOSUB 9450:GOTO 27200:REM Restore first micrometer pos
27070 IF MDD$="o3" THEN 27080 ELSE 27100
27080   VAR8$=STR$(MX%)                 'FW#3 Position
27090   O1$="M,6,"+VAR8$:GOSUB 9450:GOTO 27200
27100   VAR8$=STR$(NMX%)                'FW#3 Position
27110   O1$="M,6,"+VAR8$:GOSUB 9450
27200 RETURN
28000 Y=Y+VAL(O1$)
28010 IF TYP$="mkiv" THEN O1$="M,6,"+O1$+":G,800" ELSE O1$="B,3:M,10,"+O1$+":G,544"
28020 GOSUB 9450:GOSUB 9190:RETURN
31000 '
31001 ' *** Scan W1 to W2 Step DW on Slit SQ ***
31002 '
31005 REM M8$="-"+MC$:GOSUB 9805
31006 REM M8$=STR$(-OF%):GOSUB 9805
31010 M8$="-80":GOSUB 9815:W1=0:W2=160:MAX=0:MAX$=""
31012 N=0:FOR WV=W1 TO W2 STEP DW
31020 SN$=STR$(WV):N=N+1
31030 O1$="M,"+N9$+","+SN$+";R,"+DQ$+","+DQ$+","+CX$+";O":GOSUB 9450
31040 PRINT SN$,VA:IF VA>MAX THEN MAX=VA:MAX$=SN$
31050 X(N)=VAL(SN$):Y(N)=VA
31060 NEXT:RETURN
32000 IF MAX$="0" OR MAX$="160" THEN 32420
32010 P=4:A(1,1)=1:TTT(1)=1:D(1)=1:W(1)=1:ZI#(1,1)=1:ERASE A,TTT,D,W,ZI#
32020 DIM A(P,P),TTT(P),D(P),W(P),ZI#(P,P)
32030 FOR I=1 TO P
32040 FOR I1=1 TO N:TTT(I)=TTT(I)+X(I1)^I:NEXT
32050 TTT(I)=TTT(I)/N
32060 NEXT
32070 Y1=0
32080 FOR I1=1 TO N:Y1=Y1+Y(I1):NEXT:Y1=Y1/N
32090 FOR J=1 TO P:S1=0
32100 FOR I=1 TO N:S1=S1+(Y(I)-Y1)*(X(I)^J-TTT(J)):NEXT
32110 D(J)=S1:S1=0
32120 FOR K=J TO P:S1=0
32130 FOR I=1 TO N:S1=S1+(X(I)^J-TTT(J))*(X(I)^K-TTT(K)):NEXT
32140 A(J,K)=S1:A(K,J)=S1
32150 NEXT
32160 NEXT
32170 NT=N:N=P
32180 FOR R=1 TO N:FOR C=1 TO N:ZI#(R,C)=A(R,C):NEXT C,R
32190 FOR R=1 TO N:X#=ZI#(R,1)
32200 FOR C=1 TO N
32210 IF C<N THEN ZI#(R,C) = ZI#(R,C+1)/X# ELSE ZI#(R,C)=1/X#
32220 NEXT
32230 FOR R2=1 TO N
32240 IF R2=R THEN 32300 ELSE X#=ZI#(R2,1)
32260   FOR C=1 TO N: Y#=ZI#(R,C)*X#
32270   IF C<N THEN ZI#(R2,C)=ZI#(R2,C+1)-Y# ELSE ZI#(R2,C)=-Y#
32280   NEXT
32300 NEXT
32310 NEXT
32320 N=NT
32330 FOR I=1 TO P:TTT=0:FOR J=1 TO P:TTT=TTT+ZI#(I,J)*D(J):NEXT:W(I)=TTT:NEXT
32340 M1!=0:FOR I=1 TO P:M1!=M1!+W(I)*TTT(I):NEXT
32350 W(0)=Y1-M1!
32360 C9=0:FOR I=1 TO N:C9=C9+(Y(I)-Y1)^2:NEXT
32370 C1=0:FOR I=1 TO P:C1=C1+W(I)*D(I):NEXT
32380 MAX=0:FOR I=X(1) TO X(N) STEP .1:YMAX=W(0)
32390 FOR J=1 TO P:YMAX=YMAX+W(J)*I^J:NEXT
32400 IF MAX<YMAX THEN MAX=YMAX:XMAX=I
32410 NEXT:MAX$=STR$(INT(XMAX*100+.5)/100)
32420 PRINT #4,"Maximum intensity found at step: ";MAX$;MAX;"R^2: ";C1/C9
32430 PRINT "Maximum intensity found at step: ";MAX$;MAX;"R^2: ";C1/C9
32440 A1$(IO)=LF$+"hp"+CR$+TIME$+CR$+STR$(C1/C9)+CR$+STR$(MAX)+CR$+MAX$+CR$+STR$(TE%)
32450 IO=IO+1:GOSUB 3200
32460 RETURN
65529 REM proper last line
