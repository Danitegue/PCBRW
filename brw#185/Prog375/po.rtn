10000 REM ********************** po routine 19/11/98 *******************
10001 REM       MKII/MKIII/MKIV Print Out All Instrument Constants
10002 REM
10003 REM **************************************************************
11000 DATA po
11010 PRINT#4,CHR$(12);:REM top of page
11020 IF RM%=1 THEN 12000
11030 PRINT CL$:LOCATE 6,19:PRINT "Position paper to top of page"
11040 LOCATE 10,20:PRINT "  Press return when ready"
11050 TA=1800:IF RM%=0 THEN TA=18000
11060 GOSUB 7050
11070 A$=INKEY$:IF A$=CR$ THEN 12000
11080 TI=TIMER*60:IF TI<TA THEN 11070
12000 '
12001 ' *** Print Out Header ***
12002 '
12010 PRINT
12020 IF TYP$="mkii"  THEN PRINT#4,SPC(25);"MKII  BREWER INSTRUMENT #";NO$
12030 IF TYP$="mkiii" THEN PRINT#4,SPC(25);"MKIII BREWER INSTRUMENT #";NO$
12040 IF TYP$="mkiv"  THEN PRINT#4,SPC(25);"MKIV  BREWER INSTRUMENT #";NO$
12050 PRINT#4,SPC(25);"----------------------------"
12060 PRINT#4,SPC(31);DATE$;"  ";TIME$:PRINT#4,
13000 '
13001 ' *** O3 Values Printout ***
13002 '
13010 PRINT#4,"******************************************************************************"
13020 PRINT#4,"* Ozone Values   *    1    *    2    *    3    *    4    *    5    *    hg   *"
13030 PRINT#4,"******************************************************************************"
13040 PRINT#4,"  Wavelength(nm) *";:K=ZERO+VAL(MC$)
13050 FOR I=1 TO 6
13060   J=INT((DC(I,1)+K*DC(I,2)+K*K*DC(I,3))*100+.5)/1000
13070   IF I=6 THEN J=INT((DC(0,1)+K*DC(0,2)+K*K*DC(0,3))*100+.5)/1000
13080   PRINT#4,USING " ###.###  ";J;
13090 NEXT:PRINT#4,
13100 PRINT#4,"  Temp. Coeff    *";
13110 FOR I=2 TO 7
13120   PRINT#4,USING " ##.####  ";TC(I);
13130 NEXT:PRINT#4,
13140 PRINT#4,"  Disp. Coeff #1 *";
13150 FOR I=1 TO 5
13160   PRINT#4,USING "#####.### ";DC(I,1);
13170 NEXT:PRINT#4,USING "#####.### ";DC(0,1)
13180 PRINT#4,"  Disp. Coeff #2 *";
13190 FOR I=1 TO 5
13200   PRINT#4,USING "##.###### ";DC(I,2);
13210 NEXT:PRINT#4,USING "##.###### ";DC(0,2)
13220 PRINT#4,"  Disp. Coeff #3 *";
13230 FOR I=1 TO 5
13240   PRINT#4,USING "##.###E-6 ";DC(I,3)*1000000;
13250 NEXT:PRINT#4,USING "##.###E-6 ";DC(0,3)*1000000
14000 '
14001 ' *** NO2 Values Printout ***
14002 '
14010 IF TYP$<>"mkiv" THEN 15000
14020   PRINT#4,"******************************************************************************"
14030   PRINT#4,"* NO2 Values     *    1    *    2    *    3    *    4    *    5    *    hg   *"
14040   PRINT#4,"******************************************************************************"
14050   PRINT#4,"  Wavelength(nm) *";:K=ZERO+VAL(MC$)-SWITCH%
14060   FOR I=1 TO 6
14070     J=INT((NDC(I,1)+K*NDC(I,2)+K*K*NDC(I,3))*100+.5)/1000
14080     IF I=6 THEN J=INT((NDC(0,1)+K*NDC(0,2)+K*K*NDC(0,3))*100+.5)/1000
14090     PRINT#4,USING " ###.###  ";J;
14100   NEXT:PRINT#4,
14110   PRINT#4,"  Temp. Coeff    *";
14120   FOR I=2 TO 7
14130     PRINT#4,USING " ##.####  ";NTC(I);
14140   NEXT:PRINT#4,
14150   PRINT#4,"  Disp. Coeff #1 *";
14160   FOR I=1 TO 5
14170     PRINT#4,USING "#####.### ";NDC(I,1);
14180   NEXT:PRINT#4,USING "#####.### ";NDC(0,1)
14190   PRINT#4,"  Disp. Coeff #2 *";
14200   FOR I=1 TO 5
14210     PRINT#4,USING "##.###### ";NDC(I,2);
14220   NEXT:PRINT#4,USING "##.###### ";NDC(0,2)
14230   PRINT#4,"  Disp. Coeff #3 *";
14240   FOR I=1 TO 5
14250     PRINT#4,USING "##.###E-6 ";NDC(I,3)*1000000;
14260   NEXT:PRINT#4,USING "##.###E-6 ";NDC(0,3)*1000000
15000 '
15001 ' *** Print Out Other Constants ***
15002 '
15010 PRINT#4,"******************************************************************************"
15020 PRINT#4,
15030 PRINT#4,USING "ETC Values      :     O3 =   ######";B1;
15040 PRINT#4,USING "  ;    SO2 =   ######";B2
15050 IF TYP$="mkiv" THEN PRINT#4,USING "ETC Values      : NO2 DS =   ######";NB1;
15060 IF TYP$="mkiv" THEN PRINT#4,USING "  ; NO2 ZS =   ######";NB2
15070 PRINT#4,
15080 PRINT#4,USING "O3 Absn Coeffs  :     O3 =   #.####";A1;
15090 PRINT#4,USING "  ;    SO2 =   #.####";A3
15100 PRINT#4,USING "SO2 Absn Coeffs :     O3 =        0  ;    SO2 =   #.####";A2
15110 IF TYP$="mkiv" THEN PRINT#4,USING "NO2 Absn Coeff  :    NO2 =   ######";NA1
15120 PRINT#4,
15130 PRINT#4,USING "Micrometer steps/deg     =   ###.##";PC;
15140 PRINT#4,      "   WL cal step number       =   ";MC$
15150 PRINT#4,USING "Micrometer Zero          =   ######";ZERO;
15160 PRINT#4,      "   Umkehr offset            =   ";UO$
15170 PRINT#4,USING "Iris Open Steps          =   ######";IRIS;
15180 PRINT#4,      "   Zenith steps/rev         =   ";ER$
15190 IF Q14%=0 THEN PRINT#4,"                                      Slit mask motor delay    =   ";DL$
15200 PRINT#4,
15210 PRINT#4,USING "Micrometer 1 Offset O3   =   ######";MZ%;
15220 IF TYP$="mkii"  THEN PRINT#4,
15230 IF TYP$="mkiii" THEN PRINT#4,USING "   Micrometer 2 Offset      =   ######";MY%
15240 IF TYP$="mkiv"  THEN PRINT#4,USING "   Micrometer 1 Offset NO2  =   ######";NMZ%
15250 IF TYP$="mkiv"  THEN PRINT#4,USING "Mic 1 NO2/O3 Mode Change =   ######";SWITCH%
15260 IF TYP$="mkiii" THEN PRINT#4,USING "Grating Slope            =   ######";GS;
15270 IF TYP$="mkiii" THEN PRINT#4,USING "   Grating Intercept        =   ######";GI
15280 IF TYP$<>"mkii" THEN PRINT#4,USING "Filterwheel 3 Offset O3  =   ######";MX%;
15290 IF TYP$="mkiii" THEN PRINT#4,
15300 IF TYP$="mkiv"  THEN PRINT#4,USING "   Filterwheel 3 Offset NO2 =   ######";NMX%
15310 PRINT#4,
15320 PRINT#4,USING "Dead Time(ns)            =  ###       ";T1*1E9;
15330 IF Q14%=0 THEN PRINT#4,USING "Zenith Offset            =    #####";ZO% ELSE PRINT#4,
15340 PRINT#4,USING "Buffer Delay(s)          =  #####.#   ";DELAY;
15350 IF ZU%>999 THEN PRINT#4,USING "Zenith UVB Dome Position =    #####";ZU% ELSE PRINT#4,
15360 PRINT#4,
15370 PRINT#4,"Note:  Faster Processors May Require a Longer Buffer Delay"
15380 PRINT#4,"                (Typically 0.2 to 0.8 Seconds)"
15390 PRINT#4,
15400 YE%=VAL(YE$)
15410 PRINT#4,"Observations for ";MP$;DB$;YE$;", Julian day = " ;JD$
15420 GOSUB 7700
15430 PZ$=L3$
15440 PRINT#4,"Observations made at ";LB$;LF$;LF$
15450 LA=VAL(L1$):LO=VAL(L2$):PZ%=VAL(L3$):LC$=LO$
15460 RETURN
65529 REM dummy last line
