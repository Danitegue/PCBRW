10000 REM *************** dsp1 routine 31/03/98 *****************
10010 REM         MKII/MKIII/MKIV Dispersion Test Routine
10020 REM        
10030 REM            (SCI-TEC Instruments, March 1998)
10040 REM        
10050 REM   See Also: dsp.rtn
10060 REM *******************************************************
10070 REM
10080 REM  This routine scans the actual reference lamp lines
10090 REM  specified by DSP.  The scan results are written to
10100 REM  files that LINEFIT.EXE can use.
10110 REM
10150 REM  Global Variables Referenced:
10160 REM    AN() ND() WV()
10170 REM    SP SWV TD TI TIMER ZERO
10180 REM    CL$ DD$ H$ JD$ M8$ MC$ MDD$ NO$ O1$ TYP$ WL$ WU$ YE$
10190 REM
10200 REM  Exits: 10830, 13000
10210 REM
10220 REM  Uses:  6800, 7220, 8500, 9450, 9650, 9750, 9770, 9800,
10230 REM         9805, 9830, 9870, 9940
10240 REM
10300 REM ********************************************************
10310 REM  History: dd/mm/yy
10320 REM  31/03/98 - Added new electronics support
10330 REM  14/07/95 - Micrometer movement is split up again
10340 REM  30/11/94 - Fixed method to switch slits on a MKIV,
10350 REM  better screen formatting, shuts off HG lamp when 
10360 REM  not in use
10370 REM  14/09/94 - New method for handling slit ranges
10380 REM  05/07/94 - Handles MKII/MKIII/MKIV automatically,
10390 REM  allows up to 9 scanning wavelengths
10400 REM  01/04/94 - NO2 mode trapped and disabled
10410 REM  28/02/94 - Variable ZERO used, writes to DSP.nnn
10420 REM  instead of DSP.hst with the value of ZERO appended
10430 REM ********************************************************
10800 '
10801 ' *** Check Brewer Mode First ***
10802 '
10810 IF MDD$ = "o3" THEN 11000
10820 A$="DSP1 is not an NO2 Routine": PRINT A$:PRINT #4,A$
10830 X=1:GOSUB 7220:RETURN
11000 '
11001 ' *** Prepare for Scan ***
11002 '
11010 DATA dsp1
11020 GOSUB 9770: GOSUB 9750
11030 M8$ = "-" + MC$: GOSUB 9805: M8$ = STR$(-ZERO): GOSUB 9805
11040 SWV = 9999: IF TYP$="mkiv" THEN SWV = 3300
12000 '
12001 ' *** Begin Scan ***
12002 '
12010 FOR I = LN1 TO LN2
12020   GOSUB 22000
12030   IF AN(I)=0  AND M9$<>"1" THEN GOSUB 9830    'Turn on HG lamp if needed
12040   IF AN(I)<>0 AND M9$<>"0" THEN GOSUB 9840    'Turn off HG lamp if needed
12050   IF AN(I)=>ER%*2/3 THEN GOSUB 9785       'Set filter #1 in position 3 for UVB dome
12060   IF AN(I)< ER%*2/3 THEN GOSUB 9770       'Set filter #1 in position 1 for quartz window
12070   M5$ = STR$(ND(I) * 64): GOSUB 9800      'Set filter #2 to M5$
12080   M1$ = STR$(AN(I)): GOSUB 9870           'Set zenith to M1$
12090   IF I = LN1 AND WV(I) > 3350 THEN M8$ = "6000": GOSUB 9805
12100   CZ$ = "4": CY = 4: CLOSE 8: X = 1: IF LSN(I) > HSN(I) THEN X = -1
12110   FOR J = LSN(I) TO HSN(I) STEP X
12120     IF J = 0 THEN WL$ = STR$(0): ELSE WL$ = STR$(J + 1)
12130     WU$ = WL$: WL = J + 1: WU = J + 1
12140     REM Note that single digits will limit the number of wavelengths to nine
12150     FL$ = DD$ + NO$ + "\W" + RIGHT$(STR$(I), 1) + RIGHT$(STR$(J), 1) + JD$ + YE$
12160     FL$ = FL$ + "." + RIGHT$("###" + NO$, 3)
12170     OPEN FL$ FOR OUTPUT AS 8: PRINT #8, WV(I)
12180     SQ = J: WV = WV(I): GOSUB 8500: Y8 = M1
12190     IF Y8 < 151 THEN Y8 = 151
12200     IF WV(I) > SWV THEN O1$="M,6,50" ELSE O1$="M,6,242": REM Set based on wavelength to scan
12210     IF TYP$="mkiv" AND Q9%=1 THEN GOSUB 9450: REM Switch only on a MKIV!
12220     GOSUB 22000:LOCATE 9,32:PRINT "Scanning slit";J:LOCATE 11,9:PRINT "Step   Photon count   Step   Photon count   Step   Photon count"
12230     FOR K = Y8 - 150 TO Y8 + 150 STEP 10: REM scan forward
12240       GOSUB 20000: IF HF% = 1 THEN 13000
12250     NEXT
12260     GOSUB 22000:LOCATE 9,32:PRINT "Scanning slit";J:LOCATE 11,9:PRINT "Step   Photon count   Step   Photon count   Step   Photon count"
12270     FOR K = Y8 + 150 TO Y8 - 150 STEP -10: REM scan backward
12280       GOSUB 20000: IF HF% = 1 THEN 13000
12290     NEXT
12300     PRINT #8, "eof": CLOSE 8
12310   NEXT J
12320 NEXT I
13000 '
13001 ' *** Clean Up and Exit ***
13002 '
13010 IF HF% = 1 THEN CLOSE 8: LOCATE 24,1: PRINT "DSP scan aborted";:X = 1:GOSUB 7220
13015 IF WV(LN2) > 3350 THEN M8$ = "6000": GOSUB 9805
13020 O1$="M,6,242":IF TYP$="mkiv" AND Q9%=1 THEN GOSUB 9450: REM Switch only on a MKIV!
13030 M8$=STR$(ZERO+1): GOSUB 9805: M8$="-1": GOSUB 9805: M8$=MC$: GOSUB 9805
13040 FL$=DD$+"\"+NO$+"\DSP."+NO$:OPEN FL$ FOR OUTPUT AS 8
13050 PRINT#8, "DSPv3": PRINT#8, NO$: PRINT#8, JD$+YE$: PRINT#8, MC$
13060 PRINT#8, ZERO: PRINT#8, LN1: PRINT#8, LN2: FOR I=LN1 TO LN2: PRINT#8, LSN(I): PRINT#8, HSN(I): NEXT
13070 CLOSE 8:RETURN
20000 '
20001 ' *** Scans Through Currently Selected Line ***
20002 '
20010 O1$ = "M,10," + STR$(K)
20020 IF TYP$ = "mkiii" THEN O1$ = O1$ + SE$ + "M," + N9$ + "," + STR$(INT(K * GS + GI + .5))
20030 O1$ = O1$ + ";R," + WL$ + "," + WU$ + ",4;O": GOSUB 9450
20040 TI = TIMER * 60: TD = TI + 3000: LOCATE 4,2
20050 XI = 0: XN = 0: GOSUB 9940: A = (K - Y8) / 10 + 15
20060 LOCATE 13+A-11*INT(A/11),9+22*INT(A/11)
20070 PRINT USING "####   ##########";K;F(J+1)
20080 PRINT#8,USING "####   ##########";K;F(J+1)
20090 GOSUB 2090: RETURN
22000 '
22001 ' *** Set Up Screen Display ***
22002 '
22010 PRINT CL$
22020 LOCATE 4,28:PRINT "Line  Wavelength         Lamp"
22030 LOCATE 6,28:PRINT USING " ##    ####.##    ";I;WV(I);
22040 IF AN(I)=0 THEN PRINT     "Internal:HG lamp";:GOTO 22070
22050   IF AN(I)=>ER%*2/3 THEN PRINT "External:UVB dome";
22060   IF AN(I)< ER%*2/3 THEN PRINT "External:Quartz window";
22070 RETURN
65529 REM proper last line
