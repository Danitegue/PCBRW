10000 REM ******************* re routine 30/10/00 **********************
10010 REM 17 09 2001 JULIAN ONLY FOR NEW ELECTRONICS.
10020 REM 17 10 2007 JG Slow AZ down to avoid slipping during small movements. 16008
10030 REM 18 10 2007 JG too slow for re and az movement. Go fast for re and az and slow anywhere else
10040 REM 18 10 2007 CHANGE IS NOW ONLY in MAIN.ASC, line 2360 etc...
10590 REM **************************************************************
11000 DATA re
11010 IF Q12%=1 THEN RETURN:REM Don't reset in NOBREW mode operation
11020 KEY OFF
12000 '
12001 ' *** Verify Communications ***
12002 '
12010 UC%=0:TH%=0:TI=TIMER*60:FH%=0:IF CL$="" THEN GOSUB 2500
13000 '
13001 ' *** Reset The Brewer ***
13002 '
13010 PRINT CL$:FH%=FH%+1
13020 IF Q14%=1 THEN CLOSE 7:OPEN "com"+CP$+":1200,n,8,1,rs,ds,cs,cd" AS 7
13025 ON COM(CP%) GOSUB 9300
13030 IF Q14%=1 AND INT(FH%/3)=FH%/3 THEN GOSUB 13500:GOTO 13070
13040   BV=INP(AD):OUT AD,(BV OR 64):TA=420:GOSUB 7050
13050   GOSUB 9891:TI=TIMER*60:IF TI>=TA THEN 13070
13060   LOCATE 6,SP:PRINT "Reset in";INT(.9+(TA-TI)/60);"seconds (attempt";FH%;")      ":GOTO 13050
13070 LOCATE 8,SP:PRINT "Asking Brewer to respond";:TD=1000+8100*Q14%:IF Q2%=1 THEN PRINT " (Tracker should be ON)"
13080 GOSUB 7000:BV=INP(AD):OUT AD,(BV AND 191):IF Q14%=1 THEN X=1:GOSUB 7220
13090 IS%=42:COM(CP%) ON
13100 GOSUB 9891:IF TIMER*60>TD THEN 13000:REM retry
13110 LOCATE 10,SP:PRINT INT(.9+(TD-TIMER*60)/60);"seconds  ":IF IS%=42 THEN 13100
13120 I$="":IF(LEN(I1$)>3) THEN I$=LEFT$(I1$,LEN(I1$)-3):REM startup msg
13126 PRINT CL$:LOCATE 6,SP:PRINT I$:IF Q14%=0 OR INSTR(I$,"nitializing")=0 THEN 13140
13128   X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13130   X=1:GOSUB 7220:IS%=42:COM(CP%) ON
13132   GOSUB 9891:IF TIMER*60>TD THEN 13000:REM retry
13134   LOCATE 8,SP:PRINT INT(.9+(TD-TIMER*60)/60);"seconds  ":IF IS%=42 THEN 13132
13136   I$="":IF(LEN(I1$)>3) THEN I$=LEFT$(I1$,LEN(I1$)-3):REM startup msg
13138   PRINT CL$:LOCATE 6,SP:PRINT I$:X=5:GOSUB 7220
13140 IF FP%>0 THEN PRINT#4,I$
13150 IF Q14%=1 AND INT(FH%/3)=FH%/3 THEN GOSUB 13700
13160 X=1:GOSUB 7220:X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13170 PRINT "Please wait (this may take a minute)..."
13180 GOSUB 9270:O1$="B,0":GOSUB 9450
13186 PRINT "DONE iT":PRINT #4,"DONE IT"
13187 GOTO 14000
13500 '
13501 ' *** Get Brewer into Cosmac Mode ***
13502 '
13510 LOCATE 6,SP:PRINT "Switching to Cosmac mode (attempt";FH%;")"
13520 IF INT(FH%/2)=FH%/2 THEN 13540
13530   CLOSE 7:OPEN "com"+CP$+":9600,n,8,1,rs,ds,cs,cd" AS 7:ON COM(CP%) GOSUB 9300
13540 X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13550 IS%=42:COM(CP%) ON:PRINT#7,CHR$(1);"ANY";CHR$(28);"2 ^L";CHR$(4);:X=0.5:GOSUB 7220
13560 X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13570 IS%=42:COM(CP%) ON:PRINT#7,CHR$(1);"ANY";CHR$(28);"2 ^L";CHR$(4);:X=0.5:GOSUB 7220
13580 X=LOC(7):IF X>0 THEN A$=INPUT$(X,7):REM clear port
13590 IS%=42:COM(CP%) ON:PRINT#7,CHR$(1);"ANY";CHR$(15);"C ^H";CHR$(4);
13600 IF INT(FH%/2)=FH%/2 THEN 13620
13610   CLOSE 7:OPEN "com"+CP$+":1200,n,8,1,rs,ds,cs,cd" AS 7:ON COM(CP%) GOSUB 9300
13620 RETURN
13700 '
13701 ' *** Fix Brewer Clock ***
13702 '
13710 MO$=LEFT$(DATE$,2):DA$=MID$(DATE$,4,2):YE$=RIGHT$(DATE$,2)
13720 GOSUB 6200:GOSUB 7400:RETURN
14000 '
14001 ' *** Initialize Motors ***
14002 '
14010 KEY OFF:IF Q14%=1 THEN 15000
15000 '
15001 ' *** Zeroing Zenith Motor ***
15002 '
15003 ' Note: this subroutine is taken directly from ZE.RTN
15004 '
15010 IF Q1%=0 THEN 16000:REM No zenith motor present
15020 PRINT CL$:LOCATE ,SP:PRINT"Zeroing zenith":MS%=0:A$="101":IF ZE%>ER%*2/3 THEN A$="1"
15030 IF Q14%=1 THEN FLAG=0:M1$="1"
15040 O1$="?MOTOR.ZERO.POS["+M1$+"]":GOSUB 9450:YY%=VA:PRINT#4,"MOTOR.ZERO.POS[1]="+STR$(VA)
15050 O1$="?MOTOR.ORIGIN["+M1$+"]":GOSUB 9450:YY%=YY%-VA:PRINT#4,"MOTOR.ORIGIN[1]="+STR$(VA)
15060 O1$="?MOTOR.SLOPE["+M1$+"]":GOSUB 9450::PRINT#4,"MOTOR.SLOPE[1]="+STR$(VA):IF VA<>0 THEN YY%=INT(YY%/VA+.5)
15070 O1$="?MOTOR.DISCREPANCY["+M1$+"]":GOSUB 9450:Y=YY%-VA:PRINT#4,"MOTORDISCREPANCY[1]="+STR$(VA)
15080 MS%=ZE%-Y:GOTO 15310
15310 B$="Zenith discrepancy = "+STR$(ZE%-MS%):PRINT B$
15320 IF FP%>0 THEN PRINT#4,"Zenith zeroed at ";TIME$;" ";B$
15330 GOSUB 3050
15340 TA%=0:ZE%=0:GOTO 16000
15350 :
15800 REM *** Zenith zeroing (non-critical) failure
15810 LOCATE ,SP:ZE%=ZE%-MS%
15820 IF TA%=0 THEN B$="Zenith ZEROING FAILURE !!":PRINT B$:PRINT#4,B$:GOSUB 3050
15830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Zenith zeroing aborted"
15840 X=1:GOSUB 7220:GOTO 16000
16000 '
16001 ' *** Zeroing Azimuth Motor ***
16002 '
16003 ' Note: this subroutine is taken directly from AZ.RTN
16004 '
16010 GOSUB 2300:IF Q2%=0 THEN 17000
16020 PRINT CL$:LOCATE ,SP:PRINT"Zeroing azimuth":IF Q14%=1 THEN FLAG=0:M1$="2"
16040 O1$="?MOTOR.ZERO.POS["+M1$+"]":GOSUB 9450:YY%=VA:PRINT#4,"MOTOR.ZERO.POS[2]="+STR$(VA)
16050 O1$="?MOTOR.ORIGIN["+M1$+"]":GOSUB 9450:YY%=YY%-VA:PRINT#4,"MOTOR.ORIGIN[2]="+STR$(VA)
16060 O1$="?MOTOR.SLOPE["+M1$+"]":GOSUB 9450::PRINT#4,"MOTOR.SLOPE[2]="+STR$(VA):IF VA<>0 THEN YY%=INT(YY%/VA+.5)
16070 O1$="?MOTOR.DISCREPANCY["+M1$+"]":GOSUB 9450:Y=YY%-VA:PRINT#4,"MOTORDISCREPANCY[2]="+STR$(VA)
16080 MS%=AZ%-Y:GOTO 16330
16330 B$="Azimuth discrepancy = "+STR$(AZ%-MS%):PRINT B$:PRINT "Positioning Tracker to sun"
16340 IF FP%>0 THEN PRINT#4,"Azimuth zeroed at ";TIME$;" ";B$
16350 GOSUB 3050
16360 TA%=0:GOTO 17000
16370 :
16800 REM *** Azimuth zeroing (non-critical) failure
16810 LOCATE ,SP:AZ%=AZ%-MS%
16820 IF TA%=0 THEN B$="Azimuth tracker ZEROING FAILURE !!":HF%=1:PRINT B$:PRINT#4,B$:GOSUB 3050
16830 IF TA%=1 THEN TA%=0:PRINT "Aborted":PRINT#4, "Azimuth zeroing aborted"
16840 X=1:GOSUB 7220:GOTO 17000
17000 '
17001 ' *** Position Micrometers (New Brewers only) ***
17002 '
17005 O1$="!MOTOR.CLASS[MICROMETER.1] MICROMOTOR":GOSUB 9450
17006 O1$="!MOTOR.CLASS[MICROMETER.2] MICROMOTOR":GOSUB 9450
17010 NLOOP%=0:IF Q14%=0 THEN 18000
17020 O1$="?MOTOR.ALLSTILL":GOSUB 9450:NLOOP%=NLOOP%+1:IF INSTR(I$,"FALSE")>0 AND NLOOP%<200 THEN 17020
17030 FLAG=0:M1$="10":GOSUB 6550:B$="Micrometer #1 discrepancy = "+STR$(Y):PRINT B$
17040 IF FP%>0 AND FLAG=1 THEN PRINT#4,"Micrometer #1 zeroed at ";TIME$;" ";B$
17050 M8$="0":GOSUB 9810:M8$=STR$(1-INT(MZ%-PC*TE%+.5)):IF MDD$="n2" THEN M8$=STR$(1-INT(NMZ%-PC*TE%+.5))
17060 GOSUB 9810:M8$="-1":GOSUB 9810:M8$=MC$:GOSUB 9810:GOSUB 3050
17070 IF TYP$<>"mkiii" THEN 18000
17080 FLAG=0:M1$=N9$:GOSUB 6550:B$="Micrometer #2 discrepancy = "+STR$(Y):PRINT B$
17090 IF FP%>0 AND FLAG=1 THEN PRINT#4,"Micrometer #2 zeroed at ";TIME$;" ";B$
17100 M8$="0":GOSUB 9815:M8$=STR$(1-INT(MY%-PC*TE%+.5)):GOSUB 9815:M8$="-1":GOSUB 9815
17110 M8$=MC$:GOSUB 9815:GOSUB 3050
18000 '
18001 ' *** Quit ***
18002 '
18022 GOSUB 9500:TA=120:GOSUB 7120
18023 PRINT#4,"changing baudrate":PRINT "changing baudrate"
18024 COM(CP%) OFF:O1$="V,960,1":PRINT#7,O1$+CR$
18025 TA=120:GOSUB 7120
18026 CLOSE 7:OPEN "com"+CP$+":9600,n,8,1,rs,ds,cs,cd" AS 7
18027 TA=120:GOSUB 7120
18028 ON COM(CP%) GOSUB 9300
18029 PRINT#4,"Finished changing baudrate"
18030 AP%=0:GOSUB 7950:GOSUB 3220:RETURN
65529 REM proper last line
